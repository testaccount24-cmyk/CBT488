
       TITLE '  MIS0440B: convert dsnout file layout'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0440B.
      ******************************************************************
      *@@@ II convert dsnout file layout
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ******************************************************************
      * Date written: 08/10/99
      * Author      : Jim Aannone
      *               reformat a 121 byte file of IEHLIST output
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT DSNIN                ASSIGN TO DSNIN
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT DSNOUT               ASSIGN TO DSNOUT
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  DSNIN
           RECORDING MODE F.
       01  DSNIN-RECORD.
           05  FILLER                  PIC  X.
           05  DSNIN-DSN               PIC  X(44).
           05  FILLER                  PIC  X.
           05  DSNIN-CREATE-CYDDD      PIC  X(08).
           05  FILLER                  PIC  X.
           05  DSNIN-EXPIRE-CYDDD      PIC  X(08).
           05  FILLER                  PIC  XX.
           05  DSNIN-TYPE              PIC  X(12).
           05  FILLER                  PIC  X.
           05  DSNIN-SMS-IND           PIC  X(09).
           05  FILLER                  PIC  X.
           05  DSNIN-EXTENT            PIC  XXX.
           05  FILLER                  PIC  XX.
           05  DSNIN-VOLSER            PIC  X(06).
           05  FILLER                  PIC  XXX.
           05  DSNIN-VOL-SEQ           PIC  X(05).
           05  FILLER                  PIC  X(06).
           05  DSNIN-SECURITY          PIC  X(08).

       FD  DSNOUT
           RECORDING MODE F.
       01  DSNOUT-RECORD.
           05  FILLER                  PIC  X.
           05  FILLER                  PIC  X.
           05  DSNOUT-VOLSER           PIC  X(06).
           05  FILLER                  PIC  X.
           05  DSNOUT-DSN              PIC  X(44).
           05  FILLER                  PIC  X.
           05  DSNOUT-TYPE             PIC  XX.
           05  FILLER                  PIC  X.
           05  DSNOUT-CREATE-CYMD      PIC  X(08).
           05  FILLER                  PIC  X.
           05  DSNOUT-EXTENT           PIC  XXX.
           05  FILLER                  PIC  X.
           05  DSNOUT-SMS-IND          PIC  X(09).
           05  FILLER                  PIC  X(04).
           05  DSNOUT-VOL-SEQ          PIC  X(05).
           05  FILLER                  PIC  X(06).
           05  DSNOUT-SECURITY         PIC  X(06).
           05  FILLER                  PIC  X.
           05  DSNOUT-EXPIRE-CYMD      PIC  X(08).
           05  FILLER                  PIC  X(12).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0440B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-WRITE-RCD-CNT        PIC S9(07)    VALUE ZERO.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(05)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-DSNIN-SW         PIC  X        VALUE 'N'.
               88  END-OF-DSNIN                      VALUE 'Y'.
               88  MORE-DSNIN-RCDS                   VALUE 'N'.

       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0440B END  '.

      /
       PROCEDURE DIVISION.

       0100-PROGRAM-ENTRY.
           DISPLAY '     '.
           DISPLAY '**** MIS0440B reformat IEHLIST output'.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-DSNIN.

           DISPLAY '**** Records in DSNIN    ' WS-READ-RCD-CNT.
           DISPLAY '**** Records written     ' WS-WRITE-RCD-CNT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0440B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0440B EOJ encountered '.
           DISPLAY '     '.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  DSNIN.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0440B open error on DSNIN'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT DSNOUT.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0440B open error on DSNOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0201                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE DSNOUT.
           CLOSE DSNIN.

       0400-PROCESS-DSNIN.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0500-READ-DSNIN THRU
                       0500-READ-DSNIN-EXIT
                       UNTIL END-OF-DSNIN.

       0500-READ-DSNIN.
           READ DSNIN.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
               PERFORM 0600-SELECT THRU
                       0600-SELECT-EXIT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-DSNIN-SW
           ELSE
               DISPLAY '**** MIS0440B read error on DSNIN '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0500                     TO WS-RETURN-CODE.

       0500-READ-DSNIN-EXIT. EXIT.

       0600-SELECT.
           MOVE SPACE                         TO DSNOUT-RECORD.
           MOVE DSNIN-VOLSER                  TO DSNOUT-VOLSER.
           IF DSNIN-TYPE = 'VSAM'
               PERFORM 0700-CLEAR-SUFFIX
                   VARYING WS-SUB1 FROM 4 BY 1
                   UNTIL WS-SUB1 > 40.
           MOVE DSNIN-DSN                     TO DSNOUT-DSN.
           IF DSNIN-CREATE-CYDDD = '  00.000'
               MOVE DSNIN-CREATE-CYDDD        TO DSNOUT-CREATE-CYMD
           ELSE
               MOVE SPACE                     TO WS-STV0400B-FIELDS
               MOVE DSNIN-CREATE-CYDDD        TO STV0400B-DATA
               MOVE 'CY/DDD'                  TO STV0400B-IN
               MOVE 'CYMD  '                  TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               IF STV0400B-1X6 = 'ERROR'
                   MOVE DSNIN-CREATE-CYDDD    TO DSNOUT-CREATE-CYMD
               ELSE
                   MOVE STV0400B-1X8          TO DSNOUT-CREATE-CYMD.

           EVALUATE DSNIN-TYPE
               WHEN 'SEQUENTIAL ' MOVE 'PS'   TO DSNOUT-TYPE
               WHEN 'PARTITIONED' MOVE 'PO'   TO DSNOUT-TYPE
               WHEN 'NOT DEFINED' MOVE 'U '   TO DSNOUT-TYPE
               WHEN 'VSAM       ' MOVE 'VS'   TO DSNOUT-TYPE
               WHEN 'DIRECT ACC ' MOVE 'DA'   TO DSNOUT-TYPE
               WHEN OTHER         MOVE SPACE  TO DSNOUT-TYPE
           END-EVALUATE.

           IF DSNIN-EXPIRE-CYDDD = '  00.000'
               MOVE DSNIN-EXPIRE-CYDDD        TO DSNOUT-EXPIRE-CYMD
           ELSE
               MOVE SPACE                     TO WS-STV0400B-FIELDS
               MOVE DSNIN-EXPIRE-CYDDD        TO STV0400B-DATA
               MOVE 'CY/DDD'                  TO STV0400B-IN
               MOVE 'CYMD  '                  TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               IF STV0400B-1X6 = 'ERROR'
                   MOVE DSNIN-EXPIRE-CYDDD    TO DSNOUT-EXPIRE-CYMD
               ELSE
                   MOVE STV0400B-1X8          TO DSNOUT-EXPIRE-CYMD.
           MOVE DSNIN-SMS-IND                 TO DSNOUT-SMS-IND.
           MOVE DSNIN-EXTENT                  TO DSNOUT-EXTENT.
           MOVE DSNIN-VOL-SEQ                 TO DSNOUT-VOL-SEQ.
           MOVE DSNIN-SECURITY                TO DSNOUT-SECURITY.
           PERFORM 0800-WRITE-DSNOUT.

       0600-SELECT-EXIT. EXIT.

       0700-CLEAR-SUFFIX.
           IF DSNIN-DSN(WS-SUB1: 6) = '.DATA '
               MOVE SPACE                     TO DSNIN-DSN(WS-SUB1: )
               MOVE 40                        TO WS-SUB1
           ELSE
           IF DSNIN-DSN(WS-SUB1: 3) = '.D '
               MOVE SPACE                     TO DSNIN-DSN(WS-SUB1: )
               MOVE 40                        TO WS-SUB1.

       0800-WRITE-DSNOUT.
           WRITE DSNOUT-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0440B write error on DSNOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

           ADD 1                              TO WS-WRITE-RCD-CNT.

      ************************************ END MIS0440B
