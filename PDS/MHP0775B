
       TITLE '  MHP0775B: purge records from standard files'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MHP0775B.
      **********************************************************CX003B
      *@@@ C3 purge records with standard file lengths by date
      ****************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ****************************************************************
      * Date written: 12/09/2011
      * Author      : Jim Iannone
      *               For files with lengths 80, 81, 132, 133,
      *               100, 200, 300, 500:  purge records if older
      *               than date determined by number of day setting
      *               in PARM01
      ****************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS PAGE-TOP.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT FIL80I               ASSIGN TO FIL80I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL80O               ASSIGN TO FIL80O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL81I               ASSIGN TO FIL81I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL81O               ASSIGN TO FIL81O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL132I              ASSIGN TO FIL132I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL132O              ASSIGN TO FIL132O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL133I              ASSIGN TO FIL133I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL133O              ASSIGN TO FIL133O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL100I              ASSIGN TO FIL100I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL100O              ASSIGN TO FIL100O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL200I              ASSIGN TO FIL200I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL200O              ASSIGN TO FIL200O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL300I              ASSIGN TO FIL300I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL300O              ASSIGN TO FIL300O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL500I              ASSIGN TO FIL500I
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT FIL500O              ASSIGN TO FIL500O
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT PARM01               ASSIGN TO PARM01
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  FIL80I
           RECORDING MODE F.
       01  FIL80I-RECORD.
           05  FIL80I-RCD              PIC  X(80).

       FD  FIL80O
           RECORDING MODE F.
       01  FIL80O-RECORD.
           05  FIL80O-RCD              PIC  X(80).

       FD  FIL81I
           RECORDING MODE F.
       01  FIL81I-RECORD.
           05  FIL81I-RCD              PIC  X(81).

       FD  FIL81O
           RECORDING MODE F.
       01  FIL81O-RECORD.
           05  FIL81O-RCD              PIC  X(81).

       FD  FIL132I
           RECORDING MODE F.
       01  FIL132I-RECORD.
           05  FIL132I-RCD             PIC  X(132).

       FD  FIL132O
           RECORDING MODE F.
       01  FIL132O-RECORD.
           05  FIL132O-RCD             PIC  X(132).

       FD  FIL133I
           RECORDING MODE F.
       01  FIL133I-RECORD.
           05  FIL133I-RCD             PIC  X(133).

       FD  FIL133O
           RECORDING MODE F.
       01  FIL133O-RECORD.
           05  FIL133O-RCD             PIC  X(133).

       FD  FIL100I
           RECORDING MODE F.
       01  FIL100I-RECORD.
           05  FIL100I-RCD             PIC  X(100).

       FD  FIL100O
           RECORDING MODE F.
       01  FIL100O-RECORD.
           05  FIL100O-RCD             PIC  X(100).

       FD  FIL200I
           RECORDING MODE F.
       01  FIL200I-RECORD.
           05  FIL200I-RCD             PIC  X(200).

       FD  FIL200O
           RECORDING MODE F.
       01  FIL200O-RECORD.
           05  FIL200O-RCD             PIC  X(200).

       FD  FIL300I
           RECORDING MODE F.
       01  FIL300I-RECORD.
           05  FIL300I-RCD             PIC  X(300).

       FD  FIL300O
           RECORDING MODE F.
       01  FIL300O-RECORD.
           05  FIL300O-RCD             PIC  X(300).

       FD  FIL500I
           RECORDING MODE F.
       01  FIL500I-RECORD.
           05  FIL500I-RCD             PIC  X(500).

       FD  FIL500O
           RECORDING MODE F.
       01  FIL500O-RECORD.
           05  FIL500O-RCD             PIC  X(500).

       FD  PARM01
           RECORDING MODE F.
       01  PARM01-RECORD.
           05  PARM01-DOCU             PIC  X.
           05  FILLER                  PIC  X.
           05  PARM01-FILE-LEN         PIC  X(05).
           05  FILLER                  PIC  X.
           05  PARM01-DATE-POS         PIC  X(05).
           05  FILLER                  PIC  X.
           05  PARM01-DATE-FORMAT      PIC  X(06).
           05  FILLER                  PIC  X.
           05  PARM01-PURGE-DAYS       PIC  X(05).
           05  FILLER                  PIC  X(46).
           05  FILLER                  PIC  X(08).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MHP0775B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-PARM01-MAX           PIC  9(05)    VALUE ZERO.
           05  WS-END-POS              PIC  9(05)    VALUE ZERO.
           05  WS-TALLY                PIC S9(04)    VALUE ZERO COMP.
           05  WS-LS-DATA              PIC  X(10)    VALUE SPACE.
           05  FILLER REDEFINES WS-LS-DATA.
               10  WS-LS-CYMD          PIC  X(08).
               10  WS-LS-EXTRA         PIC  XX.
           05  WS-1-CYMD               PIC  X(08)    VALUE SPACE.
           05  WS-2-CYMD               PIC  X(08)    VALUE SPACE.
           05  WS-FILE-CYMD            PIC  9(08)    VALUE ZERO.
           05  WS-PARM-CYMD            PIC  9(08)    VALUE ZERO.
           05  WS-PARM-DAYS            PIC  X(05)    VALUE SPACE.
           05  WS-OLDEST-CYMD          PIC  9(08)    VALUE ZERO.
           05  WS-CALL-STV0270B        PIC  X(08)    VALUE 'STV0270B'.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.
           05  WS-CALL-STV0460B        PIC  X(08)    VALUE 'STV0460B'.
           05  WS-HOLD-STRING          PIC  X(10)    VALUE SPACE.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(04)    VALUE ZERO.
           05  WS-SUB2                 PIC S9(04)    VALUE ZERO.

       01  WS-TABLES.
           05  WS-PARM01-TABLE         PIC  X(352)   VALUE SPACE.
           05  WS-PARM01-ITEM REDEFINES WS-PARM01-TABLE
                                       OCCURS 08 TIMES.
               10  WS-FLEN             PIC  9(05).
               10  WS-POS              PIC  9(05).
               10  WS-FMT              PIC  X(06).
               10  WS-DAYS             PIC  9(05).
               10  WS-SLEN             PIC  99.
               10  WS-READ-CNT         PIC  9(07).
               10  WS-DEL-CNT          PIC  9(07).
               10  WS-WRITE-CNT        PIC  9(07).

       01  WS-SWITCHES.
           05  WS-END-FIL80I-SW        PIC  X        VALUE 'N'.
               88  END-OF-FIL80I                     VALUE 'Y'.
           05  WS-END-FIL81I-SW        PIC  X        VALUE 'N'.
               88  END-OF-FIL81I                     VALUE 'Y'.
           05  WS-END-FIL132I-SW       PIC  X        VALUE 'N'.
               88  END-OF-FIL132I                    VALUE 'Y'.
           05  WS-END-FIL133I-SW       PIC  X        VALUE 'N'.
               88  END-OF-FIL133I                    VALUE 'Y'.
           05  WS-END-FIL100I-SW       PIC  X        VALUE 'N'.
               88  END-OF-FIL100I                    VALUE 'Y'.
           05  WS-END-FIL200I-SW       PIC  X        VALUE 'N'.
               88  END-OF-FIL200I                    VALUE 'Y'.
           05  WS-END-FIL300I-SW       PIC  X        VALUE 'N'.
               88  END-OF-FIL300I                    VALUE 'Y'.
           05  WS-END-FIL500I-SW       PIC  X        VALUE 'N'.
               88  END-OF-FIL500I                    VALUE 'Y'.

      *    YEAR 2000 COMPATIBLE DATE ROUTINE
       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)    VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)    VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)    VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)    VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MHP0775B END  '.

       LINKAGE SECTION.

       01  LS-FIELD.
           05  LS-LENGTH               PIC S9(04)    COMP.
           05  LS-DATA                 PIC  X(10).
           05  FILLER REDEFINES LS-DATA.
               10  LS-CYMD             PIC  X(08).
               10  LS-EXTRA            PIC  XX.

      /

       PROCEDURE DIVISION USING LS-FIELD.

       0100-PROGRAM-ENTRY.

           PERFORM 0200-OPEN-FILES.
           DISPLAY '    '.
           DISPLAY '**** MHP0775B purge file records'.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0500-GET-LINKAGE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-GET-PARM-CYMD.

           PERFORM VARYING WS-SUB1 FROM 1 BY 1
               UNTIL WS-SUB1 > 8
               MOVE ZERO                      TO WS-FLEN(WS-SUB1)
               MOVE ZERO                      TO WS-POS(WS-SUB1)
               MOVE SPACE                     TO WS-FMT(WS-SUB1)
               MOVE ZERO                      TO WS-DAYS(WS-SUB1)
               MOVE ZERO                      TO WS-SLEN(WS-SUB1)
               MOVE ZERO                      TO WS-READ-CNT(WS-SUB1)
               MOVE ZERO                      TO WS-DEL-CNT(WS-SUB1)
               MOVE ZERO                      TO WS-WRITE-CNT(WS-SUB1)
           END-PERFORM.
           IF WS-RETURN-CODE = ZERO
               MOVE ZERO                      TO WS-SUB1
               PERFORM 0400-LOAD-PARM01 THRU
                       0400-LOAD-PARM01-EXIT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0700-PROCESS-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MHP0775B cond code ' WS-RETURN-CODE
                       ' set by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MHP0775B EOJ encountered '.
           DISPLAY '    '.
           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  PARM01.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on PARM01'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE PARM01.

      *---------------------------------------
      *    LOAD PARM01
      *---------------------------------------
       0400-LOAD-PARM01.
           READ PARM01.
           IF WS-STAT = 10
               MOVE WS-SUB1                   TO WS-PARM01-MAX
               GO TO 0400-LOAD-PARM01-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MHP0775B read error on PARM01'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0400                     TO WS-RETURN-CODE
               GO TO 0400-LOAD-PARM01-EXIT.

           IF PARM01-DOCU = '*'
               GO TO 0400-LOAD-PARM01.
           IF WS-SUB1 < 8
               ADD 1                          TO WS-SUB1
               PERFORM 0400-PARM01-FIELDS.

           GO TO 0400-LOAD-PARM01.

       0400-LOAD-PARM01-EXIT. EXIT.

       0400-PARM01-FIELDS.
           IF PARM01-FILE-LEN NUMERIC
               IF PARM01-FILE-LEN = '00080' OR '00081' OR '00132'
                                 OR '00133' OR '00100' OR '00200'
                                 OR '00300' OR '00500'
                   MOVE PARM01-FILE-LEN       TO WS-FLEN(WS-SUB1)
               ELSE
                   DISPLAY 'MHP0775B parm01 file len not expected'
                   MOVE +0400                 TO WS-RETURN-CODE
           ELSE
               DISPLAY 'MHP0775B parm01 file len not numeric'
               MOVE +0400                     TO WS-RETURN-CODE.

           IF PARM01-DATE-POS NUMERIC
               IF PARM01-DATE-POS = '00000'
                   DISPLAY 'MHP0775B parm01 date position = zero'
                   MOVE +0400                 TO WS-RETURN-CODE
               ELSE
                   MOVE PARM01-DATE-POS       TO WS-POS(WS-SUB1)
           ELSE
               DISPLAY 'MHP0775B parm01 date pos not numeric'
               MOVE +0400                     TO WS-RETURN-CODE.

           MOVE PARM01-DATE-FORMAT            TO WS-FMT(WS-SUB1).
           IF PARM01-DATE-FORMAT = 'MDY   '
               MOVE 6                         TO WS-SLEN(WS-SUB1)
           ELSE
           IF PARM01-DATE-FORMAT = 'CYMD  ' OR 'M/D/Y ' OR 'MDCY  '
               MOVE 8                         TO WS-SLEN(WS-SUB1)
           ELSE
           IF PARM01-DATE-FORMAT = 'M/D/CY'
               MOVE 10                        TO WS-SLEN(WS-SUB1)
           ELSE
               DISPLAY 'MHP0775B unable to determine date string '
               DISPLAY '         length for given date format'
               MOVE +0400                     TO WS-RETURN-CODE.

           IF PARM01-PURGE-DAYS NUMERIC
               MOVE PARM01-PURGE-DAYS         TO WS-DAYS(WS-SUB1)
           ELSE
               DISPLAY 'MHP0775B parm01 days not numeric'
               MOVE +0400                     TO WS-RETURN-CODE.

           MOVE ZERO                          TO WS-END-POS.
           IF WS-RETURN-CODE = ZERO
               COMPUTE WS-END-POS = WS-POS(WS-SUB1) + WS-SLEN(WS-SUB1)
               IF WS-END-POS > WS-FLEN(WS-SUB1)
                   DISPLAY '**** Date position > record length'
                   MOVE +0400                 TO WS-RETURN-CODE.

       0500-GET-LINKAGE.
           IF LS-LENGTH = ZERO
               DISPLAY '**** Parm length is zero '
               MOVE +0500                     TO WS-RETURN-CODE
           ELSE
           IF LS-LENGTH > LENGTH OF WS-LS-DATA
               DISPLAY '**** Parm length is excessive '
               MOVE +0500                     TO WS-RETURN-CODE
           ELSE
               MOVE LS-DATA(1: LS-LENGTH)     TO WS-LS-DATA.

           MOVE WS-LS-CYMD                    TO WS-2-CYMD.

       0600-GET-PARM-CYMD.
      *---------------------------------------
      *    GET CURRENT DATE
      *---------------------------------------
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-CYMD.
      *---------------------------------------
      *    GET PARM DATE
      *---------------------------------------
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           IF WS-2-CYMD = '00000000'
               MOVE SPACE                     TO WS-2-CYMD
           ELSE
           IF WS-2-CYMD(3: 1) = '/'
               AND WS-2-CYMD(6: 1) = '/'
               MOVE WS-2-CYMD                 TO STV0400B-1X8
               MOVE 'M/D/Y '                  TO STV0400B-IN
               MOVE 'CYMD  '                  TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               MOVE STV0400B-1X8              TO WS-2-CYMD
           ELSE
           IF WS-2-CYMD(1: 3) = 'CMD' OR 'CMF'
               MOVE WS-2-CYMD                 TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               MOVE STV0400B-1X8              TO WS-2-CYMD.

           IF WS-2-CYMD NOT NUMERIC
               MOVE WS-1-CYMD                 TO WS-2-CYMD.

           MOVE WS-2-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CYMD  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR'
               DISPLAY '**** MHP0775B parm date is not valid'
               DISPLAY '**** Date = ' WS-2-CYMD
               MOVE +0600                     TO WS-RETURN-CODE.

      *---------------------------------------
      *  loop through the parm table and process
      *  files listed there (identified by file length).
      *  of course the jcl should match all parm01 entries.
      *---------------------------------------
       0700-PROCESS-FILES.
           PERFORM 0800-FILE-LOOP
               VARYING WS-SUB1 FROM 1 BY 1
               UNTIL WS-SUB1 > WS-PARM01-MAX.

       0800-FILE-LOOP.
           MOVE 99999999                      TO WS-OLDEST-CYMD.
           PERFORM 0900-GET-PARM-DAYS.
           IF WS-FLEN(WS-SUB1) < 100
               DISPLAY '**** Purge file FIL' WS-FLEN(WS-SUB1)(4: 2)
                       'I rcds if <  ' WS-PARM-CYMD
                       '  :  ' WS-PARM-DAYS ' days'
           ELSE
           IF WS-FLEN(WS-SUB1) < 1000
               DISPLAY '**** Purge file FIL' WS-FLEN(WS-SUB1)(3: 3)
                       'I rcds if < ' WS-PARM-CYMD
                       '  :  ' WS-PARM-DAYS ' days'
           ELSE
           IF WS-FLEN(WS-SUB1) < 10000
               DISPLAY '**** Purge file FIL' WS-FLEN(WS-SUB1)(2: 4)
                       'I rcds if < ' WS-PARM-CYMD
                       ' :  ' WS-PARM-DAYS ' days'
           ELSE
               DISPLAY '**** Purge file ' WS-FLEN(WS-SUB1)
                       ' rcds if < ' WS-PARM-CYMD
                       ' : ' WS-PARM-DAYS ' days'.

           IF WS-FLEN(WS-SUB1) = 80
               PERFORM 0800-FIL80-LOOP
           ELSE
           IF WS-FLEN(WS-SUB1) = 81
               PERFORM 0800-FIL81-LOOP
           ELSE
           IF WS-FLEN(WS-SUB1) = 132
               PERFORM 0800-FIL132-LOOP
           ELSE
           IF WS-FLEN(WS-SUB1) = 133
               PERFORM 0800-FIL133-LOOP
           ELSE
           IF WS-FLEN(WS-SUB1) = 100
               PERFORM 0800-FIL100-LOOP
           ELSE
           IF WS-FLEN(WS-SUB1) = 200
               PERFORM 0800-FIL200-LOOP
           ELSE
           IF WS-FLEN(WS-SUB1) = 300
               PERFORM 0800-FIL300-LOOP
           ELSE
           IF WS-FLEN(WS-SUB1) = 500
               PERFORM 0800-FIL500-LOOP.

      *---------------------------------------

       0800-FIL80-LOOP.
           OPEN INPUT  FIL80I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL80I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL80O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL80O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL80I
                   UNTIL END-OF-FIL80I.
           CLOSE FIL80O.
           CLOSE FIL80I.
           DISPLAY '**** FIL80I rcds read    ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL80I purged       ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL80I written      ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL80I.
           READ FIL80I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL80-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL80I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL80I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL80-SELECT.
           MOVE FIL80I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL80I-RCD                TO FIL80O-RCD
               PERFORM 0800-WRITE-FIL80O.

       0800-WRITE-FIL80O.
           WRITE FIL80O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL80O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0800-FIL81-LOOP.
           OPEN INPUT  FIL81I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL81I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL81O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL81O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL81I
                   UNTIL END-OF-FIL81I.
           CLOSE FIL81O.
           CLOSE FIL81I.
           DISPLAY '**** FIL81I rcds read    ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL81I purged       ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL81I written      ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL81I.
           READ FIL81I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL81-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL81I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL81I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL81-SELECT.
           MOVE FIL81I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL81I-RCD                TO FIL81O-RCD
               PERFORM 0800-WRITE-FIL81O.

       0800-WRITE-FIL81O.
           WRITE FIL81O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL81O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0800-FIL132-LOOP.
           OPEN INPUT  FIL132I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL132I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL132O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL132O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL132I
                   UNTIL END-OF-FIL132I.
           CLOSE FIL132O.
           CLOSE FIL132I.
           DISPLAY '**** FIL132I rcds read   ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL132I purged      ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL132I written     ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL132I.
           READ FIL132I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL132-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL132I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL132I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL132-SELECT.
           MOVE FIL132I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL132I-RCD               TO FIL132O-RCD
               PERFORM 0800-WRITE-FIL132O.

       0800-WRITE-FIL132O.
           WRITE FIL132O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL132O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0800-FIL133-LOOP.
           OPEN INPUT  FIL133I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL133I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL133O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL133O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL133I
                   UNTIL END-OF-FIL133I.
           CLOSE FIL133O.
           CLOSE FIL133I.
           DISPLAY '**** FIL133I rcds read   ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL133I purged      ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL133I written     ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL133I.
           READ FIL133I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL133-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL133I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL133I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL133-SELECT.
           MOVE FIL133I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL133I-RCD               TO FIL133O-RCD
               PERFORM 0800-WRITE-FIL133O.

       0800-WRITE-FIL133O.
           WRITE FIL133O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL133O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0800-FIL100-LOOP.
           OPEN INPUT  FIL100I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL100I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL100O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL100O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL100I
                   UNTIL END-OF-FIL100I.
           CLOSE FIL100O.
           CLOSE FIL100I.
           DISPLAY '**** FIL100I rcds read   ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL100I purged      ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL100I written     ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL100I.
           READ FIL100I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL100-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL100I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL100I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL100-SELECT.
           MOVE FIL100I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL100I-RCD               TO FIL100O-RCD
               PERFORM 0800-WRITE-FIL100O.

       0800-WRITE-FIL100O.
           WRITE FIL100O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL100O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0800-FIL200-LOOP.
           OPEN INPUT  FIL200I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL200I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL200O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL200O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL200I
                   UNTIL END-OF-FIL200I.
           CLOSE FIL200O.
           CLOSE FIL200I.
           DISPLAY '**** FIL200I rcds read   ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL200I purged      ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL200I written     ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL200I.
           READ FIL200I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL200-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL200I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL200I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL200-SELECT.
           MOVE FIL200I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL200I-RCD               TO FIL200O-RCD
               PERFORM 0800-WRITE-FIL200O.

       0800-WRITE-FIL200O.
           WRITE FIL200O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL200O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0800-FIL300-LOOP.
           OPEN INPUT  FIL300I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL300I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL300O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL300O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL300I
                   UNTIL END-OF-FIL300I.
           CLOSE FIL300O.
           CLOSE FIL300I.
           DISPLAY '**** FIL300I rcds read   ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL300I purged      ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL300I written     ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL300I.
           READ FIL300I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL300-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL300I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL300I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL300-SELECT.
           MOVE FIL300I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL300I-RCD               TO FIL300O-RCD
               PERFORM 0800-WRITE-FIL300O.

       0800-WRITE-FIL300O.
           WRITE FIL300O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL300O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0800-FIL500-LOOP.
           OPEN INPUT  FIL500I.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL500I'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           OPEN OUTPUT FIL500O.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MHP0775B open error on FIL500O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0800-READ-FIL500I
                   UNTIL END-OF-FIL500I.
           CLOSE FIL500O.
           CLOSE FIL500I.
           DISPLAY '**** FIL500I rcds read   ' WS-READ-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) > ZERO
               DISPLAY '**** FIL500I purged      ' WS-DEL-CNT(WS-SUB1).
           DISPLAY '**** FIL500I written     ' WS-WRITE-CNT(WS-SUB1).
           IF WS-DEL-CNT(WS-SUB1) = ZERO
               PERFORM 0900-SHOW-OLDEST.

       0800-READ-FIL500I.
           READ FIL500I.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-CNT(WS-SUB1)
               PERFORM 0800-FIL500-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-FIL500I-SW
           ELSE
               DISPLAY '**** MHP0775B read error on FIL500I '
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-FIL500-SELECT.
           MOVE FIL500I-RCD(WS-POS(WS-SUB1): WS-SLEN(WS-SUB1))
                                              TO WS-HOLD-STRING.
           PERFORM 0900-CONVERT-FILE-DATE.
           IF WS-FILE-CYMD < WS-PARM-CYMD
               ADD 1                          TO WS-DEL-CNT(WS-SUB1)
           ELSE
               MOVE FIL500I-RCD               TO FIL500O-RCD
               PERFORM 0800-WRITE-FIL500O.

       0800-WRITE-FIL500O.
           WRITE FIL500O-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-WRITE-CNT(WS-SUB1)
           ELSE
               DISPLAY '**** MHP0775B write error on FIL500O'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

      *---------------------------------------

       0900-GET-PARM-DAYS.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-2-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE '-00000'                      TO STV0400B-OUT.
           MOVE WS-DAYS(WS-SUB1)              TO STV0400B-OUT(2: 5)
                                                 WS-PARM-DAYS.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR'
               DISPLAY '**** MHP0775B error converting parm01 days'
               MOVE +0900                     TO WS-RETURN-CODE
           ELSE
               MOVE STV0400B-1X8              TO WS-PARM-CYMD.

       0900-CONVERT-FILE-DATE.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-HOLD-STRING                TO STV0400B-DATA.
           MOVE WS-FMT(WS-SUB1)               TO STV0400B-IN.
           MOVE 'CYMD  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR'
               MOVE 99999999                  TO WS-FILE-CYMD
           ELSE
               MOVE STV0400B-1X8              TO WS-FILE-CYMD
               IF WS-FILE-CYMD < WS-OLDEST-CYMD
                   MOVE WS-FILE-CYMD          TO WS-OLDEST-CYMD.

       0900-SHOW-OLDEST.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-OLDEST-CYMD                TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'DAYS  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 NOT = 'ERROR'
               DISPLAY '**** Oldest date days             '
                        WS-OLDEST-CYMD
                       '  :  ' STV0400B-DAYS ' days'.

      ************************************ END MHP0775B
