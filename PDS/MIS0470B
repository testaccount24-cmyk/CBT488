
       TITLE '  MIS0470B: condense HSM activity files'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0470B.
      ******************************************************************
      *@@@ II list HSM activity files
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      *****************************************************************
      * Date written: 06/24/96
      * Author      : Jim Iannone
      *               loop through HSM activity logs and condense the
      *               data into a readable format.  also generate
      *               dataset lists for further processing.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT HSMIN                ASSIGN TO HSMIN
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT HSMLIST              ASSIGN TO HSMLIST
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT DSNOUT               ASSIGN TO DSNOUT
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  HSMIN
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  HSMIN-RECORD.
           05  FILLER                  PIC  X(121).

       FD  HSMLIST
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  HSMLIST-RECORD.
           05  FILLER                  PIC  X(132).

       FD  DSNOUT
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  DSNOUT-RECORD.
           05  FILLER                  PIC  X(80).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0470B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-LOW-VAL              PIC  X        VALUE X'00'.
           05  WS-LOW-VAL2             PIC  X        VALUE X'48'.
           05  WS-DUMP-TYPE            PIC  X(08)    VALUE SPACE.
           05  WS-1-YMD8               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-1-YMD8.
               10  WS-1-YMD8-YY        PIC  XX.
               10  FILLER              PIC  X.
               10  WS-1-YMD8-MM        PIC  XX.
               10  FILLER              PIC  X.
               10  WS-1-YMD8-DD        PIC  XX.
           05  WS-PART1                PIC  X(55)    VALUE SPACE.
           05  WS-PART2                PIC  X(50)    VALUE SPACE.
           05  WS-LINE                 PIC  X(121)   VALUE SPACE.

       01  WS-HSMLIST-RCD.
           05  HSMLIST-RCD             PIC  X(132)   VALUE SPACE.
           05  HSMLIST-RCD1 REDEFINES HSMLIST-RCD.
               15  HSMLIST-YY          PIC  XX.
               15  HSMLIST-MM          PIC  XX.
               15  HSMLIST-DD          PIC  XX.
               15  FILLER              PIC  X.
               15  HSMLIST-TIME        PIC  X(04).
               15  FILLER              PIC  X.
               15  HSMLIST-ARC         PIC  X(08).
               15  FILLER              PIC  X.
               15  HSMLIST-ACTION      PIC  X(08).
               15  FILLER              PIC  X.
               15  HSMLIST-FROM        PIC  X(06).
               15  FILLER              PIC  X.
               15  HSMLIST-TO          PIC  X(06).
               15  FILLER              PIC  X.
               15  HSMLIST-RC          PIC  X(04).
               15  FILLER              PIC  X.
               15  HSMLIST-REASON      PIC  X(05).
               15  FILLER              PIC  X.
               15  HSMLIST-DSN         PIC  X(44).
               15  FILLER              PIC  X.
               15  HSMLIST-AGE         PIC  X(04).
               15  FILLER              PIC  X.
               15  HSMLIST-TRACKS      PIC  X(06).
               15  FILLER              PIC  X(20).
               15  HSMLIST-TYPE        PIC  X.
           05  HSMLIST-RCD2 REDEFINES HSMLIST-RCD.
               15  FILLER              PIC  X(21).
               15  HSMLIST-NOTE        PIC  X(110).
               15  FILLER              PIC  X.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-HSMIN-RCD-CNT        PIC S9(07)    VALUE ZERO.
           05  WS-SKIP-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-HSM-RCD-CNT          PIC S9(07)    VALUE ZERO.
           05  WS-DSN-RCD-CNT          PIC S9(07)    VALUE ZERO.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(05)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-HSMIN-SW         PIC  X        VALUE 'N'.
               88  END-OF-HSMIN                      VALUE 'Y'.
               88  MORE-HSMIN-RCDS                   VALUE 'N'.
           05  WS-END-HSMLIST-SW       PIC  X        VALUE 'N'.
               88  END-OF-HSMLIST                    VALUE 'Y'.
               88  MORE-HSMLIST-RCDS                 VALUE 'N'.
           05  WS-NEXT-SW              PIC  X        VALUE SPACE.
           05  WS-FIRST-RCD-SW         PIC  X        VALUE 'Y'.
               88  FIRST-RECORD                      VALUE 'Y'.
           05  WS-TRACK-SW             PIC  X        VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0470B END  '.

      /
       PROCEDURE DIVISION.

       0100-PROGRAM-ENTRY.
           DISPLAY '      '.
           DISPLAY '**** MIS0470B list HSM activity files '.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-HSMIN.

           ADD WS-HSMIN-RCD-CNT TO WS-SKIP-RCD-CNT.
           DISPLAY '**** HSM in                   ' WS-HSM-RCD-CNT.
           DISPLAY '**** HSMLIST records out      ' WS-HSMIN-RCD-CNT.
           DISPLAY '**** HSMLIST rcds + skipped   ' WS-SKIP-RCD-CNT.
           DISPLAY '**** DSNOUT records out       ' WS-DSN-RCD-CNT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0470B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0470B EOJ encountered '.
           DISPLAY '      '.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  HSMIN.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0470B open error on HSMIN'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT HSMLIST.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0470B open error on HSMLIST'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT DSNOUT.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0470B open error on DSNOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE DSNOUT.
           CLOSE HSMLIST.
           CLOSE HSMIN.

       0400-PROCESS-HSMIN.
           PERFORM 0700-READ-HSMIN
               UNTIL END-OF-HSMIN.

       0700-READ-HSMIN.
           READ HSMIN.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-HSM-RCD-CNT
               PERFORM 0800-SELECT THRU
                       0800-SELECT-EXIT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-HSMIN-SW
           ELSE
               DISPLAY '**** MIS0470B read error on HSMIN '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0700                     TO WS-RETURN-CODE.

       0800-SELECT.
           MOVE HSMIN-RECORD                  TO WS-LINE.
           MOVE SPACE                         TO WS-TRACK-SW.
           IF WS-NEXT-SW = 'Y'
               PERFORM 0850-WS-NEXT-SW
               IF WS-NEXT-SW = 'Y'
                   ADD 1                      TO WS-SKIP-RCD-CNT
                   MOVE 'N'                   TO WS-NEXT-SW
                   GO TO 0800-SELECT-EXIT.
           MOVE 'N'                           TO WS-NEXT-SW.
           MOVE SPACE                         TO HSMLIST-NOTE.
           MOVE WS-LINE(2: 8)                 TO HSMLIST-ARC.
      ****************************************
      *    BACKUPS
      *+**************************************
      *    DFHSM backup log
           IF WS-LINE(3: 5) = 'DFSMS'
               AND WS-LINE(12: 10) = 'BACKUP LOG'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(29: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(32: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(45: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               ADD 1                          TO WS-SKIP-RCD-CNT
               GO TO 0800-SELECT-EXIT.
      *    automatic backup starting/ending
           IF WS-LINE(2: 8) = 'ARC0720I' OR 'ARC0721I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    OCDS successfully backed up to HSM.OCDS.BACKUP.D0000431
           IF WS-LINE(2: 8) = 'ARC0743I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: 91)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    ARC0748i last successful CDS backup-set qualifier is D0000431
           IF WS-LINE(2: 8) = 'ARC0748I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    backup of a data set of 368 tracks to ML1 volume failed
           IF WS-LINE(2: 8) = 'ARC0757I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    CDS backup starting at 22:01:02 on 96/11/13
           IF WS-LINE(2: 8) = 'ARC0740I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(34: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(37: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(48: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 20)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    CDS backup ending
           IF WS-LINE(2: 8) = 'ARC0741I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(32: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(35: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(46: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 17)           TO WS-PART1
               MOVE WS-LINE(52: 20)           TO WS-PART2
               STRING WS-PART1      DELIMITED BY '  '
                      WS-PART2      DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    backup for MCDS starting
           IF WS-LINE(2: 8) = 'ARC0742I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(39: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(42: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(53: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 24)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    backup for jrnl starting
           IF WS-LINE(2: 8) = 'ARC0750I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(39: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(42: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(54: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 25)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    expire backup versions starting
           IF WS-LINE(2: 8) = 'ARC0680I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(46: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(49: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(60: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 31)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    expire backup versions ending
           IF WS-LINE(2: 8) = 'ARC0681I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(44: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(47: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(58: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 30)           TO WS-PART1
               MOVE WS-LINE(66: 33)           TO WS-PART2
               STRING WS-PART1      DELIMITED BY '  '
                      ', '          DELIMITED BY SIZE
                      WS-PART2      DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               INSPECT HSMLIST-NOTE REPLACING ALL ',    ' BY '     '
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    movement of backup versions starting
           IF WS-LINE(2: 8) = 'ARC0718I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(51: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(54: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(65: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 36)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    movement of backup versions ending
           IF WS-LINE(2: 8) = 'ARC0719I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(49: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(52: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(11: 34)           TO WS-PART1
               MOVE WS-LINE(59: 26)           TO WS-PART2
               STRING WS-PART1      DELIMITED BY '  '
                      ', '          DELIMITED BY SIZE
                      WS-PART2      DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    backup of migrated data sets starting
           IF WS-LINE(2: 8) = 'ARC0735I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(52: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(55: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(66: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 37)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    backup of migrated data sets ending
           IF WS-LINE(2: 8) = 'ARC0736I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(50: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(53: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(11: 35)           TO WS-PART1
               MOVE WS-LINE(60: 24)           TO WS-PART2
               STRING WS-PART1      DELIMITED BY '  '
                      ', '          DELIMITED BY SIZE
                      WS-PART2      DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    backup starting on volume svmc04
           IF WS-LINE(2: 8) = 'ARC0722I'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(52: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(55: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(66: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 37)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    backup ending on volume svmc01
           IF WS-LINE(2: 8) = 'ARC0723I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(45: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(48: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(11: 30)           TO WS-PART1
               MOVE WS-LINE(55: 24)           TO WS-PART2
               STRING WS-PART1      DELIMITED BY '  '
                      ', '          DELIMITED BY SIZE
                      WS-PART2      DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    VTOC for volume svmc01 copied
           IF WS-LINE(2: 8) = 'ARC0728I'
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               IF WS-DUMP-TYPE = 'MONTH1  '
                   MOVE '0'                   TO HSMLIST-TYPE
                   MOVE WS-HSMLIST-RCD        TO HSMLIST-RECORD
                   PERFORM 1200-WRITE-HSMLIST
                   GO TO 0800-SELECT-EXIT
               ELSE
                   MOVE '8'                   TO HSMLIST-TYPE
                   MOVE WS-HSMLIST-RCD        TO HSMLIST-RECORD
                   PERFORM 1200-WRITE-HSMLIST
                   GO TO 0800-SELECT-EXIT.
      *    error when reading the DFHSM control data set B record
           IF WS-LINE(2: 8) = 'ARC0184I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
           IF WS-LINE(2: 8) = 'ARC0261I'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
           IF WS-LINE(3: 13) = 'LAST BCDS KEY'
               MOVE WS-LINE(3: )              TO HSMLIST-NOTE
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE '--------'                TO HSMLIST-ARC
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      ****************************************
      *    DUMPS
      *+**************************************
      *    DFHSM dump log
           IF WS-LINE(3: 5) = 'DFSMS'
               AND WS-LINE(12: 8) = 'DUMP LOG'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(27: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(30: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(43: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               ADD 1                          TO WS-SKIP-RCD-CNT
               GO TO 0800-SELECT-EXIT.
      *    automatic dump starting
           IF WS-LINE(2: 8) = 'ARC0620I' OR 'ARC0628I'
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    automatic dump ending
           IF WS-LINE(2: 8) = 'ARC0621I' OR 'ARC0629I'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    dump VTOC copy scratched
           IF WS-LINE(2: 8) = 'ARC0639I'
               MOVE '6'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    dump volume 311665 deleted
           IF WS-LINE(2: 8) = 'ARC0263I'
               MOVE '6'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: 26)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    dump copy invalidated
      *    dump generation invalidated
           IF WS-LINE(2: 8) = 'ARC0262I'
               MOVE '6'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    full volume dump starting on volume svmm01(nonsms)
           IF WS-LINE(2: 8) = 'ARC0622I'
               MOVE '3'                       TO HSMLIST-TYPE
               IF WS-LINE(53: 5) = '(SMS)'
                   MOVE WS-LINE(62: 2)        TO HSMLIST-TIME(1: 2)
                   MOVE WS-LINE(65: 2)        TO HSMLIST-TIME(3: 2)
                   MOVE WS-LINE(76: 8)        TO WS-1-YMD8
                   MOVE WS-1-YMD8-YY          TO HSMLIST-YY
                   MOVE WS-1-YMD8-MM          TO HSMLIST-MM
                   MOVE WS-1-YMD8-DD          TO HSMLIST-DD
                   MOVE WS-LINE(11: 47)       TO WS-PART1
                   MOVE 'Y'                   TO WS-NEXT-SW
                   GO TO 0800-SELECT-EXIT
               ELSE
               IF WS-LINE(53: 5) = '(NONS'
                   MOVE WS-LINE(65: 2)        TO HSMLIST-TIME(1: 2)
                   MOVE WS-LINE(68: 2)        TO HSMLIST-TIME(3: 2)
                   MOVE WS-LINE(79: 8)        TO WS-1-YMD8
                   MOVE WS-1-YMD8-YY          TO HSMLIST-YY
                   MOVE WS-1-YMD8-MM          TO HSMLIST-MM
                   MOVE WS-1-YMD8-DD          TO HSMLIST-DD
                   MOVE WS-LINE(11: 50)       TO WS-PART1
                   MOVE 'Y'                   TO WS-NEXT-SW
                   GO TO 0800-SELECT-EXIT
               ELSE
                   MOVE WS-LINE(63: 2)        TO HSMLIST-TIME(1: 2)
                   MOVE WS-LINE(66: 2)        TO HSMLIST-TIME(3: 2)
                   MOVE WS-LINE(77: 8)        TO WS-1-YMD8
                   MOVE WS-1-YMD8-YY          TO HSMLIST-YY
                   MOVE WS-1-YMD8-MM          TO HSMLIST-MM
                   MOVE WS-1-YMD8-DD          TO HSMLIST-DD
                   MOVE WS-LINE(11: 48)       TO WS-PART1
                   MOVE 'Y'                   TO WS-NEXT-SW
                   GO TO 0800-SELECT-EXIT.
      *    dump volume 302704 added
           IF WS-LINE(2: 8) = 'ARC0120I'
               MOVE '6'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: 50)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    dump copy of volume svmm02 complete
           IF WS-LINE(2: 8) = 'ARC0637I'
               MOVE '6'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    full volume dump of volume svmm02 ending
           IF WS-LINE(2: 8) = 'ARC0623I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(55: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(58: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(11: 40)           TO WS-PART1
               MOVE WS-LINE(65: 25)           TO WS-PART2
               STRING WS-PART1      DELIMITED BY '  '
                      ', '          DELIMITED BY SIZE
                      WS-PART2      DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    automatic deletion of excess dump VTOC copies starting
           IF WS-LINE(2: 8) = 'ARC0648I' OR 'ARC0649I'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
           IF WS-LINE(2: 8) = 'ARC0640I'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      ****************************************
      *    MIGRATION
      *+**************************************
      *    DFHSM migration log
           IF WS-LINE(3: 5) = 'DFSMS'
               AND WS-LINE(12: 13) = 'MIGRATION LOG'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(32: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(35: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(48: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               ADD 1                          TO WS-SKIP-RCD-CNT
               GO TO 0800-SELECT-EXIT.
      *    space management starting on volume svmm11(smsi)
           IF WS-LINE(2: 8) = 'ARC0522I'
               MOVE '1'                       TO HSMLIST-TYPE
               IF WS-LINE(53: 5) = '(SMS)'
                   MOVE WS-LINE(62: 2)        TO HSMLIST-TIME(1: 2)
                   MOVE WS-LINE(65: 2)        TO HSMLIST-TIME(3: 2)
                   MOVE WS-LINE(76: 8)        TO WS-1-YMD8
                   MOVE WS-1-YMD8-YY          TO HSMLIST-YY
                   MOVE WS-1-YMD8-MM          TO HSMLIST-MM
                   MOVE WS-1-YMD8-DD          TO HSMLIST-DD
                   MOVE WS-LINE(11: 48)       TO HSMLIST-NOTE
                   MOVE WS-HSMLIST-RCD        TO HSMLIST-RECORD
                   PERFORM 1200-WRITE-HSMLIST
                   GO TO 0800-SELECT-EXIT
               ELSE
               IF WS-LINE(53: 5) = '(NONS'
                   MOVE WS-LINE(65: 2)        TO HSMLIST-TIME(1: 2)
                   MOVE WS-LINE(68: 2)        TO HSMLIST-TIME(3: 2)
                   MOVE WS-LINE(79: 8)        TO WS-1-YMD8
                   MOVE WS-1-YMD8-YY          TO HSMLIST-YY
                   MOVE WS-1-YMD8-MM          TO HSMLIST-MM
                   MOVE WS-1-YMD8-DD          TO HSMLIST-DD
                   MOVE WS-LINE(11: 50)       TO HSMLIST-NOTE
                   MOVE WS-HSMLIST-RCD        TO HSMLIST-RECORD
                   PERFORM 1200-WRITE-HSMLIST
                   GO TO 0800-SELECT-EXIT
               ELSE
                   MOVE WS-LINE(63: 2)        TO HSMLIST-TIME(1: 2)
                   MOVE WS-LINE(66: 2)        TO HSMLIST-TIME(3: 2)
                   MOVE WS-LINE(77: 8)        TO WS-1-YMD8
                   MOVE WS-1-YMD8-YY          TO HSMLIST-YY
                   MOVE WS-1-YMD8-MM          TO HSMLIST-MM
                   MOVE WS-1-YMD8-DD          TO HSMLIST-DD
                   MOVE WS-LINE(11: 48)       TO HSMLIST-NOTE
                   MOVE WS-HSMLIST-RCD        TO HSMLIST-RECORD
                   PERFORM 1200-WRITE-HSMLIST
                   GO TO 0800-SELECT-EXIT.
      *    command migration for sms010 volume(s) terminated
           IF WS-LINE(2: 8) = 'ARC0570I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    migration volume 303012 is now marked full
           IF WS-LINE(2: 8) = 'ARC0421I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    error while accessing volumes's VTOC
           IF WS-LINE(2: 8) = 'ARC0404I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    dataset has more recent dump copy but backup copy restored
           IF WS-LINE(2: 8) = 'ARC0782I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    volume svmm11 is 66% free
           IF WS-LINE(2: 8) = 'ARC0400I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    VTOC for svmm01 is 0045 tracks
           IF WS-LINE(2: 8) = 'ARC0402I'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    space management processing skipped 2 data sets
           IF WS-LINE(2: 8) = 'ARC0562I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    largest extents for svmm11 are...
           IF WS-LINE(2: 8) = 'ARC0401I'
               MOVE '2'                       TO HSMLIST-TYPE
               PERFORM 0810-FIND-TRACKS
                   VARYING WS-SUB1 FROM 30 BY 1
                   UNTIL WS-SUB1 > 110
               IF WS-TRACK-SW = 'Y'
                   MOVE WS-LINE(11: )         TO HSMLIST-NOTE
                   MOVE WS-HSMLIST-RCD        TO HSMLIST-RECORD
                   PERFORM 1200-WRITE-HSMLIST
                   GO TO 0800-SELECT-EXIT
               ELSE
                   MOVE WS-LINE(11: 31)       TO HSMLIST-NOTE
                   MOVE 'Y'                   TO WS-NEXT-SW
                   GO TO 0800-SELECT-EXIT.
      *    space management ended on volume svmm11
           IF WS-LINE(2: 8) = 'ARC0523I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               INSPECT HSMLIST-NOTE REPLACING ALL ',    ' BY '     '
               MOVE 'Y'                       TO WS-NEXT-SW
               GO TO 0800-SELECT-EXIT.
      *    primary space management starting
           IF WS-LINE(2: 8) = 'ARC0520I' OR 'ARC0521I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: 43)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    DADSM partrel function released 300 tracks
           IF WS-LINE(2: 8) = 'ARC0519I'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    secondary space management starting
           IF WS-LINE(2: 8) = 'ARC0517I' OR 'ARC0518I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    interval migration started
           IF WS-LINE(2: 8) = 'ARC0580I'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    interval migration ended successfully
           IF WS-LINE(2: 8) = 'ARC0581I'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    migration cleanup starting
           IF WS-LINE(2: 8) = 'ARC0526I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(41: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(44: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(55: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 27)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    migration cleanup ended
           IF WS-LINE(2: 8) = 'ARC0527I'
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(38: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(41: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(11: 24)           TO WS-PART1
               MOVE WS-LINE(47: 26)           TO WS-PART2
               STRING WS-PART1      DELIMITED BY '  '
                      ', '          DELIMITED BY SIZE
                      WS-PART2      DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    migration cleanup parameters
           IF WS-LINE(2: 8) = 'ARC0529I'
               MOVE '3'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    volume already in use
           IF WS-LINE(2: 8) = 'ARC0301W'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    data set will be recalled using DFDSS determined value
           IF WS-LINE(2: 8) = 'ARC0303I'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    level 1 migration starting
           IF WS-LINE(2: 8) = 'ARC0530I'
               MOVE '1'                       TO HSMLIST-TYPE
               MOVE WS-LINE(41: 2)            TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(44: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(55: 8)            TO WS-1-YMD8
               MOVE WS-1-YMD8-YY              TO HSMLIST-YY
               MOVE WS-1-YMD8-MM              TO HSMLIST-MM
               MOVE WS-1-YMD8-DD              TO HSMLIST-DD
               MOVE WS-LINE(11: 27)           TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    level 1 migration ending
           IF WS-LINE(2: 8) = 'ARC0531I'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(102: 2)           TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(105: 2)           TO HSMLIST-TIME(3: 2)
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      *    scratch related function failed
           IF WS-LINE(2: 8) = 'ARC0545I'
               MOVE WS-LINE(47: 44)           TO HSMLIST-DSN
               PERFORM 1300-WRITE-DSNOUT
               MOVE '4'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.
      ****************************************
      *    recycle
      *+**************************************
      *    recycle starting on volume
           IF WS-LINE(2: 8) = 'ARC0832I' OR 'ARC0833I'
               MOVE '2'                       TO HSMLIST-TYPE
               MOVE WS-LINE(11: )             TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               GO TO 0800-SELECT-EXIT.

           IF WS-LINE(2: 8) = 'ARC0734I'
               PERFORM 0900-ARC0734I
               GO TO 0800-SELECT-EXIT.

           IF WS-LINE(2: 80) = SPACE
               ADD 1                          TO WS-SKIP-RCD-CNT
           ELSE
               MOVE '--------'                TO HSMLIST-ARC
               MOVE HSMIN-RECORD              TO HSMLIST-NOTE
               MOVE SPACE                     TO HSMLIST-TYPE
               INSPECT HSMLIST-NOTE CONVERTING WS-LOW-VAL TO SPACE
               INSPECT HSMLIST-NOTE CONVERTING WS-LOW-VAL2 TO '.'
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST.

       0800-SELECT-EXIT. EXIT.

       0810-FIND-TRACKS.
           IF WS-LINE(WS-SUB1: 8) = ' TRACKS '
               MOVE 'Y'                       TO WS-TRACK-SW.

      ****************************************
      *    place data from next record into working storage
      *    and write out.  (write was delayed until this data
      *    was obtained).
      *+**************************************
       0850-WS-NEXT-SW.
           IF WS-LINE(3: 4) = 'DSN='
               MOVE WS-LINE(7: 44)            TO HSMLIST-DSN
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               IF HSMLIST-ACTION = 'EXPIRED '
                   AND HSMLIST-RC = '  53'
                   AND HSMLIST-REASON = '    0'
                   PERFORM 1300-WRITE-DSNOUT
               ELSE
                   CONTINUE
           ELSE
           IF WS-LINE(3: 4) = 'TIME'
               MOVE WS-LINE(8: 2)             TO HSMLIST-TIME(1: 2)
               MOVE WS-LINE(11: 2)            TO HSMLIST-TIME(3: 2)
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
           ELSE
           IF WS-LINE(3: 6) = 'TRACKS'
               MOVE WS-LINE(3: 50)            TO HSMLIST-NOTE(32: )
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
           ELSE
           IF WS-LINE(3: 13) = 'TO DUMP CLASS'
               MOVE WS-LINE(24: 8)            TO WS-PART2 WS-DUMP-TYPE
               STRING WS-PART1          DELIMITED BY '  '
                      ' TO DUMP CLASS ' DELIMITED BY SIZE
                      WS-PART2          DELIMITED BY '  '
                      INTO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
           ELSE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               MOVE WS-LINE                   TO HSMLIST-NOTE
               MOVE '--NEXT--'                TO HSMLIST-ARC
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST.

       0900-ARC0734I.
           IF WS-LINE(18: 8) = 'EXBACKV '
               MOVE '3'                       TO HSMLIST-TYPE
               PERFORM 1000-STANDARD-DELIMIT
           ELSE IF WS-LINE(18: 8) = 'BACK-UP ' OR 'MOVE BV '
               MOVE '4'                       TO HSMLIST-TYPE
               PERFORM 1000-STANDARD-DELIMIT
           ELSE IF WS-LINE(18: 8) = 'DELETED '
               MOVE '4'                       TO HSMLIST-TYPE
               PERFORM 1000-STANDARD-DELIMIT
           ELSE IF WS-LINE(18: 8) = 'RECYCLE '
               MOVE '2'                       TO HSMLIST-TYPE
               PERFORM 1000-STANDARD-DELIMIT
           ELSE IF WS-LINE(18: 8) = 'MIGRATE ' OR 'SCRATCH '
                                 OR 'PARTREL ' OR 'SPCMGMT '
                                 OR 'REDUCED ' OR 'EXPIRED '
               MOVE '4'                       TO HSMLIST-TYPE
               PERFORM 1000-STANDARD-DELIMIT
           ELSE
               MOVE SPACE                     TO HSMLIST-TYPE
               MOVE HSMIN-RECORD              TO HSMLIST-NOTE
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST.

       1000-STANDARD-DELIMIT.
           MOVE SPACE                         TO HSMLIST-NOTE.
           MOVE WS-LINE(18: 8)                TO HSMLIST-ACTION.
           MOVE WS-LINE(32: 6)                TO HSMLIST-FROM.
           MOVE WS-LINE(45: 6)                TO HSMLIST-TO.
           MOVE WS-LINE(59: 6)                TO HSMLIST-TRACKS.
           MOVE WS-LINE(69: 4)                TO HSMLIST-RC.
           MOVE WS-LINE(82: 5)                TO HSMLIST-REASON.
           IF WS-LINE(18: 8) = 'BACK-UP '
               IF HSMLIST-RC = '   0'
                   MOVE '6'                   TO HSMLIST-TYPE.
           IF WS-LINE(18: 8) = 'MIGRATE ' OR 'SPCMGMT '
               IF HSMLIST-RC NOT = '   0'
                   MOVE '3'                   TO HSMLIST-TYPE.
           IF WS-LINE(18: 8) = 'DELETED '
               IF HSMLIST-RC = '   0'
                   IF WS-LINE(99: 7) = 'DSN=VSR' OR 'DSN=DSR'
                       MOVE '1'               TO HSMLIST-TYPE
                   ELSE
                       MOVE '4'               TO HSMLIST-TYPE
               ELSE
                   MOVE '6'                   TO HSMLIST-TYPE.
           IF WS-LINE(18: 8) = 'MIGRATE ' OR 'SCRATCH ' OR 'EXPIRED '
               IF HSMLIST-RC = '   0'
                   MOVE '7'                   TO HSMLIST-TYPE.
           MOVE WS-LINE(93: 4)                TO HSMLIST-AGE.
           IF WS-LINE(99: 4) = 'DSN='
               MOVE WS-LINE(103: 19)          TO HSMLIST-DSN
               MOVE WS-HSMLIST-RCD            TO HSMLIST-RECORD
               PERFORM 1200-WRITE-HSMLIST
               IF HSMLIST-ACTION = 'EXPIRED '
                   AND HSMLIST-RC = '  53'
                   AND HSMLIST-REASON = '    0'
                   PERFORM 1300-WRITE-DSNOUT
               ELSE
                   CONTINUE
           ELSE
               MOVE 'Y'                       TO WS-NEXT-SW.

       1200-WRITE-HSMLIST.
           WRITE HSMLIST-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0465B write error on HSMLIST'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +1200                     TO WS-RETURN-CODE.
           ADD 1                              TO WS-HSMIN-RCD-CNT.

       1300-WRITE-DSNOUT.
           MOVE SPACE                         TO DSNOUT-RECORD.
           STRING QUOTE DELIMITED BY SIZE
                  HSMLIST-DSN DELIMITED BY ' '
                  QUOTE DELIMITED BY SIZE
                  INTO DSNOUT-RECORD.
           WRITE DSNOUT-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0465B write error on DSNOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +1300                     TO WS-RETURN-CODE.
           ADD 1                              TO WS-DSN-RCD-CNT.

      ************************************ END MIS0470B
