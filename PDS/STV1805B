
       TITLE '  STV1805B: return DOW from date'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV1805B.
       AUTHOR.        MERCY HEALTH PARTNERS.
      ***************************************************************
      *@@@ II convert and return dow for a given date
      ***************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ***************************************************************
      * Date written  |  APRIL  8, 1996
      * Narrative     |  THIS PROGRAM IS A SUBPROGRAM DESIGNED
      *               |  TO GIVE THE DAY OF THE WEEK THAT A GIVEN
      *               |  DATE FALLS ON.
      *
      *    INPUT  - WS-DAY-IN "YYYYMMDD"
      *    OUTPUT - WS-DAY-NUM-OUT - WS-DAY-OUT
      *                          1 - "MONDAY"
      *                          2 - "TUESDAY"
      *                          3 - "WEDNESDAY"
      *                          4 - "THURSDAY"
      *                          5 - "FRIDAY"
      *                          6 - "SATURDAY"
      *                          7 - "SUNDAY"
      ***************************************************************
      * Date changed: 03/16/98
      * Programmer  : Jim Iannone
      *        CONVERT TO STV1805B, AND
      *        CORRECTLY PROCESS NON-LEAP CENTURIES (1700,1800...)
      ***************************************************************

       ENVIRONMENT DIVISION.
       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING-STORAGE STV1805B START '.

       01  WS-FIELDS.
           05  WS-DAY-FIRST-DAY        PIC 9(1).
           05  WS-DAY-JUL              PIC 9(3).
           05  WS-DAY-LEAPS            PIC 9(4).
           05  WS-DAY-LEAP2            PIC 9(4).
           05  WS-DAY-REMAINDER        PIC 9(4).
           05  WS-DAY-REMAIN2          PIC 9(4).
           05  WS-DAY-TOTAL-DAYS       PIC 9(10).
           05  WS-DAY-YEAR             PIC 9(4).
           05  WS-DAY-INDX             PIC 9(10)V9(2).
           05  WS-DAY-INDX2            PIC 9(2).

       01  WS-WEEK-TABLE.
           05  FILLER                  PIC X(9)  VALUE 'MONDAY'.
           05  FILLER                  PIC X(9)  VALUE 'TUESDAY'.
           05  FILLER                  PIC X(9)  VALUE 'WEDNESDAY'.
           05  FILLER                  PIC X(9)  VALUE 'THURSDAY'.
           05  FILLER                  PIC X(9)  VALUE 'FRIDAY'.
           05  FILLER                  PIC X(9)  VALUE 'SATURDAY'.
           05  FILLER                  PIC X(9)  VALUE 'SUNDAY'.
       01  FILLER REDEFINES WS-WEEK-TABLE.
           05  WS-WEEK-DAY         PIC  X(9)
                                   OCCURS 7 TIMES
                                   INDEXED BY WS-WEEK-NUM.

       01  WS-STV1410B-FIELDS.
           05  STV1410B-IN-DATE        PIC  X(08).
           05  STV1410B-CYDDD          PIC  X(07).
           05  FILLER REDEFINES STV1410B-CYDDD.
               10  STV1410B-CYDDD-CY   PIC  X(04).
               10  STV1410B-CYDDD-DDD  PIC  XXX.
           05  STV1410B-CYMD           PIC  X(08).
           05  FILLER REDEFINES STV1410B-CYMD.
               10  STV1410B-CYMD-C     PIC  XX.
               10  STV1410B-CYMD-Y     PIC  XX.
               10  STV1410B-CYMD-M     PIC  XX.
               10  STV1410B-CYMD-D     PIC  XX.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  END STV1805B  '.

       LINKAGE SECTION.

       01  WS-PARM.
           05  WS-DAY-IN.
               10 WS-DI-CC            PIC XX.
               10 WS-DI-YY            PIC XX.
               10 WS-DI-MM            PIC XX.
               10 WS-DI-DD            PIC XX.
           05  WS-DAY-OUT             PIC X(9).
           05  WS-DAY-NUM-OUT         PIC 9(2).

       PROCEDURE DIVISION USING WS-PARM.

       0000-START-PROCESS.

      ****
      **** FIND JULEAN DAYS SINCE JAN 1
      **** ============================
           MOVE SPACE      TO  WS-STV1410B-FIELDS.
           MOVE WS-DAY-IN  TO  STV1410B-IN-DATE.
           CALL 'STV1410B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1410B-FIELDS.
           IF STV1410B-CYDDD-DDD = ZEROS
              MOVE ZEROS        TO  WS-DAY-NUM-OUT
              MOVE '*********'  TO  WS-DAY-OUT
              GO TO 0200-END-PROGRAM.
           MOVE STV1410B-CYDDD-DDD      TO  WS-DAY-JUL.

      ****
      **** FIND LEAP YEARS SINCE THE BEGINING OF TIME
      **** ==========================================
           MOVE WS-DAY-IN (1:4)      TO  WS-DAY-YEAR.
           MOVE ZERO                 TO  WS-DAY-REMAINDER.
           DIVIDE WS-DAY-YEAR      BY  4
              GIVING        WS-DAY-LEAPS
              REMAINDER WS-DAY-REMAINDER.

      ****
      **** IF THIS IS A LEAP YEAR THEN JUL DATE WILL
      **** TAKE CARE OF THIS SUBTRACT 1
      **** =========================================
           IF WS-DAY-REMAINDER = ZERO
               SUBTRACT 1 FROM WS-DAY-LEAPS
               IF WS-DI-YY = '00'
                   DIVIDE WS-DAY-YEAR BY 400
                      GIVING WS-DAY-LEAP2
                      REMAINDER WS-DAY-REMAIN2
                   IF WS-DAY-REMAIN2 NOT = ZERO
                       ADD 1 TO WS-DAY-LEAPS.

      ****
      **** CALCULATE HOW MANY DAYS SINCE THE BEGINING OF TIME
      **** ==================================================
           COMPUTE WS-DAY-TOTAL-DAYS =
                ((WS-DAY-YEAR * 365) + WS-DAY-LEAPS + WS-DAY-JUL - 1).

      ****
      **** SET THE FIRST DAY OF THE WORLD
      **** ==============================
           MOVE 6 TO WS-DAY-FIRST-DAY.

      ****
      **** FIND THE OFFSET NUMBER OF DAYS
      **** ==============================
           DIVIDE WS-DAY-TOTAL-DAYS BY 7
              GIVING    WS-DAY-INDX.
           MOVE WS-DAY-INDX (11:2) TO WS-DAY-INDX2.

           EVALUATE WS-DAY-INDX2
              WHEN 14
                 MOVE 1 TO WS-DAY-NUM-OUT
              WHEN 28
                 MOVE 2 TO WS-DAY-NUM-OUT
              WHEN 42
                 MOVE 3 TO WS-DAY-NUM-OUT
              WHEN 57
                 MOVE 4 TO WS-DAY-NUM-OUT
              WHEN 71
                 MOVE 5 TO WS-DAY-NUM-OUT
              WHEN 85
                 MOVE 6 TO WS-DAY-NUM-OUT
              WHEN OTHER
                 MOVE ZERO TO WS-DAY-NUM-OUT
           END-EVALUATE.

      ****
      **** USE OFFSET TO GET THE DAY
      **** =========================
           ADD  WS-DAY-FIRST-DAY  TO  WS-DAY-NUM-OUT.
           IF WS-DAY-NUM-OUT  > 7
                 SUBTRACT 7 FROM WS-DAY-NUM-OUT.
           MOVE WS-WEEK-DAY(WS-DAY-NUM-OUT) TO WS-DAY-OUT.

       0200-END-PROGRAM.
           GOBACK.

      ************************************ END STV1805B

