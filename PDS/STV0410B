
       TITLE '  STV0410B: OS390 Cobol date conversion'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV0410B.
       AUTHOR.        JIM IANNONE
      ****************************************************************
      *@@@ c3 convert julian\gregorian - year 2000 compliant
      ****************************************************************
      *  Date-written.  03/10/98.
      *  Installation.  St Vincent MIS department.
      *
      *  Author of STV0300B   Tom Richardson
      ****************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ****************************************************************
      * This subprogram converts a date that was transferred from
      * the calling program from julian format (CCYYDDD) to gregorian
      * format (CCYYMMDD) or it converts a date that was transferred
      * from the calling program from gregorian format to julian
      * format.
      ****************************************************************
      * Date changed: 04/07/2006
      * Programmer  : Jim Iannone
      *               compile with Cobol 3 with DYNAM option
      ******************************************************************

       ENVIRONMENT DIVISION.

       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-ANNOUNCE-START.
           05  FILLER                  PIC  X(43)      VALUE
               '**** WORKING STORAGE STV0410B STARTS HERE  '.

       01  WS-OUTPUT.
           05  WS-OUTPUT-DATE          PIC  X(08)     VALUE SPACE.
           05  OUT-GREG-TO-JULIAN REDEFINES WS-OUTPUT-DATE.
               10  WS-OUT-JULIAN-CY    PIC  9(04).
               10  WS-OUT-JULIAN-DDD   PIC  9(03).
               10  FILLER              PIC  X.
           05  OUT-JULIAN-TO-GREG REDEFINES WS-OUTPUT-DATE.
               10  WS-OUT-GREG-CY      PIC  9(04).
               10  WS-OUT-GREG-MM      PIC  9(02).
               10  WS-OUT-GREG-DD      PIC  9(02).

       01  WS-INPUT.
           05  WS-INPUT-DATE               PIC  X(08)  VALUE SPACE.
           05  WS-INPUT-GREG REDEFINES WS-INPUT-DATE.
               10  WS-IN-GREG-CY           PIC  9(04).
               10  FILLER REDEFINES WS-IN-GREG-CY.
                   15  WS-IN-GREG-C        PIC  99.
                   15  WS-IN-GREG-Y        PIC  99.
               10  WS-IN-GREG-MM           PIC  99.
               10  WS-IN-GREG-DD           PIC  99.
           05  WS-INPUT-JULIAN REDEFINES WS-INPUT-DATE.
               10  WS-IN-JULIAN-DATE.
                   15  WS-IN-JULIAN-CY     PIC  9(04).
                   15  FILLER REDEFINES WS-IN-JULIAN-CY.
                       20  WS-IN-JULIAN-C  PIC 99.
                       20  WS-IN-JULIAN-Y  PIC 99.
                   15  WS-IN-JULIAN-DDD    PIC  9(03).
               10  WS-IN-FILLER            PIC  X(01).

      *****************************************************************
      **             TABLES USED FOR CONVERTING DATE                  *
      *****************************************************************
       01  JULIAN-DATES-TABLE.
           05  JULIAN-TABLE1.
               10  JAN                 PIC  999       VALUE 000.
               10  FEB                 PIC  999       VALUE 031.
               10  MAR                 PIC  999       VALUE 059.
               10  APR                 PIC  999       VALUE 090.
               10  MAY                 PIC  999       VALUE 120.
               10  JUN                 PIC  999       VALUE 151.
               10  JUL                 PIC  999       VALUE 181.
               10  AUG                 PIC  999       VALUE 212.
               10  SEP                 PIC  999       VALUE 243.
               10  OCT                 PIC  999       VALUE 273.
               10  NOV                 PIC  999       VALUE 304.
               10  DEC                 PIC  999       VALUE 334.
               10  EOY                 PIC  999       VALUE 365.
           05  J-TABLE1 REDEFINES JULIAN-TABLE1.
               10  MONTH               PIC  999
                   OCCURS 13.

           05  JULIAN-LEAP-YR-TABLE.
               10  JANL                PIC  999       VALUE 000.
               10  FEBL                PIC  999       VALUE 031.
               10  MARL                PIC  999       VALUE 060.
               10  APRL                PIC  999       VALUE 091.
               10  MAYL                PIC  999       VALUE 121.
               10  JUNL                PIC  999       VALUE 152.
               10  JULL                PIC  999       VALUE 182.
               10  AUGL                PIC  999       VALUE 213.
               10  SEPL                PIC  999       VALUE 244.
               10  OCTL                PIC  999       VALUE 274.
               10  NOVL                PIC  999       VALUE 305.
               10  DECL                PIC  999       VALUE 335.
               10  EOYL                PIC  999       VALUE 366.
           05  J-LEAP-YR-TABLE REDEFINES JULIAN-LEAP-YR-TABLE.
               10  MONTH-L             PIC  999
                   OCCURS 13.

       01  WS-WORK-FIELDS.
           05  WS-CY                   PIC  9(04)     VALUE ZERO.
           05  FILLER REDEFINES WS-CY.
               10  WS-CY-C             PIC  99.
               10  WS-CY-Y             PIC  99.
           05  WS-QUOTIENT             PIC S99        VALUE ZERO.
           05  WS-REMAINDER            PIC S99        VALUE ZERO.
           05  WS-QUOTIENT2            PIC S9(04)     VALUE ZERO.
           05  WS-REMAINDER2           PIC S9(04)     VALUE ZERO.

       01  WS-SWITCHES.
           05  LEAP-YR-SWITCH          PIC  X         VALUE 'N'.
               88  NOT-LEAP-YR                        VALUE 'N'.
               88  LEAP-YR                            VALUE 'Y'.
           05  CONVERSION-TYPE-SW      PIC  X         VALUE ' '.
               88  CONVERTING-TO-GREGOR               VALUE 'G'.
               88  CONVERTING-TO-JULIAN               VALUE 'J'.

       01  WS-SUBSCRIPTS.
           05   I1                     PIC 99         VALUE ZERO.
           05   I2                     PIC 99         VALUE ZERO.

       01  WS-WORK-FIELDS.
           05  WS-CONV-DATE.
               10  WS-FIRST-7          PIC  X(07).
               10  WS-LAST-1           PIC  X(01).

       01  WS-ANNOUNCE-END.
           05  FILLER                  PIC  X(43)     VALUE
               '**** END STV0410B WORKING STORAGE HERE ****'.

       LINKAGE SECTION.

       01  LS-FIELDS.
           05  LS-IN-DATE              PIC  X(08).
           05  LS-JULIAN-CYDDD         PIC  X(07).
           05  LS-GREGOR-CYMD          PIC  X(08).

       PROCEDURE DIVISION USING LS-FIELDS.

       0000-GET-CONV-TYPE.
           MOVE ZERO                   TO I1, I2.
           MOVE 'N'                    TO LEAP-YR-SWITCH.
           MOVE LS-IN-DATE             TO WS-CONV-DATE WS-INPUT-DATE.
           IF WS-LAST-1 = SPACE
               MOVE 'G'                TO CONVERSION-TYPE-SW
               GO TO 1100-EDIT-OLD-JULIAN
           ELSE
               MOVE 'J'                TO CONVERSION-TYPE-SW
               GO TO 2100-EDIT-OLD-GREGOR.

      ******************************************************************
      *    EDIT THE JULIAN DATE.  IF AN ERROR OCCURS, EXIT THIS
      *    PROGRAM AND RETURN ZEROES AS THE CONVERTED DATE.
      *    OTHERWISE CONVERT IT TO THE CORRESPONDING GREGORIAN DATE.
      ******************************************************************
       1100-EDIT-OLD-JULIAN.
           IF WS-IN-JULIAN-DATE NOT NUMERIC
               GO TO 9000-FAILED-EDIT.
           IF WS-IN-JULIAN-Y < 0
               GO TO 9000-FAILED-EDIT.
           IF WS-IN-JULIAN-DDD < 1
               GO TO 9000-FAILED-EDIT.
           DIVIDE WS-IN-JULIAN-Y      BY 4
               GIVING WS-QUOTIENT      REMAINDER WS-REMAINDER.
           IF  WS-REMAINDER = ZERO
               MOVE 'Y'                TO LEAP-YR-SWITCH.
           IF WS-IN-JULIAN-Y = ZERO
               DIVIDE WS-IN-JULIAN-CY BY 400
                   GIVING WS-QUOTIENT2 REMAINDER WS-REMAINDER2
               IF WS-REMAINDER2 NOT = ZERO
                   MOVE 'N'            TO LEAP-YR-SWITCH.
           IF LEAP-YR
               IF WS-IN-JULIAN-DDD > 366
                   GO TO 9000-FAILED-EDIT.
           IF NOT-LEAP-YR
               IF WS-IN-JULIAN-DDD > 365
                   GO TO 9000-FAILED-EDIT.
           MOVE WS-IN-JULIAN-CY        TO WS-OUT-GREG-CY.
           IF LEAP-YR
               GO TO 1300-FIND-MONTH-LEAP.

      ******************************************************************
      ** FIND THE MONTH AND CONVERT THE JULIAN DATE TO GREGORIAN
      ** (CYMD) FORMAT.
      ******************************************************************
       1200-FIND-MONTH.
           COMPUTE I1 = I1 + 1.
           COMPUTE I2 = I1 + 1.
           IF I1 > 13
               GO TO 9000-FAILED-EDIT.
           IF (WS-IN-JULIAN-DDD > MONTH(I1))
               AND (WS-IN-JULIAN-DDD NOT > MONTH(I2))
               SUBTRACT MONTH(I1)  FROM WS-IN-JULIAN-DDD
                   GIVING WS-OUT-GREG-DD
               MOVE I1             TO WS-OUT-GREG-MM
               MOVE WS-OUTPUT-DATE TO LS-GREGOR-CYMD
               GO TO 0100-END-PROCESSING
           ELSE
               GO TO 1200-FIND-MONTH.

      ******************************************************************
      ** FIND THE MONTH IF THE DATE PASSED IS FOR A LEAP YEAR.  THEN  **
      ** CONVERT THE JULIAN DATE TO GREGORIAN (MMDDYY) FORMAT.**
      ******************************************************************
       1300-FIND-MONTH-LEAP.
           COMPUTE I1 = I1 + 1.
           COMPUTE I2 = I1 + 1.
           IF I1 IS > 13
               GO TO 9000-FAILED-EDIT.
           IF (WS-IN-JULIAN-DDD > MONTH-L(I1))
               AND (WS-IN-JULIAN-DDD NOT > MONTH-L(I2))
               SUBTRACT MONTH-L (I1) FROM WS-IN-JULIAN-DDD
                   GIVING WS-OUT-GREG-DD
               MOVE I1             TO WS-OUT-GREG-MM
               MOVE WS-OUTPUT-DATE TO LS-GREGOR-CYMD
               GO TO 0100-END-PROCESSING
           ELSE
               GO TO 1300-FIND-MONTH-LEAP.

      ******************************************************************
      ** EDIT THE INPUTED GREGORIAN DATE.  IF AN ERROR OCCURS, EXIT   **
      ** THIS PROGRAM AND RETURN ZEROES AS THE CONVERTED DATE.        **
      ** OTHERWISE CONVERT IT TO THE CORRESPONDING JULIAN DATE.       **
      ******************************************************************
       2100-EDIT-OLD-GREGOR.
           IF WS-INPUT-DATE NOT NUMERIC
               GO TO 9000-FAILED-EDIT.
           IF (WS-IN-GREG-MM < 1 OR > 12 )
               GO TO 9000-FAILED-EDIT.
           IF WS-IN-GREG-Y < 0
               GO TO 9000-FAILED-EDIT.
           IF WS-IN-GREG-DD < 1
               GO TO 9000-FAILED-EDIT.
           MOVE WS-IN-GREG-MM          TO I1.
           COMPUTE I2 =  WS-IN-GREG-MM + 1.
           DIVIDE WS-IN-GREG-Y         BY 4
               GIVING WS-QUOTIENT      REMAINDER WS-REMAINDER.
           IF  WS-REMAINDER = ZERO
               MOVE 'Y'                TO LEAP-YR-SWITCH.
           IF WS-IN-GREG-Y = ZERO
               DIVIDE WS-IN-GREG-CY BY 400
                   GIVING WS-QUOTIENT2 REMAINDER WS-REMAINDER2
               IF WS-REMAINDER2 NOT = ZERO
                   MOVE 'N'            TO LEAP-YR-SWITCH.
           IF LEAP-YR
               IF WS-IN-GREG-DD >  MONTH-L(I2) - MONTH-L(I1)
                   GO TO 9000-FAILED-EDIT.
           IF NOT-LEAP-YR
               IF WS-IN-GREG-DD >  MONTH(I2) - MONTH(I1)
                   GO TO 9000-FAILED-EDIT.

      ******************************************************************
      ** A VALID GREGORIAN DATE WAS PASSED SO CONTINUE TO CONVERT IT  **
      ** TO JULIAN FORMAT (YYDDD).                                    **
      ******************************************************************

           IF  LEAP-YR
               COMPUTE WS-OUT-JULIAN-DDD = MONTH-L(I1) + WS-IN-GREG-DD
           ELSE
               COMPUTE WS-OUT-JULIAN-DDD = MONTH(I1) + WS-IN-GREG-DD.
           MOVE WS-IN-GREG-CY          TO WS-OUT-JULIAN-CY.
           MOVE WS-OUTPUT-DATE         TO LS-JULIAN-CYDDD.
           GO TO 0100-END-PROCESSING.

      ******************************************************************
      **  ROUTINE FOR HANDLING BAD OR NO DATES THAT ARE PASSED BY THE **
      **  CALLING PROGRAM.  ZEROES ARE RETURNED AS THE CONVERTED DATE.**
      ******************************************************************
       9000-FAILED-EDIT.
           IF CONVERTING-TO-GREGOR
               MOVE ZEROS              TO LS-GREGOR-CYMD
               GO TO 0100-END-PROCESSING
           ELSE
               MOVE ZEROS              TO LS-JULIAN-CYDDD
               GO TO 0100-END-PROCESSING.

       0100-END-PROCESSING.
           GOBACK.

      ************************************ END STV0410B
