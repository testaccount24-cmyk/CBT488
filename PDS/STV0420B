
       TITLE '  STV0420B: dates / nbr of days conversions'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV0420B.
      *AUTHOR.        JIM IANNONE.
      ******************************************************************
      *@@@ C3 dates/nbr of days conversions
      ******************************************************************
      *    OS390 Cobol version of STV0155B (CYMD/CYDDD format)
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ****************************************************************
      * Program-id  : stv0420b
      * Project #   : 900824 for STV0155B and STV0310B
      * Date written: 03/10/98
      * Programmer  : Jim Iannone
      *
      *               compiled under OS/390 Cobol
      *
      *               This program uses three fields: date1 (past date),
      *               date2 (future date), and a number of days.
      *               Given any two of these fields, the program will
      *               return the third. Dates will be accepted and
      *               returned in the form CCYYDDD or CCYYMMDD.
      *               The highest number of days allowed is 99599.
      *               Any error will cause '*****' to be placed in the
      *               number field.
      *
      *               Note:  century years that are divisable by 400
      *               are leap years (1600, 2000, 2400, etc.). the
      *               others are not (1700, 1800, 1900, 2100, etc.).
      *
      ******************************************************************
      * Date changed: 04/07/2006
      * Programmer  : Jim Iannone
      *               compile with Cobol 3 with DYNAM option
      ******************************************************************

       ENVIRONMENT DIVISION.
       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING-STORAGE STV0420B START '.

       01  WS-FIELDS.
           05  WS-HOLD-DATE.
               10  WS-HOLD-1X7         PIC  X(07)    VALUE SPACE.
               10  WS-HOLD-8           PIC  X        VALUE SPACE.
           05  WS-J-DATE1                            VALUE ZERO.
               10  WS-J-YYYY1          PIC  9(04).
               10  FILLER              REDEFINES WS-J-YYYY1.
                   15  WS-J-CC1        PIC  99.
                   15  WS-J-YY1        PIC  99.
               10  WS-J-DDD1           PIC  999.
           05  WS-J-DATE2                            VALUE ZERO.
               10  WS-J-YYYY2          PIC  9(04).
               10  FILLER              REDEFINES WS-J-YYYY2.
                   15  WS-J-CC2        PIC  99.
                   15  WS-J-YY2        PIC  99.
               10  WS-J-DDD2           PIC  999.
           05  WS-J-DATE3              PIC  9(07)    VALUE ZERO.
           05  WS-G-DATE1                            VALUE ZERO.
               10  WS-G-YYYY1.
                   15  WS-G-CC1        PIC  99.
                   15  WS-G-YY1        PIC  99.
               10  WS-G-MM1            PIC  99.
               10  WS-G-DD1            PIC  99.
           05  WS-G-DATE2                            VALUE ZERO.
               10  WS-G-YYYY2.
                   15  WS-G-CC2        PIC  99.
                   15  WS-G-YY2        PIC  99.
               10  WS-G-MM2            PIC  99.
               10  WS-G-DD2            PIC  99.
           05  WS-CALL-GREGORIAN                     VALUE SPACE.
               10  WS-CALL-G-CC        PIC  XX.
               10  WS-CALL-G-YY        PIC  XX.
               10  WS-CALL-G-MM        PIC  XX.
               10  WS-CALL-G-DD        PIC  XX.
           05  WS-CALL-JULIAN                        VALUE SPACE.
               10  WS-CALL-J-CC        PIC  XX.
               10  WS-CALL-J-YY        PIC  XX.
               10  WS-CALL-J-DDD       PIC  XXX.
               10  FILLER              PIC  X.
           05  WS-DATE-PAST            PIC  X(07)    VALUE SPACE.
           05  WS-DATE-FUTURE          PIC  X(07)    VALUE SPACE.
           05  WS-CALL410B             PIC  X(08)    VALUE 'STV0410B'.
           05  WS-CY                   PIC  9(04)    VALUE ZERO.
           05  FILLER REDEFINES WS-CY.
               10  WS-CY-C             PIC  99.
               10  WS-CY-Y             PIC  99.
           05  WS-NBR-5                PIC  9(05)    VALUE ZERO.
           05  WS-NBR                  PIC  999      VALUE ZERO.
           05  WS-CNT                  PIC  9(05)    VALUE ZERO.
           05  WS-RESULT               PIC  9(05)    VALUE ZERO.
           05  WS-MAX-DAYS             PIC  999      VALUE ZERO.
           05  WS-QUOTIENT             PIC  9(04)    VALUE ZERO.
           05  WS-REMAIN               PIC  99       VALUE ZERO.
           05  WS-REMAIN2              PIC  9(04)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-TYPE-DATE-SW         PIC  X        VALUE SPACE.
               88  GREGORIAN                         VALUE 'G'.

      *    JULIAN / GREGORIAN DATE CONVERSIONS
       01  WS-STV0410B-FIELDS.
           05  STV0410B-IN-DATE        PIC  X(08).
           05  STV0410B-CYDDD          PIC  X(07).
           05  STV0410B-CYMD           PIC  X(08).

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  END STV0420B  '.

       LINKAGE SECTION.

       01  LS-CALC-DATES.
           05  LS-DATE-PAST            PIC  X(08).
           05  LS-DATE-FUTURE          PIC  X(08).
           05  LS-NBR                  PIC  X(05).

       PROCEDURE DIVISION USING LS-CALC-DATES.

       0000-START-PROCESS.
           MOVE ZERO                          TO WS-J-DATE1 WS-J-DATE2
                                                 WS-G-DATE1 WS-G-DATE2.
           MOVE SPACE                         TO WS-CALL-GREGORIAN
                                                 WS-CALL-JULIAN
                                                 WS-DATE-PAST
                                                 WS-DATE-FUTURE
                                                 WS-HOLD-DATE
                                                 WS-TYPE-DATE-SW.
           MOVE ZERO                          TO WS-NBR WS-NBR-5
                                                 WS-CY WS-CNT WS-RESULT
                                                 WS-MAX-DAYS WS-QUOTIENT
                                                 WS-REMAIN WS-REMAIN2.
           INSPECT LS-NBR REPLACING ALL SPACE BY ZERO.                  930???
           MOVE LS-DATE-FUTURE                TO WS-DATE-FUTURE.
           MOVE LS-DATE-PAST                  TO WS-DATE-PAST.
           IF (WS-DATE-FUTURE NUMERIC)
               AND (WS-DATE-PAST NOT NUMERIC)
               AND (LS-NBR NUMERIC)
               GO TO 0800-RETURN-PAST-DATE.
           IF (WS-DATE-PAST NUMERIC)
               AND (WS-DATE-FUTURE NOT NUMERIC)
               AND (LS-NBR NUMERIC)
               GO TO 1200-RETURN-FUTURE-DATE.
           IF (WS-DATE-FUTURE NOT NUMERIC)
               OR (WS-DATE-PAST NOT NUMERIC)
               GO TO 1600-DATE-ERROR.
           IF LS-DATE-FUTURE NUMERIC
               MOVE LS-DATE-PAST              TO WS-HOLD-DATE
               IF WS-HOLD-8 NOT NUMERIC
                   GO TO 1600-DATE-ERROR.
           IF LS-DATE-PAST NUMERIC
               MOVE LS-DATE-FUTURE            TO WS-HOLD-DATE
               IF WS-HOLD-8 NOT NUMERIC
                   GO TO 1600-DATE-ERROR.
      ****************************************
      *    GIVEN 2 DATES, CALCULATE THE NUMBER OF DAYS BETWEEN.
      ****************************************
           MOVE LS-DATE-FUTURE                TO WS-HOLD-DATE.
           IF WS-HOLD-8 NUMERIC
               MOVE 'G'                       TO WS-TYPE-DATE-SW.
           IF GREGORIAN
               PERFORM 0100-CONVERT-TO-JUL-1
               PERFORM 0300-EDIT-J-DATE1
               PERFORM 0200-CONVERT-TO-JUL-2
               PERFORM 0400-EDIT-J-DATE2
               GO TO 0500-FIND-NUMBER.

           MOVE LS-DATE-PAST                  TO WS-HOLD-DATE.
           MOVE WS-HOLD-1X7                   TO WS-J-DATE1.
           PERFORM 0300-EDIT-J-DATE1.
           MOVE LS-DATE-FUTURE                TO WS-HOLD-DATE.
           MOVE WS-HOLD-1X7                   TO WS-J-DATE2.
           PERFORM 0400-EDIT-J-DATE2.
           GO TO 0500-FIND-NUMBER.

       0100-CONVERT-TO-JUL-1.
           MOVE LS-DATE-PAST                  TO STV0410B-IN-DATE.
           CALL WS-CALL410B USING WS-STV0410B-FIELDS.
           IF STV0410B-CYDDD NUMERIC
               IF STV0410B-CYDDD NOT = ZERO
                   MOVE STV0410B-CYDDD        TO WS-CALL-JULIAN
               ELSE
                   GO TO 1600-DATE-ERROR
           ELSE
               GO TO 1600-DATE-ERROR.
           MOVE WS-CALL-J-DDD                 TO WS-J-DDD1.
           MOVE WS-CALL-J-YY                  TO WS-J-YY1.
           MOVE WS-CALL-J-CC                  TO WS-J-CC1.

       0200-CONVERT-TO-JUL-2.
           MOVE LS-DATE-FUTURE                TO STV0410B-IN-DATE.
           CALL WS-CALL410B USING WS-STV0410B-FIELDS.
           IF STV0410B-CYDDD NUMERIC
               IF STV0410B-CYDDD NOT = ZERO
                   MOVE STV0410B-CYDDD        TO WS-CALL-JULIAN
               ELSE
                   GO TO 1600-DATE-ERROR
           ELSE
               GO TO 1600-DATE-ERROR.
           MOVE WS-CALL-J-DDD                 TO WS-J-DDD2.
           MOVE WS-CALL-J-YY                  TO WS-J-YY2.
           MOVE WS-CALL-J-CC                  TO WS-J-CC2.

       0300-EDIT-J-DATE1.
           IF WS-J-YYYY1 = ZERO
               GO TO 1600-DATE-ERROR.
           IF WS-J-DDD1 = ZERO
               OR WS-J-DDD1 > 366
               GO TO 1600-DATE-ERROR.
           IF WS-J-DDD1 = 366
               MOVE WS-J-YYYY1                TO WS-CY
               DIVIDE WS-CY BY 4 GIVING WS-QUOTIENT
                   REMAINDER WS-REMAIN
               IF WS-REMAIN NOT = ZERO
                   GO TO 1600-DATE-ERROR
               ELSE
               IF WS-CY-Y = ZERO
                   DIVIDE WS-CY BY 400 GIVING WS-QUOTIENT
                       REMAINDER WS-REMAIN2
                   IF WS-REMAIN2 NOT = ZERO
                       GO TO 1600-DATE-ERROR.

       0400-EDIT-J-DATE2.
           IF WS-J-YYYY2 = ZERO
               GO TO 1600-DATE-ERROR.
           IF WS-J-DDD2 = ZERO
               OR WS-J-DDD2 > 366
               GO TO 1600-DATE-ERROR.
           IF WS-J-DDD2 = 366
               MOVE WS-J-YYYY2                TO WS-CY
               DIVIDE WS-CY BY 4 GIVING WS-QUOTIENT
                   REMAINDER WS-REMAIN
               IF WS-REMAIN NOT = ZERO
                   GO TO 1600-DATE-ERROR
               ELSE
               IF WS-CY-Y = ZERO
                   DIVIDE WS-CY BY 400 GIVING WS-QUOTIENT
                       REMAINDER WS-REMAIN2
                   IF WS-REMAIN2 NOT = ZERO
                       GO TO 1600-DATE-ERROR.

       0500-FIND-NUMBER.
           IF WS-J-DATE1 > WS-J-DATE2
               MOVE WS-J-DATE1                TO WS-J-DATE3
               MOVE WS-J-DATE2                TO WS-J-DATE1
               MOVE WS-J-DATE3                TO WS-J-DATE2.
           IF WS-J-YYYY1 = WS-J-YYYY2
               IF WS-J-DDD2 > WS-J-DDD1
                   SUBTRACT WS-J-DDD1 FROM WS-J-DDD2
                       GIVING WS-NBR-5
               ELSE
                   MOVE ZERO                  TO WS-NBR-5
           ELSE
               ADD WS-J-DDD2                  TO WS-NBR-5
               MOVE WS-J-YYYY2                TO WS-CY
               PERFORM 0700-LOOP THRU
                       0700-LOOP-EXIT
                       UNTIL WS-CY = WS-J-YYYY1
                       OR WS-NBR-5 > 99598.

           IF WS-NBR-5 < 99600
               MOVE WS-NBR-5                  TO LS-NBR
           ELSE
               GO TO 1600-DATE-ERROR.
           GO TO 1700-END-PROCESSING.

       0700-LOOP.
           SUBTRACT 1 FROM WS-CY.
           IF WS-CY < 0001
               GO TO 1600-DATE-ERROR.
           DIVIDE WS-CY BY 4 GIVING WS-QUOTIENT
               REMAINDER WS-REMAIN.
           DIVIDE WS-CY BY 400 GIVING WS-QUOTIENT
               REMAINDER WS-REMAIN2.
           IF WS-CY = WS-J-YYYY1
             IF WS-CY-Y = ZERO
               IF WS-REMAIN2 = ZERO
                   SUBTRACT WS-J-DDD1 FROM 366
                       GIVING WS-NBR
                       ADD WS-NBR             TO WS-NBR-5
                       GO TO 0700-LOOP-EXIT
               ELSE
                   SUBTRACT WS-J-DDD1 FROM 365
                       GIVING WS-NBR
                       ADD WS-NBR             TO WS-NBR-5
                       GO TO 0700-LOOP-EXIT
             ELSE
             IF WS-REMAIN = ZERO
                 SUBTRACT WS-J-DDD1 FROM 366
                     GIVING WS-NBR
                     ADD WS-NBR               TO WS-NBR-5
                     GO TO 0700-LOOP-EXIT
             ELSE
                 SUBTRACT WS-J-DDD1 FROM 365
                     GIVING WS-NBR
                     ADD WS-NBR               TO WS-NBR-5
                     GO TO 0700-LOOP-EXIT.
           IF WS-CY-Y = ZERO
               IF WS-REMAIN2 = ZERO
                   ADD 366                    TO WS-NBR-5
               ELSE
                   ADD 365                    TO WS-NBR-5
           ELSE
           IF WS-REMAIN = ZERO
               ADD 366                        TO WS-NBR-5
           ELSE
               ADD 365                        TO WS-NBR-5.

       0700-LOOP-EXIT. EXIT.

      ****************************************
      *    GIVEN A FUTURE DATE AND A # OF DAYS, RETURN A PAST DATE
      ****************************************
       0800-RETURN-PAST-DATE.
           MOVE LS-DATE-FUTURE                TO WS-HOLD-DATE.
           IF WS-HOLD-8 NUMERIC
               MOVE 'G'                       TO WS-TYPE-DATE-SW.
           MOVE LS-NBR                        TO WS-NBR-5.
           IF WS-NBR-5 > 99599
               GO TO 1600-DATE-ERROR.
           IF GREGORIAN
               PERFORM 0200-CONVERT-TO-JUL-2
           ELSE
               MOVE WS-HOLD-1X7               TO WS-J-DATE2.
           PERFORM 0400-EDIT-J-DATE2.
           MOVE WS-J-YYYY2                    TO WS-CY.
           MOVE WS-J-DDD2                     TO WS-NBR.

       0900-FIND-PAST-DATE.
           IF WS-NBR-5 < WS-NBR
               SUBTRACT WS-NBR-5 FROM WS-NBR
           ELSE
           PERFORM 1000-LOOP2
               UNTIL WS-CNT = WS-NBR-5
               OR WS-CNT > 99599.
           MOVE WS-CY                         TO WS-J-YYYY1.
           MOVE WS-NBR                        TO WS-J-DDD1.
           IF GREGORIAN
               PERFORM 1100-CONVERT-TO-GREG-1
           ELSE
               MOVE WS-J-DATE1                TO LS-DATE-PAST.
           GO TO 1700-END-PROCESSING.

       1000-LOOP2.
           SUBTRACT 1 FROM WS-NBR.
           ADD 1                              TO WS-CNT.
           IF WS-NBR = ZERO
               SUBTRACT 1 FROM WS-CY
               IF WS-CY < 0001
                   GO TO 1600-DATE-ERROR
               ELSE
               IF WS-CY-Y = ZERO
                   DIVIDE WS-CY BY 400 GIVING WS-QUOTIENT
                       REMAINDER WS-REMAIN2
                   IF WS-REMAIN2 = ZERO
                       MOVE 366               TO WS-NBR
                   ELSE
                       MOVE 365               TO WS-NBR
               ELSE
                   DIVIDE WS-CY BY 4 GIVING WS-QUOTIENT
                       REMAINDER WS-REMAIN
                   IF WS-REMAIN = ZERO
                       MOVE 366               TO WS-NBR
                   ELSE
                       MOVE 365               TO WS-NBR.

       1100-CONVERT-TO-GREG-1.
           MOVE WS-J-DDD1                     TO WS-CALL-J-DDD.
           MOVE WS-J-YY1                      TO WS-CALL-J-YY.
           MOVE WS-J-CC1                      TO WS-CALL-J-CC.
           MOVE WS-CALL-JULIAN                TO STV0410B-IN-DATE.
           CALL WS-CALL410B USING WS-STV0410B-FIELDS.
           IF STV0410B-CYMD NUMERIC
               IF STV0410B-CYMD NOT = ZERO
                   MOVE STV0410B-CYMD         TO LS-DATE-PAST
               ELSE
                   GO TO 1600-DATE-ERROR
           ELSE
               GO TO 1600-DATE-ERROR.

      ****************************************
      *    GIVEN A PAST DATE AND A # OF DAYS, RETURN A FUTURE DATE
      ****************************************
       1200-RETURN-FUTURE-DATE.
           MOVE LS-DATE-PAST                  TO WS-HOLD-DATE.
           IF WS-HOLD-8 NUMERIC
               MOVE 'G'                       TO WS-TYPE-DATE-SW.
           MOVE LS-NBR                        TO WS-NBR-5.
           IF WS-NBR-5 > 99599
               GO TO 1600-DATE-ERROR.
           IF GREGORIAN
               PERFORM 0100-CONVERT-TO-JUL-1
           ELSE
               MOVE WS-HOLD-1X7               TO WS-J-DATE1.
           PERFORM 0300-EDIT-J-DATE1.
           MOVE WS-J-YYYY1                    TO WS-CY.
           MOVE WS-J-DDD1                     TO WS-NBR.

       1300-FIND-FUTURE-DATE.
           DIVIDE WS-CY BY 4 GIVING WS-QUOTIENT
               REMAINDER WS-REMAIN.
           DIVIDE WS-CY BY 400 GIVING WS-QUOTIENT
               REMAINDER WS-REMAIN2.
           IF WS-CY-Y = ZERO
               IF WS-REMAIN2 = ZERO
                   MOVE 366                   TO WS-MAX-DAYS
               ELSE
                   MOVE 365                   TO WS-MAX-DAYS
           ELSE
           IF WS-REMAIN = ZERO
               MOVE 366                       TO WS-MAX-DAYS
           ELSE
               MOVE 365                       TO WS-MAX-DAYS.
           ADD WS-NBR-5 TO WS-NBR
               GIVING WS-RESULT.
           IF WS-RESULT < WS-MAX-DAYS
               MOVE WS-RESULT                 TO WS-NBR
           ELSE
               PERFORM 1400-LOOP3
                   UNTIL WS-CNT = WS-NBR-5
                   OR WS-CNT > 99599.
           MOVE WS-CY                         TO WS-J-YYYY2.
           MOVE WS-NBR                        TO WS-J-DDD2.
           IF GREGORIAN
               PERFORM 1500-CONVERT-TO-GREG-2
           ELSE
               MOVE WS-J-DATE2                TO LS-DATE-FUTURE.
           GO TO 1700-END-PROCESSING.

       1400-LOOP3.
           ADD 1 TO WS-NBR WS-CNT.
           IF WS-NBR > WS-MAX-DAYS
               IF WS-CY > 9998
                   GO TO 1600-DATE-ERROR
               ELSE
                   ADD 1 TO WS-CY
                   MOVE 001                   TO WS-NBR
                   IF WS-CY-Y = ZERO
                       DIVIDE WS-CY BY 400 GIVING WS-QUOTIENT
                           REMAINDER WS-REMAIN2
                       IF WS-REMAIN2 = ZERO
                           MOVE 366           TO WS-MAX-DAYS
                       ELSE
                           MOVE 365           TO WS-MAX-DAYS
                   ELSE
                       DIVIDE WS-CY BY 4 GIVING WS-QUOTIENT
                           REMAINDER WS-REMAIN
                       IF WS-REMAIN = ZERO
                           MOVE 366           TO WS-MAX-DAYS
                       ELSE
                           MOVE 365           TO WS-MAX-DAYS.

       1500-CONVERT-TO-GREG-2.
           MOVE WS-J-DDD2                     TO WS-CALL-J-DDD.
           MOVE WS-J-YY2                      TO WS-CALL-J-YY.
           MOVE WS-J-CC2                      TO WS-CALL-J-CC.
           MOVE WS-CALL-JULIAN                TO STV0410B-IN-DATE.
           CALL WS-CALL410B USING WS-STV0410B-FIELDS.
           IF STV0410B-CYMD NUMERIC
               IF STV0410B-CYMD NOT = ZERO
                   MOVE STV0410B-CYMD         TO LS-DATE-FUTURE
               ELSE
                   GO TO 1600-DATE-ERROR
           ELSE
               GO TO 1600-DATE-ERROR.

       1600-DATE-ERROR.
           MOVE '*****'                       TO LS-NBR.
           GO TO 1700-END-PROCESSING.

       1700-END-PROCESSING.
           GOBACK.

      ************************************ END STV0420B
