
       TITLE '  MIS0660B: change offset value'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0660B.
      ******************************************************************
      *@@@ II change offset value for j-jobs
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ******************************************************************
      * Date written: 05/03/2000
      * Author      : Jim Iannone
      *
      *               SEE OFFSET DOCUMENTATION
      *
      *               ALLOW A GLOBAL OVERRIDE TO THE CURRENT J-JOB
      *               PROCESSING DATE.  UPDATE THE STATIC PARM USED
      *               FOR THIS PURPOSE
      *
      *               J-JOBS WILL CHECK THIS OFFSET VALUE AND ADJUST
      *               THE CURRENT PROCESSING DATE ACCORDINGLY.
      *
      *               SET A SWITCH WHEN THE POST-DAYEND JOBS START,
      *               AND ALSO AT THE END OF MID99.
      *               IF SWITCH ON WHEN MID99 RUNS, AUTOMATICALLY
      *               SET OFFSET TO -00001.
      *               RESET SWITCH AT THE END OF MID98.
      *
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT OFFSET               ASSIGN TO OFFSET
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT OFFCHK               ASSIGN TO OFFCHK
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  OFFSET
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  OFFSET-RECORD.
           05  OFFSET-DOCU             PIC  X.
           05  OFFSET-OFFSET.
               10  OFFSET-PAST-FUT     PIC  X.
               10  OFFSET-AMOUNT       PIC  X(05).
           05  FILLER                  PIC  X(10).
           05  OFFSET-BATCH-SW         PIC  X.
           05  FILLER                  PIC  X(62).

       FD  OFFCHK
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  OFFCHK-RECORD.
           05  OFFCHK-DOCU             PIC  X.
           05  OFFCHK-OFFCHK.
               10  OFFCHK-PAST-FUT     PIC  X.
               10  OFFCHK-AMOUNT       PIC  X(05).
           05  FILLER                  PIC  X(10).
           05  OFFCHK-BATCH-SW         PIC  X.
           05  FILLER                  PIC  X(62).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0660B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.

       01  WS-OFFSET-FIELD.
           05  WS-OFFSET               PIC  X(06)    VALUE SPACE.
           05  FILLER REDEFINES WS-OFFSET.
               10  WS-PAST-FUT         PIC  X.
               10  WS-AMOUNT           PIC  X(05).
           05  WS-FILLER               PIC  X(200)   VALUE SPACE.
           05  WS-OFFCHK               PIC  X(06)    VALUE SPACE.
           05  WS-BATCH-SW             PIC  X        VALUE SPACE.

      *    Year 2000 compatible date routine
       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-ERROR      PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-CYMD       PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0660B END  '.

       LINKAGE SECTION.

       01  PARM-FIELD.
           05  PARM-LENGTH             PIC S9(04)    COMP.
           05  PARM-OFFSET-SW          PIC  X.
           05  PARM-FILLER             PIC  X(09).

      /
       PROCEDURE DIVISION USING PARM-FIELD.

       0100-PROGRAM-ENTRY.
           DISPLAY '     '.
           DISPLAY '**** MIS0660B change offset value '.
           PERFORM 0200-OPEN-FILES.
           MOVE '-00000'                      TO WS-OFFSET.
           MOVE SPACE                         TO WS-BATCH-SW.

           IF WS-RETURN-CODE = ZERO
               PERFORM 1100-CHECK-OFFCHK THRU
                       1100-CHECK-OFFCHK-EXIT
               PERFORM 0400-PROCESS THRU
                       0400-PROCESS-EXIT.

           IF WS-PAST-FUT = '-' OR '+'
             IF WS-AMOUNT NUMERIC
               IF WS-OFFSET NOT = '-00000'
                   DISPLAY '---> Warning - dates offset by '
                            WS-OFFSET ' days '
                   DISPLAY '--> Warning - dates offset by '
                            WS-OFFSET ' days ' UPON CONSOLE.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0660B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0660B EOJ encountered '.
           DISPLAY '     '.
           STOP RUN.

       0200-OPEN-FILES.
           OPEN I-O   OFFSET.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0660B open error on OFFSET'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT OFFCHK.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0660B open error on OFFCHK'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0201                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE OFFCHK.
           CLOSE OFFSET.

      *---------------------------------------
      *    accept offset override from console
      *
      *    send different message number for CA-Opera,
      *    which responds 'SKIP' or 'RESET' automatically,
      *    if the operator has not manually responded.
      *
      *    PARM-OFFSET-SW   S             = SET
      *                     anything else = RESET
      *+--------------------------------------
       0400-PROCESS.
           IF PARM-OFFSET-SW = 'S'
             IF WS-BATCH-SW = 'Y'
                 MOVE '-00001'                TO WS-OFFSET
                 GO TO 0400-SKIP-MSG
             ELSE
                 DISPLAY 'CAWU180I  Enter J-JOB date offset: '
                          UPON CONSOLE
                 DISPLAY 'CAWU182I  SKIP or -##### or Y=yesterday '
                          UPON CONSOLE
           ELSE
               DISPLAY 'CAWU181I  Enter J-JOB date offset: '
                        UPON CONSOLE
               DISPLAY 'CAWU183I  RESET or -##### or SKIP '
                        UPON CONSOLE.
           ACCEPT WS-OFFSET FROM CONSOLE.
           IF WS-OFFSET = 'Y     '
               MOVE '-00001'                  TO WS-OFFSET.

       0400-SKIP-MSG.
           IF WS-PAST-FUT = '-' OR '+'
             IF WS-AMOUNT NUMERIC
               IF WS-OFFSET = WS-OFFCHK
                   CONTINUE
               ELSE
                   PERFORM 1200-CHANGE-OFFSET THRU
                           1200-CHANGE-OFFSET-EXIT
             ELSE
                 DISPLAY 'CAWU151I Offset not valid - reenter'
                          UPON CONSOLE
                 GO TO 0400-PROCESS
           ELSE
           IF WS-OFFSET = 'RESET'
               MOVE '-00000'                  TO WS-OFFSET
               IF WS-OFFSET = WS-OFFCHK
                   CONTINUE
               ELSE
                   PERFORM 1200-CHANGE-OFFSET THRU
                           1200-CHANGE-OFFSET-EXIT
                   DISPLAY 'CAWU151I offset reset to -00000'
                            UPON CONSOLE
           ELSE
           IF WS-OFFSET = 'SKIP'
               DISPLAY 'CAWU151I offset skipped'
                        UPON CONSOLE.

       0400-PROCESS-EXIT. EXIT.

      *---------------------------------------
      *    check existing offset in static parm
      *+--------------------------------------
       1100-CHECK-OFFCHK.
           READ OFFCHK.
           IF WS-STAT = 10
               GO TO 1100-CHECK-OFFCHK-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0660B read error on OFFCHK'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +1100                     TO WS-RETURN-CODE
               GO TO 1100-CHECK-OFFCHK-EXIT.

           IF OFFCHK-DOCU = '*'
               GO TO 1100-CHECK-OFFCHK.
           IF OFFCHK-AMOUNT NUMERIC
               MOVE OFFCHK-OFFCHK             TO WS-OFFCHK
               MOVE OFFCHK-BATCH-SW           TO WS-BATCH-SW
           ELSE
               DISPLAY '**** OFFCHK amount not numeric '
               MOVE +1101                     TO WS-RETURN-CODE
               GO TO 1100-CHECK-OFFCHK-EXIT.

       1100-CHECK-OFFCHK-EXIT. EXIT.

      *---------------------------------------
      *    change offset in static parm
      *+--------------------------------------
       1200-CHANGE-OFFSET.
           READ OFFSET.
           IF WS-STAT = 10
               GO TO 1200-CHANGE-OFFSET-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0660B read error on OFFSET'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +1200                     TO WS-RETURN-CODE
               GO TO 1200-CHANGE-OFFSET-EXIT.

           IF OFFSET-DOCU = '*'
               GO TO 1200-CHANGE-OFFSET.
           IF OFFSET-AMOUNT NUMERIC
               MOVE WS-OFFSET                 TO OFFSET-OFFSET
               PERFORM 2000-REWRITE-OFFSET
           ELSE
               DISPLAY '**** Offset amount not numeric '
               MOVE +1201                     TO WS-RETURN-CODE
               GO TO 1200-CHANGE-OFFSET-EXIT.
           IF WS-BATCH-SW = 'Y'
               DISPLAY 'CAWU157I parm MIS10010 date offset flag = Y  '
                        UPON CONSOLE.

       1200-CHANGE-OFFSET-EXIT. EXIT.

       2000-REWRITE-OFFSET.
           REWRITE OFFSET-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0660B rewrite error on OFFSET'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +2000                     TO WS-RETURN-CODE.

      ************************************ END MIS0660B
