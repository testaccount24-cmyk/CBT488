
       TITLE '  STV1295B: return the name of the month'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV1295B.
       AUTHOR.        JIM IANNONE.
      ******************************************************************
      *@@@ II call program: given 19910101 return 'January 1st 1991'
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      *****************************************************************
      * Program-id  : STV1295B
      * Project #   : 910663
      * Date written: 07/01/91
      * Programmer  : Jim Iannone
      *
      *               GIVEN A FULL NUMERIC DATE, RETURN THE MONTH NAME
      *               AND DAY SUFFIX SUCH AS 'ST' 'ND' 'RD' 'TH'.
      *
      *               NOTE: MONTH MAY BE SENT ALONE TO RETURN ONLY A
      *                     MONTH NAME - LEAVE DD AND CCYY SPACES.
      *               NOTE: IF DAY = SPACE, REPLACE WITH END OF MONTH
      *                     NBR (EX: IF MONTH = 12, DAY = 31).
      *               NOTE: ANY ERROR WILL CAUSE '****' TO BE
      *                     PLACED IN LS-YEAR.
      *
      *                                  BEFORE:        AFTER:
      *               LS-CCYYMMDD        '19910702'     '19910702'
      *               LS-MONTH           SPACE          'JULY        '
      *               LS-DAY             SPACE          ' 2ND'
      *               LS-YEAR            SPACE          '1991'
      *
      ******************************************************************

       ENVIRONMENT DIVISION.
       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING-STORAGE STV1295B START '.

       01  WORK-FIELDS.
           05  WS-CCYYMMDD             PIC  X(08).
           05  FILLER REDEFINES WS-CCYYMMDD.
               10  WS-CCYY             PIC  9(04).
               10  FILLER REDEFINES WS-CCYY.
                   15  WS-CCYY-C       PIC  99.
                   15  WS-CCYY-Y       PIC  99.
               10  WS-MM               PIC  99.
               10  WS-DD               PIC  99.
           05  FILLER REDEFINES WS-CCYYMMDD.
               10  WS-CCYY-X           PIC  X(04).
               10  WS-MM-X             PIC  XX.
               10  WS-DD-X             PIC  XX.
           05  WS-DAY.
               10  WS-DAY-NBR          PIC  XX.
               10  FILLER REDEFINES WS-DAY-NBR.
                   15  WS-DAY-NBR-1    PIC  X.
                   15  WS-DAY-NBR-2    PIC  X.
               10  WS-DAY-SUFFIX       PIC  XX.
           05  WS-QUOTIENT             PIC  999      VALUE ZERO.
           05  WS-REMAINDER            PIC  999      VALUE ZERO.
           05  WS-REMAIN2              PIC  9(04)    VALUE ZERO.

       01  WS-TABLES.
           05  WS-MONTH-TABLE1.
               10  FILLER        PIC  X(14)    VALUE 'JANUARY     31'.
               10  FILLER        PIC  X(14)    VALUE 'FEBRUARY    28'.
               10  FILLER        PIC  X(14)    VALUE 'MARCH       31'.
               10  FILLER        PIC  X(14)    VALUE 'APRIL       30'.
               10  FILLER        PIC  X(14)    VALUE 'MAY         31'.
               10  FILLER        PIC  X(14)    VALUE 'JUNE        30'.
               10  FILLER        PIC  X(14)    VALUE 'JULY        31'.
               10  FILLER        PIC  X(14)    VALUE 'AUGUST      31'.
               10  FILLER        PIC  X(14)    VALUE 'SEPTEMBER   30'.
               10  FILLER        PIC  X(14)    VALUE 'OCTOBER     31'.
               10  FILLER        PIC  X(14)    VALUE 'NOVEMBER    30'.
               10  FILLER        PIC  X(14)    VALUE 'DECEMBER    31'.
           05  WS-MONTH-TABLE2 REDEFINES WS-MONTH-TABLE1.
               10  FILLER              OCCURS 12 TIMES.
                   15  WS-MONTH        PIC  X(12).
                   15  WS-EOM-DAY      PIC  XX.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  STV1295B END  '.

       LINKAGE SECTION.

       01  LS-RECORD.
           05  LS-CCYYMMDD             PIC  X(08).
           05  LS-MONTH                PIC  X(12).
           05  LS-DAY                  PIC  X(04).
           05  LS-YEAR                 PIC  X(04).

       PROCEDURE DIVISION USING LS-RECORD.

       0100-PROGRAM-ENTRY.
      ****************************************
      *    RESET FIELDS IN CASE OF MULTIPLE CALLS BY SAME PROGRAM
      ****************************************
           MOVE SPACE                  TO LS-MONTH LS-DAY
                                          LS-YEAR.
           MOVE ZERO                   TO WS-CCYYMMDD WS-QUOTIENT
                                          WS-REMAINDER.
      ****************************************
      *    MONTH MAY BE ENTERED ALONE: ONLY A MONTH NAME IS RETURNED
      ****************************************
           MOVE LS-CCYYMMDD                   TO WS-CCYYMMDD.
           IF WS-MM-X NOT NUMERIC
               GO TO 0200-EXIT-ERROR.
           IF WS-MM = ZERO
               OR WS-MM > 12
               GO TO 0200-EXIT-ERROR.
           IF (WS-DD-X = SPACE)
               AND (WS-CCYY-X = SPACE)
               MOVE WS-MONTH (WS-MM)          TO LS-MONTH
               GO TO 0300-EXIT-PROGRAM.

      ****************************************
      *    IF WS-DD = SPACE, MOVE THE CORRESPONDING END OF MONTH NBR
      *    TO WS-DD.
      ****************************************
           IF WS-CCYY-X NOT NUMERIC
               GO TO 0200-EXIT-ERROR.
           IF WS-DD-X = SPACE
               DIVIDE WS-CCYY BY 4 GIVING WS-QUOTIENT
                   REMAINDER WS-REMAINDER
               IF WS-MM = 02
                   IF WS-REMAINDER = ZERO
                     IF WS-CCYY-Y = ZERO
                         DIVIDE WS-CCYY BY 400 GIVING WS-QUOTIENT
                             REMAINDER WS-REMAIN2
                         IF WS-REMAIN2 = ZERO
                             MOVE '29'        TO WS-DD-X
                         ELSE
                             CONTINUE
                     ELSE
                         MOVE '29'            TO WS-DD-X.

           IF WS-DD-X = SPACE
               MOVE WS-EOM-DAY (WS-MM)        TO WS-DD-X.

      ****************************************
      *    FILL OUTPUT FIELDS WITH CONVERTED DATE NAMES.
      ****************************************
           MOVE WS-MONTH (WS-MM)              TO LS-MONTH.

           MOVE WS-DD                         TO WS-DAY-NBR.
           IF WS-DD = 01 OR 21 OR 31
               MOVE 'ST'                      TO WS-DAY-SUFFIX
           ELSE
           IF WS-DD = 02 OR 22
               MOVE 'ND'                      TO WS-DAY-SUFFIX
           ELSE
           IF WS-DD = 03 OR 23
               MOVE 'RD'                      TO WS-DAY-SUFFIX
           ELSE
               MOVE 'TH'                      TO WS-DAY-SUFFIX.
           IF WS-DAY-NBR-1 = '0'
               MOVE SPACE                     TO WS-DAY-NBR-1.
           MOVE WS-DAY                        TO LS-DAY.
           MOVE WS-CCYY                       TO LS-YEAR.
           MOVE WS-CCYYMMDD                   TO LS-CCYYMMDD.
           GO TO 0300-EXIT-PROGRAM.

       0200-EXIT-ERROR.
           MOVE '****'                        TO LS-YEAR.

       0300-EXIT-PROGRAM.
           GOBACK.

      ************************************ END STV1295B

