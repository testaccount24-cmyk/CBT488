
       TITLE '  STV0405B: edit an 8 character CYMD date'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV0405B.
       AUTHOR.        JIM IANNONE.
      ******************************************************************
      *@@@ C3 edit an 8 character CYMD or CYDDD date
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ****************************************************************
      * Program-id  : STV0405B
      * Project #   : 930105 for STV0305B/STV0205B
      * Date written: 03/11/98
      * Programmer  : Jim Iannone
      *               This program accepts a date in the form CYMD
      *               and edits the date.  It will place '**' in any
      *               CC, YY, MM, or DD field which is invalid.
      *
      *               Note:  if date is received as '19925 6 ' (perhaps
      *               because an examine turned underscores into spaces
      *               in an online field) the date is returned as
      *               '19920506'.
      *
      *               recompiled in OS390 Cobol from STV0205B
      ******************************************************************
      * Date changed: 04/07/2006
      * Programmer  : Jim Iannone
      *               compile with Cobol 3 with DYNAM option
      ******************************************************************

       ENVIRONMENT DIVISION.
       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING-STORAGE STV0405B START '.

       01  WS-DATE-FIELDS.
           05  WS-8-DATE               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-8-DATE.
               10  WS-8-CC             PIC  XX.
               10  WS-8-YY             PIC  XX.
               10  WS-8-MM             PIC  XX.
               10  WS-8-DD             PIC  XX.
           05  FILLER REDEFINES WS-8-DATE.
               10  WS-8-C1             PIC  X.
               10  WS-8-C2             PIC  X.
               10  WS-8-Y1             PIC  X.
               10  WS-8-Y2             PIC  X.
               10  WS-8-M1             PIC  X.
               10  WS-8-M2             PIC  X.
               10  WS-8-D1             PIC  X.
               10  WS-8-D2             PIC  X.
           05  FILLER REDEFINES WS-8-DATE.
               10  WS-8-CY             PIC  X(04).
               10  FILLER              PIC  X(04).
           05  WS-8-DATE-N             PIC  9(08)    VALUE ZERO.
           05  FILLER REDEFINES WS-8-DATE-N.
               10  WS-8-CC-N           PIC  99.
               10  WS-8-YY-N           PIC  99.
               10  WS-8-MM-N           PIC  99.
               10  WS-8-DD-N           PIC  99.
           05  FILLER REDEFINES WS-8-DATE-N.
               10  WS-8-CY-N           PIC  9(04).
               10  FILLER              PIC  9(04).
           05  WS-QUOTIENT             PIC  999     VALUE ZERO.
           05  WS-REMAINDER            PIC  99      VALUE ZERO.
           05  WS-REMAIN2              PIC  9(04)   VALUE ZERO.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  END STV0405B  '.

       LINKAGE SECTION.

       01  LS-FIELDS.
           05  LS-DATE                 PIC  X(08).

       PROCEDURE DIVISION USING LS-FIELDS.

       0100-START-PROCESS.
           MOVE ZERO                          TO WS-8-DATE-N.
           MOVE LS-DATE                       TO WS-8-DATE.

           IF WS-8-M1 = SPACE
               MOVE ZERO                      TO WS-8-M1.
           IF WS-8-M2 = SPACE
               MOVE WS-8-M1                   TO WS-8-M2
               MOVE ZERO                      TO WS-8-M1.
           IF WS-8-D1 = SPACE
               MOVE ZERO                      TO WS-8-D1.
           IF WS-8-D2 = SPACE
               MOVE WS-8-D1                   TO WS-8-D2
               MOVE ZERO                      TO WS-8-D1.

           IF WS-8-CC NOT NUMERIC
               MOVE '**'                      TO WS-8-CC
           ELSE
               MOVE WS-8-CC                   TO WS-8-CC-N.

           IF WS-8-YY NOT NUMERIC
               MOVE '**'                      TO WS-8-YY
           ELSE
               MOVE WS-8-YY                   TO WS-8-YY-N.

           IF WS-8-MM NOT NUMERIC
               MOVE '**'                      TO WS-8-MM
           ELSE
               MOVE WS-8-MM                   TO WS-8-MM-N
               IF WS-8-MM-N < 1
                   OR WS-8-MM-N > 12
                   MOVE '**'                  TO WS-8-MM.

           IF WS-8-DD NOT NUMERIC
               MOVE '**'                      TO WS-8-DD
           ELSE
               MOVE WS-8-DD                   TO WS-8-DD-N
               IF WS-8-DD-N < 1
                   OR WS-8-DD-N > 31
                   MOVE '**'                  TO WS-8-DD.

           IF WS-8-MM = '04' OR '06' OR '09' OR '11'
               IF WS-8-DD > '30'
                   MOVE '**'                  TO WS-8-DD.
           IF WS-8-MM = '02'
               IF WS-8-DD > '29'
                   MOVE '**'                  TO WS-8-DD.

           IF WS-8-MM = '**'
               OR WS-8-DD = '**'
               OR WS-8-YY = '**'
               GO TO 0400-EXIT-PROGRAM.

           IF WS-8-MM-N = 02
               DIVIDE WS-8-YY-N BY 4 GIVING WS-QUOTIENT
                   REMAINDER WS-REMAINDER
               IF WS-REMAINDER = ZERO
                   IF WS-8-YY-N = ZERO
                       DIVIDE WS-8-CY-N BY 400 GIVING WS-QUOTIENT
                           REMAINDER WS-REMAIN2
                       IF WS-REMAIN2 NOT = ZERO
                         IF WS-8-DD-N > 28
                             MOVE '**'        TO WS-8-DD.

           IF WS-8-MM-N = 02
               IF WS-REMAINDER NOT = ZERO
                    IF WS-8-DD-N > 28
                        MOVE '**'             TO WS-8-DD.

       0400-EXIT-PROGRAM.
           MOVE WS-8-DATE                     TO LS-DATE.
           GOBACK.

      ************************************ END STV0405B
