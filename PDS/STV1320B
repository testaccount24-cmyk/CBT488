
       TITLE '  STV1320B: Cobol II time conversion'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV1320B.
       AUTHOR.        JIM IANNONE.
      ******************************************************************
      *@@@ II call program to convert military/standard time
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      *****************************************************************
      * Program-id  : STV1320B
      * Project #   : 901030
      * Date written: 10/08/90
      * Programmer  : Jim Iannone
      *
      *               THIS PROGRAM WILL ACCEPT EITHER A 12 HR OR 24 HR
      *               TIME PARAMETER AND RETURN THE OTHER TIME.
      *               THE TIME IS ACCEPTED AS 'HHMMAM' OR 'HHMMPM' FOR
      *               STANDARD TIME OR 'HHMMSS' FOR MILITARY TIME. IF
      *               THERE IS ANY ERROR FOUND, THE ORIGINAL TIME IS
      *               RETURNED UNCHANGED.
      ******************************************************************

       ENVIRONMENT DIVISION.
       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING-STORAGE STV1320B START '.

       01  WS-FIELDS.
           05  WS-TIME                               VALUE SPACE.
               10  WS-HH               PIC  XX.
               10  WS-MM               PIC  XX.
               10  WS-SS               PIC  XX.
           05  WS-NEW-TIME                           VALUE SPACE.
               10  WS-NEW-HH           PIC  XX.
               10  WS-NEW-MM           PIC  XX.
               10  WS-NEW-SS           PIC  XX.
           05  WS-HH-NUMERIC           PIC  99       VALUE ZERO.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  END STV1320B  '.

       LINKAGE SECTION.

       01  LS-CALC-TIME.
           05  FILLER                  PIC  X(06).

       PROCEDURE DIVISION USING LS-CALC-TIME.

      ****************************************
      *    IF 'AM' OR 'PM' ARE FOUND, PROCESS AS A MILITARY
      *    CONVERSION, ELSE PROCESS AS A STANDARD CONVERSION.
      ****************************************
       0000-START-PROCESS.
           MOVE SPACE                         TO WS-TIME
                                                 WS-NEW-TIME.
           MOVE LS-CALC-TIME                  TO WS-TIME.
           IF WS-HH NOT NUMERIC
               OR WS-MM NOT NUMERIC
               GO TO 0300-END-PROCESS.
           IF WS-HH > '23'
               OR WS-MM > '59'
               GO TO 0300-END-PROCESS.
           IF WS-SS = 'AM' OR 'PM'
               GO TO 0100-CALC-TO-MILITARY.

           MOVE WS-MM                         TO WS-NEW-MM.
           IF WS-HH > '12'
               MOVE WS-HH                     TO WS-HH-NUMERIC
               SUBTRACT 12 FROM WS-HH-NUMERIC
               MOVE WS-HH-NUMERIC             TO WS-NEW-HH
               MOVE 'PM'                      TO WS-NEW-SS
               GO TO 0200-MOVE-TIME.
           MOVE WS-HH                         TO WS-NEW-HH.
           IF WS-HH < '12'
               MOVE 'AM'                      TO WS-NEW-SS.
           IF WS-HH = '12'
               MOVE 'PM'                      TO WS-NEW-SS.
           IF WS-HH = ZERO
               MOVE '12'                      TO WS-NEW-HH.
           GO TO 0200-MOVE-TIME.

       0100-CALC-TO-MILITARY.
           IF WS-HH > '12'
               OR WS-HH = ZERO
               GO TO 0300-END-PROCESS.
           MOVE ZERO                          TO WS-NEW-SS.
           MOVE WS-MM                         TO WS-NEW-MM.
           IF WS-SS = 'PM'
               IF WS-HH = '12'
                   MOVE WS-HH                 TO WS-NEW-HH
                   GO TO 0200-MOVE-TIME
               ELSE
                   MOVE WS-HH                 TO WS-HH-NUMERIC
                   ADD 12                     TO WS-HH-NUMERIC
                   MOVE WS-HH-NUMERIC         TO WS-NEW-HH
                   GO TO 0200-MOVE-TIME
           ELSE
               MOVE WS-HH                     TO WS-NEW-HH
               IF WS-NEW-HH = '12'
                   MOVE ZERO                  TO WS-NEW-HH.

       0200-MOVE-TIME.
           MOVE WS-NEW-TIME                   TO LS-CALC-TIME.

       0300-END-PROCESS.
           GOBACK.

      ************************************ END STV1320B

