
       TITLE '  MIS0590B: build SMTP commands'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0590B.
      ******************************************************************
      *@@@ C3 build SMTP commands
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ******************************************************************
      * Date written: 09/20/99
      * Author      : Jim Iannone
      *               build SMTP commands to send data on
      *               specified days
      ******************************************************************
      * Date changed: 01/07/2010  Jim Iannone
      *               corrections to logic when flag = space
      *****************************************************************
      * Date changed: 11/27/2019  Jim Iannone
      *               add logic to format 'Date: ' command
      *****************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT OUTSMTP              ASSIGN TO OUTSMTP
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT PARM01               ASSIGN TO PARM01
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.


       DATA DIVISION.
       FILE SECTION.

       FD  OUTSMTP
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  OUTSMTP-RECORD.
           05  OUTSMTP-RCD             PIC  X(80).

       FD  PARM01
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  PARM01-RECORD.
           05  PARM01-DOCU             PIC  X.
           05  PARM01-CODE             PIC  XXX.
           05  FILLER                  PIC  X.
           05  PARM01-SMTP             PIC  X(50).
           05  FILLER                  PIC  X.
           05  PARM01-DAYS             PIC  X(07).
           05  FILLER                  PIC  X.
           05  PARM01-FLAG             PIC  X.
           05  FILLER                  PIC  X(07).
           05  FILLER                  PIC  X(08).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0590B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-TALLY                PIC S9(04)    VALUE ZERO COMP.
           05  WS-HOLD-DAY             PIC  X        VALUE SPACE.
           05  WS-HOLD-FLD1            PIC  X(50)    VALUE SPACE.
           05  WS-HOLD-FLD2            PIC  X(50)    VALUE SPACE.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.
           05  WS-STR1                 PIC  X(10)    VALUE SPACE.
           05  WS-STR2                 PIC  XX       VALUE SPACE.
           05  WS-STR3                 PIC  X(10)    VALUE SPACE.
           05  WS-STR4                 PIC  X(04)    VALUE SPACE.
           05  WS-STR5                 PIC  XX       VALUE SPACE.
           05  WS-STR6                 PIC  XX       VALUE SPACE.
           05  WS-STR7                 PIC  XX       VALUE SPACE.
           05  WS-STR8                 PIC  X(05)    VALUE SPACE.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-WRITE-RCD-CNT        PIC S9(07)    VALUE ZERO.

       01  WS-TABLES.
           05  WS-PARM-TABLE.
               10  WS-LIST             OCCURS 40 TIMES.
                   15  WS-CODE         PIC  XXX.
                   15  WS-SMTP.
                       20  WS-RCPT     PIC  X(05).
                       20  FILLER      PIC  X(45).
                   15  WS-DAYS         PIC  X(07).
                   15  FILLER REDEFINES WS-DAYS.
                       20  WS-DAY      PIC  X
                                       OCCURS 7 TIMES.
                   15  WS-FLAG         PIC  X.

       01  WS-SWITCHES.
           05  WS-NOTIFY-SW            PIC  X        VALUE 'Y'.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(05)    VALUE ZERO.
           05  WS-SUB2                 PIC S9(05)    VALUE ZERO.
           05  WS-PARM01-MAX           PIC S9(05)    VALUE ZERO.

       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0590B END  '.

       LINKAGE SECTION.

       01  PARM-FIELD.
           05  PARM-LENGTH             PIC S9(04)    COMP.
           05  PARM-CODE               PIC  XXX.
           05  FILLER                  PIC  X(17).

      /
       PROCEDURE DIVISION USING PARM-FIELD.

       0100-PROGRAM-ENTRY.
           DISPLAY '    '.
           DISPLAY '**** MIS0590B build SMTP commands '.
           PERFORM 0200-OPEN-FILES.

           PERFORM VARYING WS-SUB1 FROM 1 BY 1
               UNTIL WS-SUB1 > 40
               MOVE SPACE                 TO WS-CODE(WS-SUB1)
               MOVE SPACE                 TO WS-SMTP(WS-SUB1)
               MOVE SPACE                 TO WS-DAYS(WS-SUB1)
               MOVE SPACE                 TO WS-FLAG(WS-SUB1)
           END-PERFORM.
           IF WS-RETURN-CODE = ZERO
               MOVE ZERO                  TO WS-SUB1
               PERFORM 0350-LOAD-PARM01 THRU
                       0350-LOAD-PARM01-EXIT.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-PARM.

           DISPLAY '**** Records written     ' WS-WRITE-RCD-CNT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0590B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0590B EOJ encountered '.
           DISPLAY '    '.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  PARM01.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0590B open error on PARM01'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT OUTSMTP.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0590B open error on OUTSMTP'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE OUTSMTP.
           CLOSE PARM01.

       0350-LOAD-PARM01.
           READ PARM01.
           IF WS-STAT = 10
               MOVE WS-SUB1                   TO WS-PARM01-MAX
               GO TO 0350-LOAD-PARM01-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0590B read error on PARM01'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0350                     TO WS-RETURN-CODE
               GO TO 0350-LOAD-PARM01-EXIT.

           IF PARM01-DOCU = '*'
               GO TO 0350-LOAD-PARM01.
           IF PARM-CODE NOT = PARM01-CODE
               GO TO 0350-LOAD-PARM01.
           IF WS-SUB1 < 40
               ADD 1                          TO WS-SUB1
               MOVE PARM01-CODE               TO WS-CODE(WS-SUB1)
               MOVE PARM01-SMTP               TO WS-SMTP(WS-SUB1)
               PERFORM 0400-ALTER-SMTP-CASE
               MOVE PARM01-DAYS               TO WS-DAYS(WS-SUB1)
               MOVE PARM01-FLAG               TO WS-FLAG(WS-SUB1).

           GO TO 0350-LOAD-PARM01.

       0350-LOAD-PARM01-EXIT. EXIT.

       0400-ALTER-SMTP-CASE.
           MOVE SPACE                         TO WS-HOLD-FLD1.
           MOVE SPACE                         TO WS-HOLD-FLD2.
           INSPECT WS-SMTP(WS-SUB1)(1: 4)     CONVERTING
                   'abcdefghijklmnopqrstuvwxyz' TO
                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.
           IF WS-SMTP(WS-SUB1)(1: 4) = 'AUTH' OR 'DATA' OR 'EHLO'
                                    OR 'HELO' OR 'HELP' OR 'NOOP'
                                    OR 'QUIT' OR 'RSET' OR 'SIZE'
                                    OR 'STAR' OR 'VRFY' OR '.'
               CONTINUE
           ELSE
               UNSTRING WS-SMTP(WS-SUB1)      DELIMITED BY ':'
                   INTO WS-HOLD-FLD1 WS-HOLD-FLD2
               INSPECT WS-HOLD-FLD1           CONVERTING
                   'abcdefghijklmnopqrstuvwxyz' TO
                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
               IF WS-HOLD-FLD1 = 'MAIL FROM' OR 'RCPT TO'
                   IF WS-HOLD-FLD2(1: 1) = SPACE
                       STRING WS-HOLD-FLD1    DELIMITED BY '  '
                              ':'             DELIMITED BY SIZE
                              WS-HOLD-FLD2    DELIMITED BY SIZE
                              INTO WS-SMTP(WS-SUB1)
                   ELSE
                       STRING WS-HOLD-FLD1    DELIMITED BY '  '
                              ': '            DELIMITED BY SIZE
                              WS-HOLD-FLD2    DELIMITED BY SIZE
                              INTO WS-SMTP(WS-SUB1)
               ELSE
               IF WS-HOLD-FLD1 = 'SUBJECT' OR 'DATE' OR 'FROM'
                              OR 'TO'
                   INSPECT WS-HOLD-FLD1(2: )      CONVERTING
                       'ABCDEFGHIJKLMNOPQRSTUVWXYZ' TO
                       'abcdefghijklmnopqrstuvwxyz'
                   IF WS-HOLD-FLD2(1: 1) = SPACE
                       STRING WS-HOLD-FLD1        DELIMITED BY SPACE
                              ':'                 DELIMITED BY SIZE
                              WS-HOLD-FLD2        DELIMITED BY SIZE
                              INTO WS-SMTP(WS-SUB1)
                   ELSE
                       STRING WS-HOLD-FLD1        DELIMITED BY SPACE
                              ': '                DELIMITED BY SIZE
                              WS-HOLD-FLD2        DELIMITED BY SIZE
                              INTO WS-SMTP(WS-SUB1)
               ELSE
               IF WS-HOLD-FLD1 = 'REPLY-TO'
                   STRING 'Reply-To'          DELIMITED BY SIZE
                          ': '                DELIMITED BY SIZE
                          WS-HOLD-FLD2        DELIMITED BY SIZE
                          INTO WS-SMTP(WS-SUB1).

       0400-PROCESS-PARM.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE 'DAY   '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-DAY-NBR              TO WS-HOLD-DAY.
      *---------------------------------------
      *    Check to see if any recipient records will qualify...
      *    if none, skip writing any records.
      *---------------------------------------
           MOVE 'N'                           TO WS-NOTIFY-SW.
           PERFORM 1200-INITIAL-TEST
               VARYING WS-SUB1 FROM 1 BY 1
               UNTIL WS-SUB1 > WS-PARM01-MAX.

           IF WS-NOTIFY-SW = 'N'
               DISPLAY '**** MIS0590B no recipients selected'
               MOVE +0004                     TO WS-RETURN-CODE.
      *---------------------------------------
      *    if WS-RETURN-CODE = ZERO then a recipient
      *    or END record qualified, so loop through
      *    all records for this code and generate SMTP records.
      *---------------------------------------
           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-BUILD-OUTSMTP THRU
                       0600-BUILD-EXIT
                       VARYING WS-SUB1 FROM 1 BY 1
                       UNTIL WS-SUB1 > WS-PARM01-MAX.

       0600-BUILD-OUTSMTP.
           IF WS-FLAG(WS-SUB1) = 'N'
               CONTINUE
           ELSE
           IF WS-FLAG(WS-SUB1) = 'Y'
               PERFORM 0700-SEND-OUTSMTP
           ELSE
           IF WS-DAYS(WS-SUB1) = SPACE
               PERFORM 0700-SEND-OUTSMTP
           ELSE
               MOVE 'N'                       TO WS-NOTIFY-SW
               PERFORM 1200-TEST-DAYS
                   VARYING WS-SUB2 FROM 1 BY 1
                   UNTIL WS-SUB2 > 7
               IF WS-NOTIFY-SW = 'Y'
                   PERFORM 0700-SEND-OUTSMTP.

       0600-BUILD-EXIT. EXIT.

       0700-SEND-OUTSMTP.
           MOVE SPACE                         TO OUTSMTP-RCD.
           MOVE WS-SMTP(WS-SUB1)              TO OUTSMTP-RCD.
           IF OUTSMTP-RCD(1: 4) = 'QUIT'
               DISPLAY '**** END record '
           ELSE
           IF OUTSMTP-RCD(1: 4) = 'Date'
               PERFORM 1300-BUILD-DATE-LINE
               DISPLAY '**** ' WS-CODE(WS-SUB1)
                       ' date            '  WS-HOLD-FLD1
           ELSE
           IF OUTSMTP-RCD(1: 4) = 'RCPT'
               MOVE ZERO                      TO WS-TALLY
               INSPECT OUTSMTP-RCD TALLYING WS-TALLY FOR ALL '<'
               IF WS-TALLY > ZERO
                   MOVE SPACE                 TO WS-HOLD-FLD1
                   MOVE SPACE                 TO WS-HOLD-FLD2
                   UNSTRING OUTSMTP-RCD       DELIMITED BY '<'
                       INTO WS-HOLD-FLD1      WS-HOLD-FLD2
                   INSPECT WS-HOLD-FLD2 REPLACING ALL '>' BY SPACE
                   DISPLAY '**** ' WS-CODE(WS-SUB1) ' recipient       '
                                   WS-HOLD-FLD2
               ELSE
                   MOVE SPACE                 TO WS-HOLD-FLD1
                   MOVE SPACE                 TO WS-HOLD-FLD2
                   UNSTRING OUTSMTP-RCD       DELIMITED BY ':'
                       INTO WS-HOLD-FLD1      WS-HOLD-FLD2
                   DISPLAY '**** ' WS-CODE(WS-SUB1) ' recipient       '
                                   WS-HOLD-FLD2.

           PERFORM 2000-WRITE-OUTSMTP.

       1200-INITIAL-TEST.
           IF WS-CODE(WS-SUB1) = 'END'
               MOVE 'Y'                       TO WS-NOTIFY-SW
           ELSE
           IF WS-RCPT(WS-SUB1) = 'RCPT '
             IF WS-FLAG(WS-SUB1) = 'N'
                 CONTINUE
             ELSE
             IF WS-FLAG(WS-SUB1) = 'Y'
                 MOVE 'Y'                     TO WS-NOTIFY-SW
             ELSE
             IF WS-DAYS(WS-SUB1) = SPACE
                 AND WS-FLAG(WS-SUB1) = SPACE
                 MOVE 'Y'                     TO WS-NOTIFY-SW
             ELSE
                 PERFORM 1200-TEST-DAYS
                     VARYING WS-SUB2 FROM 1 BY 1
                     UNTIL WS-SUB2 > 7.

       1200-TEST-DAYS.
           IF WS-DAY(WS-SUB1 WS-SUB2) = WS-HOLD-DAY
               MOVE 'Y'                       TO WS-NOTIFY-SW.

      *    Date: Thu, 21 May 2008 05:33:29 -0700
      *
       1300-BUILD-DATE-LINE.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE 'DAY   '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-DATA(1: 3)           TO WS-STR1.
           INSPECT WS-STR1(2: )                 CONVERTING
                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ' TO
                   'abcdefghijklmnopqrstuvwxyz'.
           MOVE FUNCTION CURRENT-DATE(7: 2)   TO WS-STR2.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE 'MONTH '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-DATA                 TO WS-STR3.
           INSPECT WS-STR3(2: )                 CONVERTING
                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ' TO
                   'abcdefghijklmnopqrstuvwxyz'.
           MOVE FUNCTION CURRENT-DATE(1: 4)   TO WS-STR4.
           MOVE FUNCTION CURRENT-DATE(9: 2)   TO WS-STR5.
           MOVE FUNCTION CURRENT-DATE(11: 2)  TO WS-STR6.
           MOVE FUNCTION CURRENT-DATE(13: 2)  TO WS-STR7.
           MOVE FUNCTION CURRENT-DATE(17: 5)  TO WS-STR8.

           MOVE SPACE                         TO OUTSMTP-RCD.
           MOVE SPACE                         TO WS-HOLD-FLD1.
           STRING WS-STR1                     DELIMITED BY SPACE
                  ', '                        DELIMITED BY SIZE
                  WS-STR2                     DELIMITED BY SIZE
                  ' '                         DELIMITED BY SIZE
                  WS-STR3                     DELIMITED BY SPACE
                  ' '                         DELIMITED BY SIZE
                  WS-STR4                     DELIMITED BY SIZE
                  ' '                         DELIMITED BY SIZE
                  WS-STR5                     DELIMITED BY SIZE
                  ':'                         DELIMITED BY SIZE
                  WS-STR6                     DELIMITED BY SIZE
                  ':'                         DELIMITED BY SIZE
                  WS-STR7                     DELIMITED BY SIZE
                  ' '                         DELIMITED BY SIZE
                  WS-STR8                     DELIMITED BY SIZE
                  INTO WS-HOLD-FLD1.
           STRING WS-SMTP(WS-SUB1)(1: 5)      DELIMITED BY SIZE
                  ' '                         DELIMITED BY SIZE
                  WS-HOLD-FLD1                DELIMITED BY SIZE
                  INTO OUTSMTP-RCD.

       2000-WRITE-OUTSMTP.
           WRITE OUTSMTP-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0590B write error on OUTSMTP '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +2000                     TO WS-RETURN-CODE.
           ADD 1                              TO WS-WRITE-RCD-CNT.

      ************************************ END MIS0590B
