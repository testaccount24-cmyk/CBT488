
       TITLE '  MIS0380B: submit purge requests to RMS'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0380B.
      ******************************************************************
      *@@@ II submit purge requests to RMS
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ******************************************************************
      * Date written: 01/19/2000
      * Author      : Jim Iannone
      *               loop through a list of RMS reports and submit
      *               RMS purge commands to the internal reader.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT RMSDEL               ASSIGN TO RMSDEL
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT PARM01               ASSIGN TO PARM01
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT INTRDR               ASSIGN TO INTRDR
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL.

       DATA DIVISION.
       FILE SECTION.

       FD  RMSDEL
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  RMSDEL-RECORD.
           05  FILLER                  PIC  X.
           05  RMSDEL-FILE             PIC  X(08).
           05  FILLER                  PIC  X.
           05  RMSDEL-CLASS            PIC  X.
           05  FILLER                  PIC  X.
           05  RMSDEL-QUEUE            PIC  X(07).
           05  FILLER                  PIC  X.
           05  RMSDEL-JOB              PIC  X(08).
           05  RMSDEL-JOB-DASH         PIC  X.
           05  RMSDEL-JOB-NBR          PIC  X(05).
           05  FILLER                  PIC  X.
           05  RMSDEL-MDY              PIC  X(08).
           05  FILLER REDEFINES RMSDEL-MDY.
               10  FILLER              PIC  XX.
               10  RMSDEL-MDY-3X1      PIC  X.
               10  FILLER              PIC  XX.
               10  RMSDEL-MDY-6X1      PIC  X.
               10  FILLER              PIC  XX.
           05  FILLER                  PIC  X.
           05  RMSDEL-HMS              PIC  X(08).
           05  FILLER REDEFINES RMSDEL-HMS.
               10  FILLER              PIC  XX.
               10  RMSDEL-HMS-3X1      PIC  X.
               10  FILLER              PIC  XX.
               10  RMSDEL-HMS-6X1      PIC  X.
               10  FILLER              PIC  XX.
           05  FILLER                  PIC  X.
           05  RMSDEL-PAGES            PIC  X(05).
           05  FILLER                  PIC  XX.
           05  RMSDEL-LINES            PIC  X(07).
           05  FILLER                  PIC  X.
           05  RMSDEL-FORM             PIC  X(05).
           05  FILLER                  PIC  X(06).
           05  RMSDEL-NODE             PIC  X(08).
           05  FILLER                  PIC  X.
           05  RMSDEL-CODE             PIC  X(12).
           05  FILLER                  PIC  X(18).
           05  RMSDEL-KB-IN            PIC  X(08).
           05  RMSDEL-KB-OUT           PIC  X(07).

       FD  PARM01
           RECORDING MODE F.
       01  PARM01-RECORD.
           05  PARM01-DOCU             PIC  X.
           05  PARM01-MASK             PIC  X(08).
           05  PARM01-FILLER           PIC  X(63).
           05  FILLER                  PIC  X(08).

       FD  INTRDR
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  INTRDR-RECORD.
           05  INTRDR-PREFIX           PIC  X(06).
           05  INTRDR-QUOTE1           PIC  X.
           05  INTRDR-DATA             PIC  X(17).
           05  INTRDR-QUOTE2           PIC  X.
           05  INTRDR-FILLER           PIC  X(55).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working Storage MIS0380B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-PARM01-MAX           PIC  9(05)    VALUE ZERO.
           05  WS-PARM-DAYS            PIC  9(05)    VALUE ZERO.
           05  WS-WAIT-SECONDS         PIC  9(07)    VALUE ZERO.
           05  WS-WAIT-MINUTES         PIC  9(07)    VALUE ZERO.
           05  WS-WAIT-REMAIN          PIC  9(05)    VALUE ZERO.
           05  WS-CALC-DAYS            PIC  9(05)    VALUE ZERO.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.
           05  WS-1-CYMD               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-1-CYMD.
               10  WS-1-CYMD-CC        PIC  XX.
               10  WS-1-CYMD-YY        PIC  XX.
               10  WS-1-CYMD-MM        PIC  XX.
               10  WS-1-CYMD-DD        PIC  XX.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-PURGE-CNT            PIC S9(07)    VALUE ZERO.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(05)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-RMSDEL-SW        PIC  X        VALUE 'N'.
               88  END-OF-RMSDEL                     VALUE 'Y'.
               88  MORE-RMSDEL-RCDS                  VALUE 'N'.
           05  WS-DAY-SW               PIC  X        VALUE SPACE.
           05  WS-MASK-SW              PIC  X        VALUE SPACE.

       01  WS-TABLES.
           05  WS-MASK-TABLE                         VALUE SPACE.
               10  WS-MASK             PIC  X(08)
                                       OCCURS 20 TIMES.

       01  NEWWAIT-DATA.
           05  NEWWAIT-LEN             PIC S9999     VALUE +3 COMP.
           05  NEWWAIT-DELAY           PIC  999.

      *    dataset mask comparison
       01  WS-MIS0540B-FIELDS.
           05  MIS0540B-MASK           PIC  X(44).
           05  MIS0540B-DSN            PIC  X(44).
           05  MIS0540B-DISPLAY        PIC  X.
           05  MIS0540B-EDIT-NAMES     PIC  X.
           05  MIS0540B-MATCH          PIC  XXX.

       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0380B END  '.

       LINKAGE SECTION.

       01  PARM-FIELD.
           05  PARM-LENGTH             PIC S9(04)    COMP.
           05  PARM-FACILITY           PIC  X.
           05  PARM-PROCESS            PIC  X.
           05  PARM-DEL-DAYS           PIC  XXX.
           05  PARM-DELAY              PIC  XXX.
           05  PARM-EMERG              PIC  X.

      /
       PROCEDURE DIVISION USING PARM-FIELD.

       0100-PROGRAM-ENTRY.
           DISPLAY '     '.
           DISPLAY '**** MIS0380B submit purge requests to RMS'.
           PERFORM 0200-OPEN-FILES.

           MOVE ZERO                          TO WS-SUB1.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-LOAD-PARM01 THRU
                       0300-LOAD-PARM01-EXIT.
      *---------------------------------------
      *  if no mask entered in parm, or a single mask of '*'...
      *---------------------------------------
           IF WS-PARM01-MAX = ZERO
               MOVE '*'                       TO WS-MASK-SW
           ELSE
               PERFORM VARYING WS-SUB1 FROM 1 BY 1
                   UNTIL WS-SUB1 > WS-PARM01-MAX
                   IF WS-MASK(WS-SUB1) = '*'
                       MOVE '*'               TO WS-MASK-SW
                   END-IF
               END-PERFORM
           END-IF.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-RMSDEL.

           COMPUTE WS-WAIT-SECONDS = NEWWAIT-DELAY * WS-PURGE-CNT.
           DIVIDE WS-WAIT-SECONDS BY 60 GIVING WS-WAIT-MINUTES
                  REMAINDER WS-WAIT-REMAIN.

           IF PARM-EMERG = 'Y'
               DISPLAY '**** Emergency overrides allowed'.
           DISPLAY '**** Parm mask entries   ' WS-PARM01-MAX.
           DISPLAY '**** Process code        ' PARM-PROCESS.
           DISPLAY '**** Facility code       ' PARM-FACILITY.
           DISPLAY '**** Delete days         ' WS-PARM-DAYS.
           DISPLAY '**** Records in RMSDEL   ' WS-READ-RCD-CNT.
           DISPLAY '**** Reports purged      ' WS-PURGE-CNT.
           DISPLAY '**** Delay interval      ' NEWWAIT-DELAY.
           DISPLAY '**** Delay min/sec       ' WS-WAIT-MINUTES
                                               ' ' WS-WAIT-REMAIN.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0380B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0380B EOJ encountered '.
           DISPLAY '     '.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  RMSDEL.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0380B open error on RMSDEL'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT  PARM01.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0380B open error on PARM01'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0201                     TO WS-RETURN-CODE.
           OPEN OUTPUT INTRDR.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0380B open error on INTRDR'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0202                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE INTRDR.
           CLOSE PARM01.
           CLOSE RMSDEL.

      *---------------------------------------
      *    load PARM01
      *---------------------------------------
       0300-LOAD-PARM01.
           READ PARM01.
           IF WS-STAT = 10
               MOVE WS-SUB1                   TO WS-PARM01-MAX
               GO TO 0300-LOAD-PARM01-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0380B read error on PARM01'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0300                     TO WS-RETURN-CODE
               GO TO 0300-LOAD-PARM01-EXIT.

           IF PARM01-DOCU = '*'
               OR PARM01-MASK = SPACE
               GO TO 0300-LOAD-PARM01.
           IF WS-SUB1 < 20
               ADD 1                          TO WS-SUB1
               MOVE PARM01-MASK               TO WS-MASK(WS-SUB1).
           GO TO 0300-LOAD-PARM01.

       0300-LOAD-PARM01-EXIT. EXIT.

       0400-PROCESS-RMSDEL.
           IF PARM-DEL-DAYS NUMERIC
               MOVE PARM-DEL-DAYS             TO WS-PARM-DAYS
           ELSE
               DISPLAY '**** Parm del days not numeric '
               DISPLAY '**** ' PARM-DEL-DAYS
               MOVE +0400                     TO WS-RETURN-CODE.
           IF PARM-EMERG NOT = 'Y'
               IF WS-PARM-DAYS < 032
                   MOVE 032                   TO WS-PARM-DAYS.
           IF PARM-DELAY NUMERIC
               MOVE PARM-DELAY                TO NEWWAIT-DELAY.
           IF PARM-EMERG NOT = 'Y'
               IF NEWWAIT-DELAY < 2
                   MOVE 2                     TO NEWWAIT-DELAY.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0500-READ-RMSDEL THRU
                       0500-READ-RMSDEL-EXIT
                       UNTIL END-OF-RMSDEL.

       0500-READ-RMSDEL.
           READ RMSDEL.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
               PERFORM 0600-SELECT THRU
                       0600-SELECT-EXIT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-RMSDEL-SW
           ELSE
               DISPLAY '**** MIS0380B read error on RMSDEL '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0500                     TO WS-RETURN-CODE.

       0500-READ-RMSDEL-EXIT. EXIT.

       0600-SELECT.
           IF RMSDEL-MDY-3X1 = '/'
               AND RMSDEL-MDY-6X1 = '/'
               AND RMSDEL-HMS-3X1 = '.'
               AND RMSDEL-HMS-6X1 = '.'
               AND RMSDEL-JOB-DASH = '-'
               GO TO 0600-CONTINUE.

           GO TO 0600-SELECT-EXIT.

       0600-CONTINUE.
           IF RMSDEL-CODE NOT = SPACE
               GO TO 0600-SELECT-EXIT.
           MOVE SPACE                         TO WS-DAY-SW.
           PERFORM 0800-DETERMINE-DAYS.
           IF WS-MASK-SW NOT = '*'
               MOVE SPACE                     TO WS-MASK-SW
               PERFORM 1000-TEST-MASK
                   VARYING WS-SUB1 FROM 1 BY 1
                   UNTIL WS-SUB1 > WS-PARM01-MAX
                   OR WS-MASK-SW = 'Y'.
           IF WS-DAY-SW = 'Y'
             IF WS-MASK-SW = 'Y' OR '*'
                 PERFORM 0900-BUILD-DEL-RCD.

       0600-SELECT-EXIT. EXIT.

       0800-DETERMINE-DAYS.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE RMSDEL-MDY                    TO STV0400B-DATA.
           MOVE 'M/D/Y '                      TO STV0400B-IN.
           MOVE 'CYMD  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR'
               DISPLAY '**** STV0400B date conversion error '
               DISPLAY '**** Error = ' STV0400B-NBR
               MOVE +0800                     TO WS-RETURN-CODE
           ELSE
               MOVE 'CYMD  '                  TO STV0400B-IN
               MOVE 'DAYS  '                  TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               IF STV0400B-1X6 = 'ERROR'
                   DISPLAY '**** STV0400B days calc error '
                   DISPLAY '**** Error = ' STV0400B-NBR
                   MOVE +0801                 TO WS-RETURN-CODE
               ELSE
               IF STV0400B-DAYS NOT NUMERIC
                   DISPLAY '**** STV0400B days not numeric '
                   DISPLAY '**** Error = ' STV0400B-DAYS
                   MOVE +0802                 TO WS-RETURN-CODE
               ELSE
                   MOVE STV0400B-DAYS         TO WS-CALC-DAYS
                   IF WS-CALC-DAYS > WS-PARM-DAYS
                       MOVE 'Y'               TO WS-DAY-SW.

       0900-BUILD-DEL-RCD.
           MOVE SPACE                         TO INTRDR-DATA.
           MOVE SPACE                         TO INTRDR-FILLER.
           MOVE '/*$VS,'                      TO INTRDR-PREFIX.
           IF PARM-FACILITY = 'C'
               STRING 'F RMS2,PF '            DELIMITED BY SIZE
                      RMSDEL-QUEUE            DELIMITED BY SIZE
                      INTO INTRDR-DATA
           ELSE
               STRING 'F RMS,PF '             DELIMITED BY SIZE
                      RMSDEL-QUEUE            DELIMITED BY SIZE
                      INTO INTRDR-DATA.
           MOVE QUOTE                         TO INTRDR-QUOTE1.
           MOVE QUOTE                         TO INTRDR-QUOTE2.
           PERFORM 1200-WRITE-INTRDR.

      *---------------------------------------
      *    Call NEWWAIT: delay processing to avoid flooding
      *    the internal reader
      *+--------------------------------------
           IF PARM-PROCESS = 'Y'
               CALL 'NEWWAIT' USING NEWWAIT-DATA.

      *---------------------------------------
      *    Test report name against a report name mask,
      *    to select only specified reports for deletion.
      *---------------------------------------
       1000-TEST-MASK.
           MOVE WS-MASK(WS-SUB1)              TO MIS0540B-MASK.
           MOVE RMSDEL-FILE                   TO MIS0540B-DSN.
           MOVE SPACE                         TO MIS0540B-MATCH
                                                 MIS0540B-DISPLAY
                                                 MIS0540B-EDIT-NAMES.
           CALL 'MIS0540B' USING MIS0540B-MASK MIS0540B-DSN
                                 MIS0540B-DISPLAY MIS0540B-EDIT-NAMES
                                 MIS0540B-MATCH.
           IF MIS0540B-MATCH NUMERIC
               DISPLAY '**** MIS0545B call error to MIS0540B'
               DISPLAY '**** Code = ' MIS0540B-MATCH
               MOVE +0900                     TO WS-RETURN-CODE
           ELSE
           IF MIS0540B-MATCH = 'YES'
               MOVE 'Y'                       TO WS-MASK-SW.

       1200-WRITE-INTRDR.
           IF PARM-PROCESS = 'Y'
               WRITE INTRDR-RECORD
               MOVE '/*EOF '                  TO INTRDR-RECORD
               WRITE INTRDR-RECORD.
           ADD 1                              TO WS-PURGE-CNT.
           IF WS-PURGE-CNT = 1
               DISPLAY '****'
               DISPLAY '**** The following were selected for deletion '
               DISPLAY '****'
               DISPLAY '**** Report    Queue #  Date     # days'
               DISPLAY '**** --------  -------  -------- ------'.
           DISPLAY '**** ' RMSDEL-FILE '  ' RMSDEL-QUEUE
                   '  ' RMSDEL-MDY '  ' WS-CALC-DAYS.

      ************************************ END MIS0380B
