
       TITLE '  MIS0500B: change symbolics in run JCL'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0500B.
      ******************************************************************
      *@@@ II change symbolics in run JCL
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      *****************************************************************
      * Date written: 02/19/95
      * Author      : Jim Iannone
      *               loop through run JCL and increment
      *               hardcoded symbolics in preparation for run
      *               of desired job.
      *
      *               allow global override of current date offset
      *               via static parm
      *
      *    SYNTAX:
      *
      *        OFFX=        WILL REFLECT PARM-OFFSET-SW
      *
      *        EOMDATE=     MM/DD/YY  END OF PREV MONTH
      *        CURDATE=     MM/DD/YY  CURRENT DATE
      *        EOYDATE=     MM/DD/YY  END OF PREV YEAR
      *        CURREOM=     MM/DD/YY  END OF CURR MONTH
      *        YESTDAY=     MM/DD/YY  YESTERDAY
      *        LASTMON=     MMMCCYY   MONTH AND YEAR, 1 MONTH AGO
      *        PREVMON=     MMMCCYY   MONTH AND YEAR, 2 MONTHS AGO
      *        ----------------------------------------------------
      *        YMDX=     -  RETURNS CURRENT DATE IN YYMMDD FORMAT
      *        YDDX=     -  RETURNS CURRENT DATE IN YYDDD FORMAT
      *        CMDX=     -  RETURNS CURRENT DATE IN CCYYMMDD FORMAT
      *        CDDX=     -  RETURNS CURRENT DATE IN CCYYDDD FORMAT
      *        CYDX=     -  RETURNS CURRENT YEAR IN CCYY FORMAT
      *        HMSX=     -  RETURNS CURRENT TIME IN HHMMS FORMAT
      *        RUNX=     -  INCREMENTS A 2 DIGIT VERSION COUNTER IN
      *                     WHICHEVER JCL IS SPECIFIED
      *        DAYX=     -  NOT UPDATED BY YMDX=.  SEE YMD###=.
      *
      *        QTRX=     -  QUARTER:  1/2/3/4 DEPENDING ON MONTH
      *        ----------------------------------------------------
      *        YMF###=      RETURNS FUTURE DATE EQUAL TO CURRENT DATE
      *        YDF###=      PLUS ### DAYS
      *        CMF###=
      *        CDF###=
      *        CYF###=
      *
      *        YMD###=      RETURNS PAST DATE EQUAL TO CURRENT DATE
      *        YDD###=      MINUS ### DAYS
      *        CMD###=
      *        CDD###=
      *        CYD###=
      *        DAYX=        #### WILL BE PLACED IN DAYX= IF SPECIFIED
      *        ----------------------------------------------------
      *        YMFE##=      RETURNS AN END-OF-MONTH DATE FOR ## MONTHS
      *        YDFE##=      AHEAD OF CURRENT DATE
      *        CMFE##=
      *        CDFE##=
      *
      *        YMDE##=      RETURNS AN END-OF-MONTH DATE FOR ## MONTHS
      *        YDDE##=      PREVIOUS TO CURRENT DATE
      *        CMDE##=
      *        CDDE##=
      *        DAYM=        DAYM= WILL BE SET TO THE NUMBER OF DAYS
      *                     BETWEEN THESE DATES (#### FORMAT)
      *        ----------------------------------------------------
      *        YMFEY#=      RETURNS AN END-OF-YEAR DATE FOR # YEARS
      *        YDFEY#=      AHEAD OF CURRENT DATE
      *        CMFEY#=
      *        CDFEY#=
      *
      *        YMDEY#=      RETURNS AN END-OF-YEAR DATE FOR # YEARS
      *        YDDEY#=      PREVIOUS TO CURRENT DATE
      *        CMDEY#=
      *        CDDEY#=
      *        DAYY=        DAYY= WILL BE SET TO THE NUMBER OF DAYS
      *                     BETWEEN THESE DATES (#### FORMAT)
      *
      *
      *    RULES:
      *
      *        IF RUNX OCCURS BEFORE Y--X OR C--X, RUNX IS ALWAYS
      *        INCREMENTED.  IF RUNX OCCURS ANYWHERE AFTER Y--X OR
      *        C--X IT INCREMENTS AS LONG AS Y--X OR C--X HAS NOT
      *        CHANGED.  IF IT HAS (IE: RUN ON A NEW DAY) THEN
      *        RUNX IS SET TO 01
      *
      *        DAYX MUST OCCUR ANYWHERE AFTER Y--### OR C--###
      *        DAYM MUST OCCUR ANYWHERE AFTER Y--E## OR C--E##
      *        DAYY MUST OCCUR ANYWHERE AFTER Y--EY# OR C--EY#
      *
      *    EXAMPLES:
      *
      *        IF YOUR JCL LOOKS LIKE THIS:        YMDE02=000000,
      *                                            DAYM=0000
      *        AND THE CURRENT DATE IS:            04/20/95
      *
      *        THE RESULTS ARE:                    YMDE02=950228,
      *                                            DAYM=0051
      *
      *        IF USING THESE PARAMETERS IN A FILE NAME:
      *        MEDP.STVSSO.BACKUP.P&YMDE02..D&DAYM,   CONVERTS TO
      *        MEDP.STVSSO.BACKUP.P950228.D0051,
      *
      *    NOTES:
      *
      *        THESE PARAMETERS MAY BE USED AS INPUT TO COBOL PROGRAMS
      *        TO AVOID CALCULATING DATES/NUMBER OF DAYS/ETC.
      *
      *        USE THE 'DATE' TRANSACTION TO CONVERT DATES OR NUMBER
      *        OF DAYS BETWEEN DATES, ETC.
      *
      *        USE MIS0425B IN CONJUNCTION WITH THIS PROGRAM TO
      *        DYNAMICALLY UPDATE DATES OR SEQUENCE NUMBERS IN A
      *        STATIC PARM.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT JOBJCL               ASSIGN TO JOBJCL
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT OFFSET               ASSIGN TO OFFSET
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  JOBJCL
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  JOBJCL-RECORD.
           05  JOBJCL-PREFIX-1X2       PIC  XX.
           05  JOBJCL-PREFIX-3         PIC  X.
           05  FILLER                  PIC  X(77).

       FD  OFFSET
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  OFFSET-RECORD.
           05  OFFSET-DOCU             PIC  X.
           05  OFFSET-AMOUNT.
               10  OFFSET-PAST-FUT     PIC  X.
               10  OFFSET-OFFSET       PIC  X(05).
           05  FILLER                  PIC  X(73).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0500B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-TIME                 PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-TIME.
               10  WS-TIME-1X5         PIC  X(05).
               10  FILLER              PIC  XXX.
           05  WS-1-CYDDD              PIC  X(07)    VALUE SPACE.
           05  FILLER REDEFINES WS-1-CYDDD.
               10  FILLER              PIC  XX.
               10  WS-1-YDDD           PIC  X(05).
           05  WS-1-CYMD               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-1-CYMD.
               10  FILLER              PIC  XX.
               10  WS-1-YMD            PIC  X(06).
           05  FILLER REDEFINES WS-1-CYMD.
               10  WS-1-CY             PIC  X(04).
               10  FILLER              PIC  X(04).
           05  WS-2-CYMD               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-2-CYMD.
               10  FILLER              PIC  XX.
               10  WS-2-YMD            PIC  X(06).
           05  WS-DATA                 PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-DATA.
               10  FILLER              PIC  XX.
               10  WS-DATA2            PIC  X(06).
           05  WS-LEN                  PIC  99       VALUE ZERO.
           05  WS-NBR                  PIC  99       VALUE ZERO.
           05  FILLER REDEFINES WS-NBR.
               10  WS-NBR-1            PIC  9.
               10  WS-NBR-2            PIC  9.
           05  WS-RUN                  PIC  99       VALUE ZERO.
           05  WS-DAYX                 PIC  9(04)    VALUE ZERO.
           05  WS-DAYM                 PIC  9(04)    VALUE ZERO.
           05  WS-DAYY                 PIC  9(04)    VALUE ZERO.
           05  WS-NBR-DAYS             PIC  X(05)    VALUE SPACE.
           05  WS-REQUESTED            PIC  X(06)    VALUE SPACE.
           05  WS-1-EOM8               PIC  X(08)    VALUE SPACE.
           05  WS-1-YES8               PIC  X(08)    VALUE SPACE.
           05  WS-1-CUR8               PIC  X(08)    VALUE SPACE.
           05  WS-1-COM8               PIC  X(08)    VALUE SPACE.
           05  WS-1-EOY8               PIC  X(08)    VALUE SPACE.
           05  WS-1-LST7               PIC  X(07)    VALUE SPACE.
           05  WS-1-PRV7               PIC  X(07)    VALUE SPACE.
           05  WS-WORK1                PIC  9(04)    VALUE ZERO.
           05  WS-WORK2                PIC  9(04)    VALUE ZERO.
           05  WS-OFFSET               PIC  X(06)    VALUE SPACE.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(05)    VALUE ZERO.
           05  WS-SUB2                 PIC S9(05)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-JOBJCL-SW        PIC  X        VALUE 'N'.
               88  END-OF-JOBJCL                     VALUE 'Y'.
               88  MORE-JOBJCL-RCDS                  VALUE 'N'.
           05  WS-REWRITE-SW           PIC  X        VALUE SPACE.
           05  WS-STVDATE-CALC-SW      PIC  X        VALUE SPACE.
           05  WS-DATE-SW              PIC  X        VALUE SPACE.
           05  WS-DAYX-SW              PIC  X        VALUE SPACE.
           05  WS-DAYM-SW              PIC  X        VALUE SPACE.
           05  WS-DAYY-SW              PIC  X        VALUE SPACE.
           05  WS-CY-SW                PIC  XX       VALUE SPACE.
           05  FILLER REDEFINES WS-CY-SW.
               10  WS-CY-SW-1          PIC  X.
               10  WS-CY-SW-2          PIC  X.

      *    Year 2000 compatible date routine
       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0500B END  '.

       LINKAGE SECTION.

       01  LS-FIELD.
           05  LS-LENGTH               PIC S9(04)    COMP.
           05  LS-OFFSET-SW            PIC  X.

      /
       PROCEDURE DIVISION USING LS-FIELD.

       0100-PROGRAM-ENTRY.
           DISPLAY '      '.
           DISPLAY '**** MIS0500B change JCL symbolics'.
           PERFORM 0200-OPEN-FILES.

      *---------------------------------------
      *    Wherever MIS0500B generates a date, alter the
      *    date by WS-OFFSET amount.
      *+--------------------------------------
           MOVE '-00000'                      TO WS-OFFSET.
           IF WS-RETURN-CODE = ZERO
             IF LS-OFFSET-SW = 'Y'
                 PERFORM 0300-LOAD-OFFSET THRU
                         0300-LOAD-OFFSET-EXIT.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-JOBJCL.

           IF WS-OFFSET NOT = '-00000'
               DISPLAY '---> Warning - dates offset by '
                        WS-OFFSET ' days '.
           DISPLAY '**** Records in JOBJCL   ' WS-READ-RCD-CNT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0500B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0500B EOJ encountered '.
           DISPLAY '      '.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN I-O  JOBJCL.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0500B open error on JOBJCL'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT OFFSET.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0500B open error on OFFSET'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0201                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE OFFSET.
           CLOSE JOBJCL.

      ****************************************
      *    load OFFSET
      ****************************************
       0300-LOAD-OFFSET.
           READ OFFSET.
           IF WS-STAT = 10
               GO TO 0300-LOAD-OFFSET-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0500B read error on OFFSET'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0300                     TO WS-RETURN-CODE
               GO TO 0300-LOAD-OFFSET-EXIT.

           IF OFFSET-DOCU = '*'
               GO TO 0300-LOAD-OFFSET.
           IF OFFSET-OFFSET NUMERIC
               MOVE OFFSET-AMOUNT             TO WS-OFFSET
           ELSE
               DISPLAY '**** Offset amount not numeric '
               MOVE +0301                     TO WS-RETURN-CODE
               GO TO 0300-LOAD-OFFSET-EXIT.

       0300-LOAD-OFFSET-EXIT. EXIT.

       0400-PROCESS-JOBJCL.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE 'H:M:SM'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-DATA                 TO WS-TIME.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-CYMD.
           DISPLAY '**** System date ' WS-1-CYMD ' ' WS-TIME.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-OFFSET                     TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-CYMD.
           DISPLAY '**** Offset date ' WS-1-CYMD.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CYDDD '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-CYDDD.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE 'HMSM  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-DATA                 TO WS-TIME.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0500-READ-JOBJCL
                   UNTIL END-OF-JOBJCL.

       0500-READ-JOBJCL.
           READ JOBJCL.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
               PERFORM 0600-SELECT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-JOBJCL-SW
           ELSE
               DISPLAY '**** MIS0500B read error on JOBJCL '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0500                     TO WS-RETURN-CODE.

       0600-SELECT.
           MOVE SPACE                         TO WS-REWRITE-SW.
           MOVE SPACE                         TO WS-CY-SW.
           IF JOBJCL-PREFIX-1X2 = '//'
             IF JOBJCL-PREFIX-3 NOT = '*'
                 PERFORM 0700-TEST-JCL
                     VARYING WS-SUB1 FROM 5 BY 1
                     UNTIL WS-SUB1 > 60.
           IF WS-REWRITE-SW = 'Y'
               PERFORM 2000-REWRITE-JOBJCL.

       0700-TEST-JCL.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'YMDX='
                                      OR 'YDDX='
                                      OR 'CMDX='
                                      OR 'CDDX='
                                      OR 'HMSX='
                                      OR 'RUNX='
                                      OR 'CYDX='
                                      OR 'QTRX='
               COMPUTE WS-SUB2 = WS-SUB1 + 5
               IF JOBJCL-RECORD(WS-SUB2:1) NUMERIC
                   PERFORM 0800-CHANGE
               ELSE
                   ADD 1                      TO WS-SUB2
                   PERFORM 0800-CHANGE.

           IF JOBJCL-RECORD(WS-SUB1:8) = 'EOMDATE='
                                      OR 'CURDATE='
                                      OR 'CURREOM='
                                      OR 'YESTDAY='
                                      OR 'EOYDATE='
                                      OR 'LASTMON='
                                      OR 'PREVMON='
               PERFORM 1300-STVDATE THRU
                       1300-STVDATE-EXIT
               COMPUTE WS-SUB2 = WS-SUB1 + 8
               IF JOBJCL-RECORD(WS-SUB2:1) = QUOTE
                   ADD 1                      TO WS-SUB2
                   PERFORM 0800-CHANGE
               ELSE
                   PERFORM 0800-CHANGE.

           IF JOBJCL-RECORD(WS-SUB1:3) = 'CMD' OR 'CMF'
               MOVE 'GC'                      TO WS-CY-SW
               MOVE 8                         TO WS-LEN
           ELSE IF JOBJCL-RECORD(WS-SUB1:3) = 'YMD' OR 'YMF'
               MOVE 'GY'                      TO WS-CY-SW
               MOVE 6                         TO WS-LEN
           ELSE IF JOBJCL-RECORD(WS-SUB1:3) = 'CDD' OR 'CDF'
               MOVE 'JC'                      TO WS-CY-SW
               MOVE 7                         TO WS-LEN
           ELSE IF JOBJCL-RECORD(WS-SUB1:3) = 'YDD' OR 'YDF'
               MOVE 'JY'                      TO WS-CY-SW
               MOVE 5                         TO WS-LEN
           ELSE IF JOBJCL-RECORD(WS-SUB1:3) = 'CYD' OR 'CYF'
               MOVE 'GC'                      TO WS-CY-SW
               MOVE 4                         TO WS-LEN.

           IF JOBJCL-RECORD(WS-SUB1:3) = 'CMD' OR 'YMD'
                                      OR 'CDD' OR 'YDD'
                                      OR 'CMF' OR 'YMF'
                                      OR 'CDF' OR 'YDF'
               MOVE JOBJCL-RECORD(WS-SUB1:6) TO WS-REQUESTED
               COMPUTE WS-SUB2 = WS-SUB1 + 3
               IF JOBJCL-RECORD(WS-SUB2:3) NUMERIC
                   ADD 3                      TO WS-SUB2
                   IF JOBJCL-RECORD(WS-SUB2:1) = '='
                       PERFORM 0900-CHANGE-DAYS.

           IF JOBJCL-RECORD(WS-SUB1:3) = 'CYD' OR 'CYF'
               MOVE JOBJCL-RECORD(WS-SUB1:6) TO WS-REQUESTED
               COMPUTE WS-SUB2 = WS-SUB1 + 3
               IF JOBJCL-RECORD(WS-SUB2:3) NUMERIC
                   MOVE JOBJCL-RECORD(WS-SUB2:3) TO WS-WORK2
                   ADD 3                      TO WS-SUB2
                   IF JOBJCL-RECORD(WS-SUB2:1) = '='
                       PERFORM 1000-CHANGE-YEARS.

           IF JOBJCL-RECORD(WS-SUB1:4) = 'CMDE' OR 'YMDE'
                                      OR 'CDDE' OR 'YDDE'
                                      OR 'CMFE' OR 'YMFE'
                                      OR 'CDFE' OR 'YDFE'
               MOVE JOBJCL-RECORD(WS-SUB1:6) TO WS-REQUESTED
               COMPUTE WS-SUB2 = WS-SUB1 + 6
               IF JOBJCL-RECORD(WS-SUB2:1) = '='
                   COMPUTE WS-SUB2 = WS-SUB1 + 4
                   IF JOBJCL-RECORD(WS-SUB2:2) NUMERIC
                       MOVE JOBJCL-RECORD(WS-SUB2:2) TO WS-NBR
                       PERFORM 1100-CHANGE-MONTH THRU
                               1100-CHANGE-MONTH-EXIT
                   ELSE
                   IF JOBJCL-RECORD(WS-SUB2:1) = 'Y'
                       ADD 1                       TO WS-SUB2
                       IF JOBJCL-RECORD(WS-SUB2:1) NUMERIC
                           MOVE JOBJCL-RECORD(WS-SUB2:1) TO WS-NBR
                           PERFORM 1200-CHANGE-YEAR THRU
                                   1200-CHANGE-YEAR-EXIT.

           IF JOBJCL-RECORD(WS-SUB1:5) = 'OFFX='
             IF LS-OFFSET-SW = 'Y' OR 'N'
                 COMPUTE WS-SUB2 = WS-SUB1 + 5
                 IF JOBJCL-RECORD(WS-SUB2:1) = QUOTE
                     ADD 1                    TO WS-SUB2
                     PERFORM 0800-CHANGE
                 ELSE
                     PERFORM 0800-CHANGE.

           IF WS-DAYX-SW = 'Y'
             IF JOBJCL-RECORD(WS-SUB1:5) = 'DAYX='
                 COMPUTE WS-SUB2 = WS-SUB1 + 5
                 DISPLAY '**** DAYX set to ' WS-DAYX
                 MOVE 'Y'                TO WS-REWRITE-SW
                 IF JOBJCL-RECORD(WS-SUB2:1) NUMERIC
                     MOVE WS-DAYX        TO JOBJCL-RECORD(WS-SUB2:4)
                 ELSE
                     ADD 1               TO WS-SUB2
                     MOVE WS-DAYX        TO JOBJCL-RECORD(WS-SUB2:4).
           IF WS-DAYM-SW = 'Y'
             IF JOBJCL-RECORD(WS-SUB1:5) = 'DAYM='
                 COMPUTE WS-SUB2 = WS-SUB1 + 5
                 DISPLAY '**** DAYM set to ' WS-DAYM
                 MOVE 'Y'                TO WS-REWRITE-SW
                 IF JOBJCL-RECORD(WS-SUB2:1) NUMERIC
                     MOVE WS-DAYM        TO JOBJCL-RECORD(WS-SUB2:4)
                 ELSE
                     ADD 1               TO WS-SUB2
                     MOVE WS-DAYM        TO JOBJCL-RECORD(WS-SUB2:4).
           IF WS-DAYY-SW = 'Y'
             IF JOBJCL-RECORD(WS-SUB1:5) = 'DAYY='
                 COMPUTE WS-SUB2 = WS-SUB1 + 5
                 DISPLAY '**** DAYY set to ' WS-DAYY
                 MOVE 'Y'                TO WS-REWRITE-SW
                 IF JOBJCL-RECORD(WS-SUB2:1) NUMERIC
                     MOVE WS-DAYY        TO JOBJCL-RECORD(WS-SUB2:4)
                 ELSE
                     ADD 1               TO WS-SUB2
                     MOVE WS-DAYY        TO JOBJCL-RECORD(WS-SUB2:4).

       0800-CHANGE.
           IF WS-STVDATE-CALC-SW = 'Y'
               PERFORM 0800-CHANGE-STVDATE.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'CMDX='
             IF JOBJCL-RECORD(WS-SUB2:8) NUMERIC
               IF JOBJCL-RECORD(WS-SUB2:8) NOT = WS-1-CYMD
                   MOVE WS-1-CYMD TO JOBJCL-RECORD(WS-SUB2:8)
                   DISPLAY '**** CMDX set to ' WS-1-CYMD
                   MOVE 'Y'               TO WS-DATE-SW
                   MOVE 'Y'               TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'CDDX='
             IF JOBJCL-RECORD(WS-SUB2:7) NUMERIC
               IF JOBJCL-RECORD(WS-SUB2:7) NOT = WS-1-CYDDD
                   MOVE WS-1-CYDDD TO JOBJCL-RECORD(WS-SUB2:7)
                   DISPLAY '**** CDDX set to ' WS-1-CYDDD
                   MOVE 'Y'               TO WS-DATE-SW
                   MOVE 'Y'               TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'YMDX='
             IF JOBJCL-RECORD(WS-SUB2:6) NUMERIC
               IF JOBJCL-RECORD(WS-SUB2:6) NOT = WS-1-YMD
                   MOVE WS-1-YMD TO JOBJCL-RECORD(WS-SUB2:6)
                   DISPLAY '**** YMDX set to ' WS-1-YMD
                   MOVE 'Y'               TO WS-DATE-SW
                   MOVE 'Y'               TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'YDDX='
             IF JOBJCL-RECORD(WS-SUB2:5) NUMERIC
               IF JOBJCL-RECORD(WS-SUB2:5) NOT = WS-1-YDDD
                   MOVE WS-1-YDDD TO JOBJCL-RECORD(WS-SUB2:5)
                   DISPLAY '**** YDDX set to ' WS-1-YDDD
                   MOVE 'Y'               TO WS-DATE-SW
                   MOVE 'Y'               TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'CYDX='
             IF JOBJCL-RECORD(WS-SUB2:4) NUMERIC
               IF JOBJCL-RECORD(WS-SUB2:4) NOT = WS-1-CY
                   MOVE WS-1-CY TO JOBJCL-RECORD(WS-SUB2:4)
                   DISPLAY '**** CYDX set to ' WS-1-CY
                   MOVE 'Y'               TO WS-DATE-SW
                   MOVE 'Y'               TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'HMSX='
             IF JOBJCL-RECORD(WS-SUB2:5) NUMERIC
                 MOVE WS-TIME-1X5         TO JOBJCL-RECORD(WS-SUB2:5)
                 DISPLAY '**** HMSX set to ' WS-TIME-1X5
                 MOVE 'Y'                 TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'QTRX='
             IF JOBJCL-RECORD(WS-SUB2:1) NUMERIC
                 MOVE SPACE                   TO WS-STV0400B-FIELDS
                 MOVE WS-1-CYMD               TO STV0400B-1X8
                 MOVE 'CYMD  '                TO STV0400B-IN
                 MOVE 'QTR   '                TO STV0400B-OUT
                 CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
                 IF STV0400B-1X6 = 'ERROR'
                     DISPLAY '**** STV0400B error converting dates '
                             ' 0800 '
                     MOVE +0800               TO WS-RETURN-CODE
                 ELSE
                 IF JOBJCL-RECORD(WS-SUB2:1) NOT = STV0400B-DATA(1:1)
                     MOVE STV0400B-DATA TO JOBJCL-RECORD(WS-SUB2:1)
                     DISPLAY '**** QTRX set to ' STV0400B-DATA
                     MOVE 'Y'             TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'RUNX='
             IF JOBJCL-RECORD(WS-SUB2:2) NUMERIC
                 MOVE JOBJCL-RECORD(WS-SUB2:2) TO WS-RUN
                 MOVE 'Y'                 TO WS-REWRITE-SW
                 IF WS-DATE-SW = 'Y'
                     MOVE '01'            TO JOBJCL-RECORD(WS-SUB2:2)
                     DISPLAY '**** RUNX set to 01'
                     MOVE SPACE           TO WS-DATE-SW
                 ELSE
                 IF WS-RUN < 99
                     ADD 1                TO WS-RUN
                     MOVE WS-RUN          TO JOBJCL-RECORD(WS-SUB2:2)
                     DISPLAY '**** RUNX set to ' WS-RUN
                 ELSE
                     MOVE '01'            TO JOBJCL-RECORD(WS-SUB2:2)
                     DISPLAY '**** RUNX set to 01'.
           IF JOBJCL-RECORD(WS-SUB1:5) = 'OFFX='
             IF JOBJCL-RECORD(WS-SUB2:1) NOT = LS-OFFSET-SW
                 MOVE LS-OFFSET-SW        TO JOBJCL-RECORD(WS-SUB2:1)
                 DISPLAY '**** Offset set to ' LS-OFFSET-SW
                 MOVE 'Y'                 TO WS-REWRITE-SW.

       0800-CHANGE-STVDATE.
           IF JOBJCL-RECORD(WS-SUB1:8) = 'EOMDATE='
             IF JOBJCL-RECORD(WS-SUB2:8) NOT = WS-1-EOM8
                 MOVE WS-1-EOM8 TO JOBJCL-RECORD(WS-SUB2:8)
                 DISPLAY '**** EOMDATE set to ' WS-1-EOM8
                 MOVE 'Y'                 TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:8) = 'YESTDAY='
             IF JOBJCL-RECORD(WS-SUB2:8) NOT = WS-1-YES8
                 MOVE WS-1-YES8 TO JOBJCL-RECORD(WS-SUB2:8)
                 DISPLAY '**** YESTDAY set to ' WS-1-YES8
                 MOVE 'Y'                 TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:8) = 'CURDATE='
             IF JOBJCL-RECORD(WS-SUB2:8) NOT = WS-1-CUR8
                 MOVE WS-1-CUR8 TO JOBJCL-RECORD(WS-SUB2:8)
                 DISPLAY '**** CURDATE set to ' WS-1-CUR8
                 MOVE 'Y'                 TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:8) = 'EOYDATE='
             IF JOBJCL-RECORD(WS-SUB2:8) NOT = WS-1-EOY8
                 MOVE WS-1-EOY8 TO JOBJCL-RECORD(WS-SUB2:8)
                 DISPLAY '**** EOYDATE set to ' WS-1-EOY8
                 MOVE 'Y'                 TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:8) = 'CURREOM='
             IF JOBJCL-RECORD(WS-SUB2:8) NOT = WS-1-COM8
                 MOVE WS-1-COM8 TO JOBJCL-RECORD(WS-SUB2:8)
                 DISPLAY '**** CURREOM set to ' WS-1-COM8
                 MOVE 'Y'                 TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:8) = 'LASTMON='
             IF JOBJCL-RECORD(WS-SUB2:7) NOT = WS-1-LST7
                 MOVE WS-1-LST7 TO JOBJCL-RECORD(WS-SUB2:7)
                 DISPLAY '**** LASTMON set to ' WS-1-LST7
                 MOVE 'Y'                 TO WS-REWRITE-SW.
           IF JOBJCL-RECORD(WS-SUB1:8) = 'PREVMON='
             IF JOBJCL-RECORD(WS-SUB2:7) NOT = WS-1-PRV7
                 MOVE WS-1-PRV7 TO JOBJCL-RECORD(WS-SUB2:7)
                 DISPLAY '**** PREVMON set to ' WS-1-PRV7
                 MOVE 'Y'                 TO WS-REWRITE-SW.

       0900-CHANGE-DAYS.
           PERFORM 1700-GET-DIFF-DATE THRU
                   1700-GET-DIFF-DATE-EXIT.
           MOVE 'Y'                       TO WS-REWRITE-SW.
           MOVE 'Y'                       TO WS-DAYX-SW.
           MOVE WS-NBR-DAYS               TO WS-DAYX.
           IF WS-CY-SW-2 = 'Y'
               DISPLAY '**** ' WS-REQUESTED ' set to ' WS-DATA2
           ELSE
               DISPLAY '**** ' WS-REQUESTED ' set to ' WS-DATA.

       1000-CHANGE-YEARS.
           MOVE 'Y'                           TO WS-REWRITE-SW.
           MOVE 'N'                           TO WS-DAYX-SW.
           MOVE WS-1-CY                       TO WS-WORK1.
           IF JOBJCL-RECORD(WS-SUB1:3) = 'CYF'
               COMPUTE WS-WORK1 = WS-WORK1 + WS-WORK2
               MOVE WS-WORK1                  TO WS-DATA
               PERFORM 1900-MOVE-TO-JCL
           ELSE
               COMPUTE WS-WORK1 = WS-WORK1 - WS-WORK2
               MOVE WS-WORK1                  TO WS-DATA
               PERFORM 1900-MOVE-TO-JCL.
           DISPLAY '**** ' WS-REQUESTED ' set to ' WS-DATA.

       1100-CHANGE-MONTH.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           IF WS-REQUESTED(3: 1) = 'F'
               MOVE 'CMFE'                    TO STV0400B-OUT
           ELSE
               MOVE 'CMDE'                    TO STV0400B-OUT.
           MOVE WS-NBR                        TO STV0400B-OUT(5: 2).
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR'
               DISPLAY '**** STV0400B error converting dates '
                       ' 1100 '
               MOVE +1100                     TO WS-RETURN-CODE
               GO TO 1100-CHANGE-MONTH-EXIT.
           MOVE STV0400B-1X8                  TO WS-2-CYMD WS-DATA.
           IF WS-CY-SW-1 = 'J'
               MOVE SPACE                     TO WS-STV0400B-FIELDS
               MOVE WS-2-CYMD                 TO STV0400B-1X8
               MOVE 'CYMD  '                  TO STV0400B-IN
               MOVE 'CYDDD '                  TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               IF STV0400B-1X6 = 'ERROR '
                   DISPLAY '**** STV0400B error converting dates '
                           ' 1101 '
                   DISPLAY '**** ' WS-STV0400B-FIELDS
                   MOVE +1102                 TO WS-RETURN-CODE
                   GO TO 1100-CHANGE-MONTH-EXIT
               ELSE
                   MOVE STV0400B-1X8          TO WS-DATA.
           PERFORM 1800-GET-NBR-DAYS THRU
                   1800-GET-NBR-DAYS-EXIT.
           MOVE 'Y'                           TO WS-DAYM-SW.
           MOVE WS-NBR-DAYS                   TO WS-DAYM.
           IF WS-CY-SW-2 = 'Y'
               DISPLAY '**** ' WS-REQUESTED ' set to ' WS-DATA2
           ELSE
               DISPLAY '**** ' WS-REQUESTED ' set to ' WS-DATA.

       1100-CHANGE-MONTH-EXIT. EXIT.

       1200-CHANGE-YEAR.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           IF WS-REQUESTED(3: 1) = 'F'
               MOVE 'CMFEY'                   TO STV0400B-OUT
           ELSE
               MOVE 'CMDEY'                   TO STV0400B-OUT.
           MOVE WS-NBR-2                      TO STV0400B-OUT(6: 1).
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR'
               DISPLAY '**** STV0400B error converting dates '
                       ' 1200 '
               MOVE +1200                     TO WS-RETURN-CODE
               GO TO 1200-CHANGE-YEAR-EXIT.
           MOVE STV0400B-1X8                  TO WS-2-CYMD WS-DATA.
           IF WS-CY-SW-1 = 'J'
               MOVE SPACE                     TO WS-STV0400B-FIELDS
               MOVE WS-2-CYMD                 TO STV0400B-1X8
               MOVE 'CYMD  '                  TO STV0400B-IN
               MOVE 'CYDDD '                  TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               IF STV0400B-1X6 = 'ERROR '
                   DISPLAY '**** STV0400B error converting dates '
                           ' 1201 '
                   DISPLAY '**** ' WS-STV0400B-FIELDS
                   MOVE +1202                 TO WS-RETURN-CODE
                   GO TO 1200-CHANGE-YEAR-EXIT
               ELSE
                   MOVE STV0400B-1X8          TO WS-DATA.
           PERFORM 1800-GET-NBR-DAYS THRU
                   1800-GET-NBR-DAYS-EXIT.
           MOVE 'Y'                           TO WS-DAYY-SW.
           MOVE WS-NBR-DAYS                   TO WS-DAYY.
           IF WS-CY-SW-2 = 'Y'
               DISPLAY '**** ' WS-REQUESTED ' set to ' WS-DATA2
           ELSE
               DISPLAY '**** ' WS-REQUESTED ' set to ' WS-DATA.

       1200-CHANGE-YEAR-EXIT. EXIT.

       1300-STVDATE.
           IF WS-STVDATE-CALC-SW = 'Y'
               GO TO 1300-STVDATE-EXIT.
           MOVE 'Y'                           TO WS-STVDATE-CALC-SW.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CMDE01'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'M/D/Y '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-EOM8.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CMD001'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'M/D/Y '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-YES8.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'M/D/Y '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-CUR8.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CMDE00'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'M/D/Y '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-COM8.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CMDEY1'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'M/D/Y '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-EOY8.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CMDE01'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'MMMCY '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-LST7.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'CMDE02'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'MMMCY '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-1-PRV7.

       1300-STVDATE-EXIT. EXIT.

       1700-GET-DIFF-DATE.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           IF WS-REQUESTED(3: 1) = 'F'
               MOVE '+00000'                  TO STV0400B-OUT
           ELSE
               MOVE '-00000'                  TO STV0400B-OUT.
           COMPUTE WS-SUB2 = WS-SUB1 + 3.
           MOVE JOBJCL-RECORD(WS-SUB2:3)      TO STV0400B-OUT(4: 3).
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR '
               DISPLAY '**** STV0400B error converting dates '
                       ' 1700 '
               DISPLAY '**** ' WS-STV0400B-FIELDS
               MOVE +1700                     TO WS-RETURN-CODE
               GO TO 1700-GET-DIFF-DATE-EXIT.
           MOVE STV0400B-OUT(2: 5)            TO WS-NBR-DAYS.
           MOVE STV0400B-1X8                  TO WS-DATA WS-2-CYMD.
           IF WS-CY-SW-1 = 'J'
               MOVE SPACE                     TO WS-STV0400B-FIELDS
               MOVE WS-DATA                   TO STV0400B-1X8
               MOVE 'CYMD  '                  TO STV0400B-IN
               MOVE 'CYDDD '                  TO STV0400B-OUT
               CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS
               IF STV0400B-1X6 = 'ERROR '
                   DISPLAY '**** STV0400B error converting dates '
                           ' 1701 '
                   DISPLAY '**** ' WS-STV0400B-FIELDS
                   MOVE +1702                 TO WS-RETURN-CODE
                   GO TO 1700-GET-DIFF-DATE-EXIT
               ELSE
                   MOVE STV0400B-1X8          TO WS-DATA.
           PERFORM 1900-MOVE-TO-JCL.

       1700-GET-DIFF-DATE-EXIT. EXIT.

       1800-GET-NBR-DAYS.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           IF WS-REQUESTED(3: 1) = 'F'
               MOVE WS-2-CYMD                 TO STV0400B-DATA2
               MOVE WS-1-CYMD                 TO STV0400B-1X8
           ELSE
               MOVE WS-2-CYMD                 TO STV0400B-1X8
               MOVE WS-1-CYMD                 TO STV0400B-DATA2.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'DAYS  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR '
               DISPLAY '**** STV0400B error converting dates '
                       ' 1800 '
               DISPLAY '**** ' WS-STV0400B-FIELDS
               MOVE +1800                     TO WS-RETURN-CODE
               GO TO 1800-GET-NBR-DAYS-EXIT.
           MOVE 'Y'                           TO WS-REWRITE-SW.
           MOVE STV0400B-DAYS                 TO WS-NBR-DAYS.
           PERFORM 1900-MOVE-TO-JCL.

       1800-GET-NBR-DAYS-EXIT. EXIT.

       1900-MOVE-TO-JCL.
           COMPUTE WS-SUB2 = WS-SUB1 + 7.
           IF JOBJCL-RECORD(WS-SUB2:1) NUMERIC
               IF WS-CY-SW-2 = 'Y'
                   MOVE WS-DATA2 TO JOBJCL-RECORD(WS-SUB2: WS-LEN)
               ELSE
                   MOVE WS-DATA TO JOBJCL-RECORD(WS-SUB2: WS-LEN)
           ELSE
               ADD 1              TO WS-SUB2
               IF WS-CY-SW-2 = 'Y'
                   MOVE WS-DATA2 TO JOBJCL-RECORD(WS-SUB2: WS-LEN)
               ELSE
                   MOVE WS-DATA TO JOBJCL-RECORD(WS-SUB2: WS-LEN).

       2000-REWRITE-JOBJCL.
           REWRITE JOBJCL-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0500B rewrite error on JOBJCL'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +2000                     TO WS-RETURN-CODE.

      ************************************ END MIS0500B
