
       TITLE '  STV0115C: greg/jul and nbr-of-days convert'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV0115C
       INSTALLATION.  ST VINCENT MIS DEPARTMENT.
      ******************************************************************
      *@@@ II  greg/jul and nbr-of-days convert
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ******************************************************************
      * Project no.  |  890886
      * Date-written |  nov 24, 1989.
      * Narrative    |
      *
      * This online program converts a date entered in julian format
      * (yyddd) to gregorian format (mmddyy), and a date entered in
      * gregorian format to julian format.    transid = date
      ******************************************************************
      * Project #   : 900839
      * Date changed: 09/11/90
      * Programmer  : Jim Iannone
      *               Allow calculating nbr of days between 2 dates, or
      *               calculating a past or future date
      ******************************************************************
      * Date changed: 03/13/98
      * Programmer  : Jim Iannone
      *               rework for Cobol II and year 2000 compliance
      ******************************************************************
      * Date changed: 01/03/99
      * Programmer  : Jim Iannone
      *               integrate with 'conv'
      ******************************************************************

       ENVIRONMENT DIVISION.

       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-ANNOUNCE-START.
           05  FILLER                  PIC  X(43)      VALUE
               '**** Working Storage STV0115C starts here  '.

       01  WS-COMMAREA.
           05   WS-RETURN-SW           PIC  X.
           05   FILLER                 PIC  X(09).

       01  WS-OUTPUT.
           05  WS-OUT-MESSAGE          PIC  X(70)     VALUE SPACE.
           05  WS-OUTPUT-DATE          PIC  X(08)     VALUE SPACE.

       01  WS-INPUT.
           05  WS-INPUT-DATE           PIC  X(08)      VALUE SPACE.
           05  FILLER REDEFINES WS-INPUT-DATE.
               10  WS-IN-CY            PIC  9(04).
               10  WS-IN-MM            PIC  99.
               10  WS-IN-DD            PIC  99.
               10  WS-IN-DD-X REDEFINES WS-IN-DD
                                       PIC XX.
           05  FILLER REDEFINES WS-INPUT-DATE.
               10  WS-IN-JUL-DATE      PIC  X(07).
               10  FILLER REDEFINES WS-IN-JUL-DATE.
                   15  WS-IN-CC        PIC  99.
                   15  WS-IN-YY        PIC  99.
                   15  WS-IN-DDD       PIC  9(03).
               10  FILLER REDEFINES WS-IN-JUL-DATE.
                   15  FILLER          PIC  99.
                   15  WS-IN-DDD2      PIC  999.
                   15  WS-IN-EXTRA     PIC  XX.
               10  WS-IN-FILLER        PIC  X.

       01  WS-LEAP-YR-WORK-FIELDS.
           05  WS-LEAP-YR              PIC S99        VALUE ZERO.
           05  WS-LEAP-REMAIN          PIC S99        VALUE ZERO.
           05  LEAP-YR-SWITCH          PIC  X         VALUE 'N'.
               88  NOT-LEAP-YR                        VALUE 'N'.
               88  LEAP-YR                            VALUE 'Y'.

       01  WS-SWITCHES.
           05  WS-F-TYPE-SW            PIC  X         VALUE SPACE.
               88  WS-F-GREGORIAN                     VALUE 'G'.
               88  WS-F-JULIAN                        VALUE 'J'.

       01  WS-INDEXES.
           05   WS-SUB                 PIC S9(2)      VALUE ZERO.
           05   WS-SUB2                PIC S9(2)      VALUE ZERO.

       01  WS-WORK-FIELDS.
           05  WS-ABSOLUTE-TIME        PIC S9(16)      COMP.
           05  WS-EIBCALEN             PIC S9(04)    VALUE ZERO COMP.
           05  WS-EIBAID               PIC  X        VALUE SPACE.
           05  WS-EIBTRMID             PIC  X(04)    VALUE SPACE.
           05  WS-NBRDAY               PIC  X(05)      VALUE '00000'.
           05  WS-NBRDAY-N REDEFINES WS-NBRDAY
                                       PIC  9(05).
           05  WS-FDATE                PIC  X(08)      VALUE SPACE.
           05  FILLER                  REDEFINES WS-FDATE.
               10  WS-FDATE-7          PIC  X(07).
               10  WS-FDATE-8          PIC  X.
           05  FILLER                  REDEFINES WS-FDATE.
               10  WS-FDATE-J-YYYY     PIC  X(04).
               10  FILLER REDEFINES WS-FDATE-J-YYYY.
                   15  WS-FDATE-CC     PIC  XX.
                   15  WS-FDATE-YY     PIC  XX.
               10  WS-FDATE-J-DDD      PIC  XXX.
               10  FILLER              PIC  X.
           05  FILLER                  REDEFINES WS-FDATE.
               10  WS-FDATE-G-YYYY     PIC  X(04).
               10  WS-FDATE-G-MM       PIC  XX.
               10  WS-FDATE-G-DD       PIC  XX.
           05  WS-PDATE                PIC  X(08)      VALUE SPACE.
           05  FILLER                  REDEFINES WS-PDATE.
               10  WS-PDATE-7          PIC  X(07).
               10  WS-PDATE-8          PIC  X.
           05  FILLER                  REDEFINES WS-PDATE.
               10  WS-PDATE-J-YYYY     PIC  XXXX.
               10  WS-PDATE-J-DDD      PIC  XXX.
               10  FILLER              PIC  X.
           05  FILLER                  REDEFINES WS-PDATE.
               10  WS-PDATE-G-YYYY     PIC  X(04).
               10  WS-PDATE-G-MM       PIC  XX.
               10  WS-PDATE-G-DD       PIC  XX.
           05  WS-DATA1                PIC  X(10)      VALUE SPACE.
           05  WS-DATA2                PIC  X(10)      VALUE SPACE.
           05  WS-IN                   PIC  X(06)      VALUE SPACE.
           05  WS-OUT                  PIC  X(06)      VALUE SPACE.
           05  WS-QUOTIENT             PIC  9(04)      VALUE ZERO.
           05  WS-REMAINDER            PIC  99         VALUE ZERO.
           05  WS-REMAIN2              PIC  9(04)      VALUE ZERO.
           05  WS-YYYY                 PIC  9(04)      VALUE ZERO.
           05  WS-NBR-TABLE.
               10  WS-NBR              PIC  X
                                       OCCURS 5 TIMES.
           05  WS-SYSID                                VALUE SPACE.
               10  WS-SYSID-1X3        PIC  XXX.
               10  WS-SYSID-4          PIC  X.

       01  WS-STV1420B-FIELDS.
           05  STV1420B-PAST         PIC  X(08)   VALUE SPACE.
           05  STV1420B-FUTURE       PIC  X(08)   VALUE SPACE.
           05  STV1420B-DAYS         PIC  X(05)   VALUE SPACE.

       01  WS-STV1400B-FIELDS.
           05  STV1400B-DATA         PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV1400B-DATA.
               10  STV1400B-ERROR    PIC  X(06).
               10  STV1400B-NBR      PIC  X(04).
           05  FILLER REDEFINES STV1400B-DATA.
               10  STV1400B-CYMD     PIC  X(08).
               10  FILLER REDEFINES STV1400B-CYMD.
                   15  STV1400B-CYMD-C  PIC  XX.
                   15  STV1400B-CYMD-Y  PIC  XX.
                   15  STV1400B-CYMD-M  PIC  XX.
                   15  STV1400B-CYMD-D  PIC  XX.
               10  FILLER            PIC  XX.
           05  FILLER REDEFINES STV1400B-DATA.
               10  STV1400B-DAYS     PIC  X(05).
               10  FILLER            PIC  X(05).
           05  FILLER REDEFINES STV1400B-DATA.
               10  STV1400B-DAY      PIC  X(09).
               10  STV1400B-DAY-NBR  PIC  X.
           05  STV1400B-DATA2        PIC  X(10)   VALUE SPACE.
           05  STV1400B-IN           PIC  X(06)   VALUE SPACE.
           05  STV1400B-OUT          PIC  X(06)   VALUE SPACE.

      *CPYBKLIB ATTRIB
           COPY ATTRIB.    (see stv0110c)

      *CPYBKLIB CNTLCHAR
           COPY CNTLCHAR.  (see stv0110c)

      *CPYBKLIB DFHAID KEYS
           COPY DFHAID.

      *CPYBKLIB STV115M
           COPY STV115M.

       01  WS-ANNOUNCE-END.
           05  FILLER                  PIC  X(43)     VALUE
               '**** END STV0115C WORKING STORAGE HERE     '.

       LINKAGE SECTION.

       01  DFHCOMMAREA                 PIC X(10).
      *
       PROCEDURE DIVISION.
      ******************************************************************
      ** EIBCALEN SHOULD EQUAL 0 OR 10.  0 FOR FIRST EXECUTION OF
      ** ONLINE PROGRAM; 10 FOR SUBSEQUENT EXECUTIONS
      ******************************************************************
       0000-SETUP-EIB.
           MOVE EIBAID                        TO WS-EIBAID.
           MOVE EIBCALEN                      TO WS-EIBCALEN.
           MOVE EIBTRMID                      TO WS-EIBTRMID.

       0000-CHECK-EIBCALEN.
           IF WS-EIBAID = DFHCLEAR
               EXEC CICS ASSIGN
                   SYSID (WS-SYSID)
               END-EXEC
               MOVE '5'                       TO WS-SYSID-4
               EXEC CICS SEND CONTROL
                   FREEKB
                   ERASE
               END-EXEC
               EXEC CICS START
                   TRANSID('MENU')
                   TERMID(WS-EIBTRMID)
                   SYSID(WS-SYSID)
                   NOHANDLE
               END-EXEC
               EXEC CICS RETURN
               END-EXEC.
           IF WS-EIBCALEN = 0
               MOVE SPACE                     TO WS-COMMAREA
               GO TO 7000-INITIAL-MAP-SEND
           ELSE
           IF WS-EIBCALEN = 10
               MOVE DFHCOMMAREA               TO WS-COMMAREA
               GO TO 1000-PROCESS-SCREEN-INPUT
           ELSE
               GO TO 8000-COMMAREA-ERROR.
      *
       1000-PROCESS-SCREEN-INPUT.
           EXEC CICS HANDLE CONDITION
                     MAPFAIL (8300-MAPFAIL)
                     END-EXEC.
           EXEC CICS RECEIVE
                     MAP ('STV115A')
                     MAPSET ('STV115M')
                     END-EXEC.

           IF WS-EIBAID = DFHENTER
               MOVE SPACE                     TO WS-RETURN-SW
           ELSE
           IF WS-EIBAID = DFHPF1
               PERFORM 9400-XCTL-WIN0140C
               MOVE SPACE                     TO WS-RETURN-SW
           ELSE
           IF WS-EIBAID = DFHPF5
               GO TO 9400-XCTL-STV0110C
               MOVE SPACE                     TO WS-RETURN-SW
           ELSE
           IF WS-EIBAID = DFHPF3
               GO TO 7000-INITIAL-MAP-SEND
           ELSE
               MOVE SPACE                     TO WS-RETURN-SW
               GO TO 8100-ANYKEY-ERROR.
      ****************************************
      *    EDIT PAST DATE, FUTURE DATE, NBR OF DAYS FIELDS.
      *    BYPASS IF PROGRAM LINKED TO.
      ****************************************
           MOVE '---------------------------- Institution Name Here ----
      -    '------------------------'          TO STVMSGO.
           IF NBRDAYA = CC-EOF-KEY
               MOVE 1                           TO NBRDAYL.
           IF NBRDAYL > ZERO
               MOVE NORM-UNPROT-ALPHA-FSET      TO NBRDAYA
               INSPECT NBRDAYI REPLACING ALL LOW-VALUE BY '_'
               INSPECT NBRDAYI REPLACING ALL SPACE     BY '_'
               PERFORM 1000-JUSTIFY-NBR
               MOVE NBRDAYI                     TO WS-NBRDAY
               INSPECT WS-NBRDAY REPLACING ALL '_' BY ZERO.
           IF FDATEA = CC-EOF-KEY
               MOVE 1                           TO FDATEL.
           IF FDATEL > ZERO
               MOVE NORM-UNPROT-ALPHA-FSET      TO FDATEA
               INSPECT FDATEI REPLACING ALL LOW-VALUE BY '_'
               INSPECT FDATEI REPLACING ALL SPACE   BY '_'
               MOVE FDATEI                      TO WS-FDATE
               INSPECT WS-FDATE REPLACING ALL '_' BY SPACE.
           IF PDATEA = CC-EOF-KEY
               MOVE 1                           TO PDATEL.
           IF PDATEL > ZERO
               MOVE NORM-UNPROT-ALPHA-FSET      TO PDATEA
               INSPECT PDATEI REPLACING ALL LOW-VALUE BY '_'
               INSPECT PDATEI REPLACING ALL SPACE BY '_'
               MOVE PDATEI                      TO WS-PDATE
               INSPECT WS-PDATE REPLACING ALL '_' BY SPACE.
           IF DATA1A = CC-EOF-KEY
               MOVE 1                           TO DATA1L.
           IF DATA1L > ZERO
               MOVE NORM-UNPROT-ALPHA-FSET      TO DATA1A
               INSPECT DATA1I REPLACING ALL LOW-VALUE BY '_'
               INSPECT DATA1I REPLACING ALL SPACE BY '_'
               MOVE DATA1I                      TO WS-DATA1
               INSPECT WS-DATA1 REPLACING ALL '_' BY SPACE
               IF WS-DATA1 = SPACE
                   MOVE NORM-UNPROT-ALPHA       TO DATA1A.
           IF DATA2A = CC-EOF-KEY
               MOVE 1                           TO DATA2L.
           IF DATA2L > ZERO
               MOVE NORM-UNPROT-ALPHA-FSET      TO DATA2A
               INSPECT DATA2I REPLACING ALL LOW-VALUE BY '_'
               INSPECT DATA2I REPLACING ALL SPACE BY '_'
               MOVE DATA2I                      TO WS-DATA2
               INSPECT WS-DATA2 REPLACING ALL '_' BY SPACE
               IF WS-DATA2 = SPACE
                   MOVE NORM-UNPROT-ALPHA       TO DATA2A.
           IF INA = CC-EOF-KEY
               MOVE 1                           TO INL.
           IF INL > ZERO
               MOVE NORM-UNPROT-ALPHA-FSET      TO INA
               INSPECT INI REPLACING ALL LOW-VALUE BY '_'
               INSPECT INI REPLACING ALL SPACE BY '_'
               MOVE INI                         TO WS-IN
               INSPECT WS-IN REPLACING ALL '_' BY SPACE
               IF WS-IN = SPACE
                   MOVE NORM-UNPROT-ALPHA       TO INA.
           IF OUTA = CC-EOF-KEY
               MOVE 1                           TO OUTL.
           IF OUTL > ZERO
               MOVE NORM-UNPROT-ALPHA-FSET      TO OUTA
               INSPECT OUTI REPLACING ALL LOW-VALUE BY '_'
               INSPECT OUTI REPLACING ALL SPACE BY '_'
               MOVE OUTI                        TO WS-OUT
               INSPECT WS-OUT REPLACING ALL '_' BY SPACE
               IF WS-OUT = SPACE
                   MOVE NORM-UNPROT-ALPHA       TO OUTA.

           PERFORM 1000-SUBROUTINE THRU
                   1000-SUBROUTINE-EXIT.

           IF WS-NBRDAY = ZERO
               AND WS-FDATE = SPACE
               AND WS-PDATE = SPACE
               GO TO 1000-CONTINUE.

           IF NBRDAYL > ZERO
               IF WS-NBRDAY NOT NUMERIC
                   MOVE BRT-UNPROT-ALPHA-FSET   TO NBRDAYA
                   MOVE -1                      TO NBRDAYL
                   MOVE 'Nbr of days must be numeric' TO STVMSGI
               ELSE
               IF WS-NBRDAY-N > 99599
                   MOVE BRT-UNPROT-ALPHA-FSET   TO NBRDAYA
                   MOVE -1                      TO NBRDAYL
                   MOVE 'Nbr of days must be less than 99600'
                                                TO STVMSGI.

           IF FDATEL > ZERO
               IF (WS-FDATE-7 NOT = SPACE)
                   AND (WS-FDATE-7 NOT NUMERIC)
                   MOVE BRT-UNPROT-ALPHA-FSET   TO FDATEA
                   MOVE -1                      TO FDATEL
                   MOVE 'Future date must be numeric' TO STVMSGI.

           IF FDATEL > ZERO
               IF WS-FDATE = SPACE
                   GO TO 1000-EDIT-PDATE
               ELSE
               IF WS-FDATE-8 = SPACE
                   GO TO 1000-EDIT-FDATE-7
               ELSE
                   CONTINUE
           ELSE
               GO TO 1000-EDIT-PDATE.

           MOVE 'G'                             TO WS-F-TYPE-SW.
           IF (WS-FDATE-G-MM > 12)
               OR (WS-FDATE-G-MM = ZERO)
               MOVE 'Invalid future date month' TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO FDATEA
               MOVE -1                          TO FDATEL
           ELSE
           IF (WS-FDATE-G-DD > 31)
               OR (WS-FDATE-G-DD = ZERO)
               MOVE 'Invalid future date day'   TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO FDATEA
               MOVE -1                          TO FDATEL
           ELSE
           IF (WS-FDATE-G-YYYY < 0001)
               MOVE 'Invalid future date year'  TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO FDATEA
               MOVE -1                          TO FDATEL.

           GO TO 1000-EDIT-PDATE.

       1000-EDIT-FDATE-7.
           MOVE 'J'                             TO WS-F-TYPE-SW.
           IF (WS-FDATE-J-DDD > 366)
               OR (WS-FDATE-J-DDD = ZERO)
               MOVE 'Invalid future date days'  TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO FDATEA
               MOVE -1                          TO FDATEL
           ELSE
           IF (WS-FDATE-J-YYYY < 0001)
               MOVE 'Invalid future date year'  TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO FDATEA
               MOVE -1                          TO FDATEL
           ELSE
           IF WS-FDATE-J-DDD = 366
               MOVE WS-FDATE-J-YYYY             TO WS-YYYY
               DIVIDE WS-YYYY BY 4 GIVING WS-QUOTIENT
                   REMAINDER WS-REMAINDER
               IF WS-REMAINDER NOT = ZERO
                   MOVE 'Future date year not a leap year'
                                                TO STVMSGI
                   MOVE BRT-UNPROT-ALPHA-FSET   TO FDATEA
                   MOVE -1                      TO FDATEL
               ELSE
               IF WS-FDATE-YY = '00'
                   DIVIDE WS-YYYY BY 400 GIVING WS-QUOTIENT
                       REMAINDER WS-REMAIN2
                   IF WS-REMAIN2 NOT = ZERO
                       MOVE 'Future date year not a leap year'
                                                TO STVMSGI
                       MOVE BRT-UNPROT-ALPHA-FSET TO FDATEA
                       MOVE -1                  TO FDATEL.

       1000-EDIT-PDATE.
           IF PDATEL > ZERO
               IF (WS-PDATE-7 NOT = SPACE)
                   AND (WS-PDATE-7 NOT NUMERIC)
                   MOVE BRT-UNPROT-ALPHA-FSET   TO PDATEA
                   MOVE -1                      TO PDATEL
                   MOVE 'Past date must be numeric' TO STVMSGI.

           IF PDATEL > ZERO
               IF WS-PDATE = SPACE
                   GO TO 1000-PREPARE-STV1420B
               ELSE
               IF WS-PDATE-8 = SPACE
                   GO TO 1000-EDIT-PDATE-7
               ELSE
                   CONTINUE
           ELSE
               GO TO 1000-PREPARE-STV1420B.

           IF (WS-PDATE-G-MM > 12)
               OR (WS-PDATE-G-MM = ZERO)
               MOVE 'Invalid past date month'   TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA
               MOVE -1                          TO PDATEL
           ELSE
           IF (WS-PDATE-G-DD > 31)
               OR (WS-PDATE-G-DD = ZERO)
               MOVE 'Invalid past date day'     TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA
               MOVE -1                          TO PDATEL
           ELSE
           IF (WS-PDATE-G-YYYY < 0001)
               MOVE 'Invalid past date year'    TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA
               MOVE -1                          TO PDATEL.
           IF WS-F-GREGORIAN
               IF WS-FDATE-G-YYYY < WS-PDATE-G-YYYY
                   CONTINUE
               ELSE
               IF WS-FDATE-G-YYYY = WS-PDATE-G-YYYY
                   IF WS-FDATE-G-MM < WS-PDATE-G-MM
                       CONTINUE
                   ELSE
                   IF WS-FDATE-G-MM = WS-PDATE-G-MM
                       IF WS-FDATE-G-DD < WS-PDATE-G-DD
                           CONTINUE
                       ELSE
                           GO TO 1000-PREPARE-STV1420B
                   ELSE
                       GO TO 1000-PREPARE-STV1420B
               ELSE
                   GO TO 1000-PREPARE-STV1420B
           ELSE
           IF WS-F-JULIAN
               MOVE 'Please enter both dates in the same format'
                                                TO STVMSGI
               MOVE -1                          TO PDATEL FDATEL
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA FDATEA
               GO TO 1000-PREPARE-STV1420B
           ELSE
               GO TO 1000-PREPARE-STV1420B.

           MOVE 'Future date must be greater or equal to past date'
                                                TO STVMSGI.
           MOVE -1                              TO PDATEL FDATEL.
           MOVE BRT-UNPROT-ALPHA-FSET           TO PDATEA FDATEA.
           GO TO 1000-PREPARE-STV1420B.

       1000-EDIT-PDATE-7.
           IF (WS-PDATE-J-DDD > 366)
               OR (WS-PDATE-J-DDD = ZERO)
               MOVE 'Invalid past date days'    TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA
               MOVE -1                          TO PDATEL
           ELSE
           IF (WS-PDATE-J-YYYY < 0001)
               MOVE 'Invalid past date year'    TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA
               MOVE -1                          TO PDATEL
           ELSE
           IF WS-PDATE-J-DDD = 366
               MOVE WS-PDATE-J-YYYY             TO WS-YYYY
               DIVIDE WS-YYYY BY 4 GIVING WS-QUOTIENT
                   REMAINDER WS-REMAINDER
               IF WS-REMAINDER NOT = ZERO
                   MOVE 'Past date year not a leap year'
                                                TO STVMSGI
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA
               MOVE -1                          TO PDATEL.

           IF WS-F-JULIAN
               IF WS-FDATE-J-YYYY < WS-PDATE-J-YYYY
                   CONTINUE
               ELSE
               IF WS-FDATE-J-YYYY = WS-PDATE-J-YYYY
                   IF WS-FDATE-J-DDD < WS-PDATE-J-DDD
                       CONTINUE
                   ELSE
                       GO TO 1000-PREPARE-STV1420B
               ELSE
                   GO TO 1000-PREPARE-STV1420B
           ELSE
           IF WS-F-GREGORIAN
               MOVE 'Please enter both dates in the same format'
                                                TO STVMSGI
               MOVE -1                          TO PDATEL FDATEL
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA FDATEA
               GO TO 1000-PREPARE-STV1420B
           ELSE
               GO TO 1000-PREPARE-STV1420B.

           MOVE 'Future date must be greater or equal to past date'
                                                TO STVMSGI.
           MOVE -1                              TO PDATEL FDATEL.
           MOVE BRT-UNPROT-ALPHA-FSET           TO PDATEA FDATEA.
           GO TO 1000-PREPARE-STV1420B.

      ****************************************
      *    STV1400B SUBROUTINE
      ****************************************
       1000-SUBROUTINE.
           IF DATA2L = ZERO
               AND INL = ZERO
               AND OUTL = ZERO
               IF DATA1L = ZERO
                   OR WS-DATA1 = SPACE
                   GO TO 1000-SUBROUTINE-EXIT.
           MOVE WS-DATA1                      TO STV1400B-DATA.
           MOVE WS-DATA2                      TO STV1400B-DATA2.
           MOVE WS-IN                         TO STV1400B-IN.
           MOVE WS-OUT                        TO STV1400B-OUT.
           CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1400B-FIELDS.
           MOVE STV1400B-DATA                 TO DATA1I.
           MOVE STV1400B-DATA2                TO DATA2I.
           MOVE STV1400B-IN                   TO INI.
           MOVE STV1400B-OUT                  TO OUTI.
           INSPECT DATA1I REPLACING ALL SPACE BY '_'.
           INSPECT DATA2I REPLACING ALL SPACE BY '_'.
           INSPECT INI REPLACING ALL SPACE BY '_'.
           INSPECT OUTI REPLACING ALL SPACE BY '_'.
           IF DATA1I NOT = '__________'
               MOVE NORM-UNPROT-ALPHA-FSET    TO DATA1A.
           IF DATA2I NOT = '__________'
               MOVE NORM-UNPROT-ALPHA-FSET    TO DATA2A.
           IF INI NOT = '______'
               MOVE NORM-UNPROT-ALPHA-FSET    TO INA.
           IF OUTI NOT = '______'
               MOVE NORM-UNPROT-ALPHA-FSET    TO OUTA.

       1000-SUBROUTINE-EXIT. EXIT.

      ****************************************
      *    FIND NBR OF DAYS OR PAST OR FUTURE DATE
      ****************************************
       1000-PREPARE-STV1420B.
           IF NBRDAYL = -1
               OR PDATEL = -1
               OR FDATEL = -1
               GO TO 1000-CONTINUE.
           IF WS-PDATE = SPACE
               AND WS-FDATE = SPACE
               GO TO 1000-CONTINUE.

           MOVE WS-NBRDAY                       TO STV1420B-DAYS.
           MOVE WS-FDATE                        TO STV1420B-FUTURE.
           MOVE WS-PDATE                        TO STV1420B-PAST.
           CALL 'STV1420B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1420B-FIELDS.
      *          STV1420B IS A COBOL II ONLINE VERSION OF STV0420B
           IF STV1420B-DAYS = '*****'
               MOVE 'Error in calculation process - please verify data'
                                                TO STVMSGI
               MOVE -1                          TO PDATEL
               MOVE BRT-UNPROT-ALPHA-FSET       TO PDATEA FDATEA NBRDAY
               GO TO 1000-CONTINUE
           ELSE
               MOVE STV1420B-PAST               TO PDATEI
               MOVE STV1420B-FUTURE             TO FDATEI
               MOVE STV1420B-DAYS               TO NBRDAYI
               MOVE NORM-UNPROT-ALPHA-FSET      TO PDATEA FDATEA NBRDAY
               INSPECT PDATEI    REPLACING ALL SPACE BY '_'
               INSPECT FDATEI    REPLACING ALL SPACE BY '_'
               INSPECT PDATEI    REPLACING ALL LOW-VALUE BY '_'
               INSPECT FDATEI    REPLACING ALL LOW-VALUE BY '_'
               INSPECT NBRDAYI REPLACING LEADING '0' BY '_'
               IF NBRDAYI = '_____'
                   MOVE '____0'                 TO NBRDAYI.

       1000-CONTINUE.
           IF INDATEF = CC-EOF-KEY
               MOVE 1                           TO INDATEL.
           IF INDATEL > 0
               MOVE NORM-UNPROT-ALPHA-FSET      TO INDATEA
               INSPECT INDATEI REPLACING ALL LOW-VALUE BY '_'
               INSPECT INDATEI REPLACING ALL SPACE BY '_'.

           IF INDATEI = '________'
               OR INDATEL = 0
                   MOVE DRK-ASKIP-ASKIP         TO SIGNA ANSWERA DAYA
                   GO TO 9200-SEND-MAP-DATAONLY.
           MOVE INDATEI                         TO WS-INPUT-DATE.
           INSPECT WS-INPUT-DATE REPLACING ALL '_' BY SPACE.
           GO TO 2500-WHICH-CONVERSION.

      ****************************************
      *    RIGHT JUSTIFY THE ALPHANUMERIC FIELD NBRDAYI
      ****************************************
       1000-JUSTIFY-NBR.
           MOVE NBRDAYI                         TO WS-NBR-TABLE.
           MOVE 5                               TO WS-SUB WS-SUB2.
           PERFORM 1000-JUSTIFY
               UNTIL WS-SUB < 1.
           MOVE WS-NBR-TABLE                    TO NBRDAYI.

       1000-JUSTIFY.
           IF WS-NBR(WS-SUB) NUMERIC
               IF WS-SUB = WS-SUB2
                   SUBTRACT 1 FROM WS-SUB WS-SUB2
               ELSE
                   MOVE WS-NBR(WS-SUB)          TO WS-NBR(WS-SUB2)
                   SUBTRACT 1  FROM WS-SUB2
                   MOVE '_'                     TO WS-NBR(WS-SUB)
           ELSE
               SUBTRACT 1  FROM WS-SUB.

       2500-WHICH-CONVERSION.
           IF WS-IN-EXTRA = SPACE
               GO TO 4000-EDIT-CK-JULIAN
           ELSE IF WS-IN-DD-X = SPACE
               GO TO 3000-EDIT-CK-GREG
           ELSE IF WS-IN-FILLER = SPACE
               GO TO 4000-EDIT-CK-JULIAN
           ELSE
               GO TO 3000-EDIT-CK-GREG.

      ******************************************************************
      ** EDIT CHECK THE INPUTED GREGORIAN DATE.  RETURN AN ERROR      **
      ** MESSAGE IF IT IS OUT OF RANGE, ELSE CONVERT TO JULIAN.       **
      ******************************************************************
       3000-EDIT-CK-GREG.
           IF WS-IN-CY NUMERIC
               AND WS-IN-MM NUMERIC
               AND WS-IN-DD-X = SPACE
               MOVE SPACE                      TO WS-STV1400B-FIELDS
               MOVE WS-INPUT-DATE(1: 6)        TO STV1400B-DATA(1: 6)
               MOVE 'YMD   '                   TO STV1400B-IN
               MOVE 'CYMD  '                   TO STV1400B-OUT
               CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
                                     WS-STV1400B-FIELDS
               IF STV1400B-ERROR = 'ERROR'
                   MOVE 'Date not valid - please reenter' TO STVMSGO
                   GO TO 8500-FAILED-EDIT
               ELSE
                   MOVE STV1400B-CYMD          TO WS-INPUT-DATE.
           IF WS-INPUT-DATE NOT NUMERIC
               MOVE 'Date must be numeric'     TO STVMSGO
               GO TO 8500-FAILED-EDIT.
           IF WS-IN-MM < 1
               OR WS-IN-MM > 12
               MOVE 'Month must be in the range of 1 to 12' TO STVMSGO
               GO TO 8500-FAILED-EDIT.
           IF WS-IN-DD > 31
               MOVE 'Invalid day found - please reenter' TO STVMSGO
               GO TO 8500-FAILED-EDIT.

      *
      * PASSED EDIT-CONVERT THE GREGORIAN DATE TO JULIAN CYDDD FORMAT **
      *
           MOVE SPACE                          TO WS-STV1400B-FIELDS.
           MOVE WS-INPUT-DATE                  TO STV1400B-CYMD.
           MOVE 'CYMD  '                       TO STV1400B-IN.
           MOVE 'CYDDD '                       TO STV1400B-OUT.
           CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
                                          WS-STV1400B-FIELDS.
           IF STV1400B-ERROR = 'ERROR'
               MOVE 'Gregorian to julian conversion error'
                                               TO STVMSGO
               GO TO 8500-FAILED-EDIT
           ELSE
               MOVE STV1400B-DATA              TO WS-OUTPUT-DATE.
           MOVE SPACE                          TO WS-STV1400B-FIELDS.
           MOVE WS-INPUT-DATE                  TO STV1400B-DATA.
           MOVE 'CYMD  '                       TO STV1400B-IN.
           MOVE 'DAY   '                       TO STV1400B-OUT.
           CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
                                          WS-STV1400B-FIELDS.
           IF STV1400B-ERROR = 'ERROR'
               MOVE 'Day determination error' TO STVMSGO
               GO TO 8500-FAILED-EDIT
           ELSE
               MOVE STV1400B-DAY               TO DAYI.

           GO TO 9100-SEND-ONLINE-DATE.
      *
      ******************************************************************
      ** EDIT CHECK THE INPUTED JULIAN DATE.  RETURN AN ERROR MESSAGE **
      ** IF IT IS OUT OF RANGE, ELSE CONVERT TO GREGORIAN.            **
      ******************************************************************
       4000-EDIT-CK-JULIAN.
           IF WS-IN-CC NUMERIC
               AND WS-IN-DDD2 NUMERIC
               AND WS-IN-EXTRA = SPACE
               MOVE SPACE                      TO WS-STV1400B-FIELDS
               MOVE WS-IN-JUL-DATE(1: 5)       TO STV1400B-DATA(1: 5)
               MOVE 'YDDD  '                   TO STV1400B-IN
               MOVE 'CYDDD '                   TO STV1400B-OUT
               CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
                                     WS-STV1400B-FIELDS
               IF STV1400B-ERROR = 'ERROR'
                   MOVE 'Date not valid - please reenter' TO STVMSGO
                   GO TO 8500-FAILED-EDIT
               ELSE
                   MOVE STV1400B-DATA(1: 7)    TO WS-IN-JUL-DATE.
           IF WS-IN-JUL-DATE NOT NUMERIC
               MOVE 'Date not numeric - please reenter' TO STVMSGO
               GO TO 8500-FAILED-EDIT.
           IF WS-IN-DDD < 1
               MOVE 'Day must be greater than zero - please reenter'
                                               TO STVMSGO
               GO TO 8500-FAILED-EDIT.
           DIVIDE WS-IN-YY                     BY 4
               GIVING WS-LEAP-YR               REMAINDER WS-LEAP-REMAIN.
           DIVIDE WS-IN-CY                     BY 400
               GIVING WS-LEAP-YR               REMAINDER WS-REMAIN2.
           IF  WS-LEAP-REMAIN EQUAL ZERO
               MOVE 'Y'                        TO LEAP-YR-SWITCH
               IF WS-IN-YY = ZERO
                  IF WS-REMAIN2 NOT = ZERO
                      MOVE 'N'                 TO LEAP-YR-SWITCH.
           IF LEAP-YR
               IF WS-IN-DDD > 366
                   MOVE 'Day must be less than 367 - please reenter'
                                              TO STVMSGO
                   GO TO 8500-FAILED-EDIT.
           IF NOT-LEAP-YR
               IF WS-IN-DDD > 365
                   MOVE 'Day must be less than 366 - please reenter'
                                               TO STVMSGO
                   GO TO 8500-FAILED-EDIT.

      *
      *  CONVERT JULIAN DATE TO GREGORIAN CYMD FORMAT *
      *
           MOVE SPACE                          TO WS-STV1400B-FIELDS.
           MOVE WS-IN-JUL-DATE                 TO STV1400B-DATA.
           MOVE 'CYDDD '                       TO STV1400B-IN.
           MOVE 'CYMD  '                       TO STV1400B-OUT.
           CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
                                          WS-STV1400B-FIELDS.
           IF STV1400B-ERROR = 'ERROR'
               MOVE 'Julian to gregorian conversion error' TO STVMSGO
               GO TO 8500-FAILED-EDIT
           ELSE
               MOVE STV1400B-DATA              TO WS-OUTPUT-DATE.
           MOVE SPACE                          TO WS-STV1400B-FIELDS.
           MOVE WS-IN-JUL-DATE                 TO STV1400B-DATA.
           MOVE 'CYDDD '                       TO STV1400B-IN.
           MOVE 'DAY   '                       TO STV1400B-OUT.
           CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
                                          WS-STV1400B-FIELDS.
           IF STV1400B-ERROR = 'ERROR'
               MOVE 'Day determination error'  TO STVMSGO
               GO TO 8500-FAILED-EDIT
           ELSE
               MOVE STV1400B-DAY               TO DAYI.

           GO TO 9100-SEND-ONLINE-DATE.

      *
      ******************************************************************
      ** SEND INITIAL SCREEN TO USER TERMINAL AFTER OBTAINING USER ID **
      ** AND CURRENT DATE AND TIME.                                   **
      ******************************************************************
       7000-INITIAL-MAP-SEND.
           IF WS-RETURN-SW = 'Y'
               MOVE DFHCLEAR                  TO WS-EIBAID
               GO TO 0000-CHECK-EIBCALEN.
           MOVE LOW-VALUE                     TO STV115AI.
           MOVE 'Y'                           TO WS-RETURN-SW.
           EXEC CICS ASSIGN
               USERID (USERIDO)
           END-EXEC.
           EXEC CICS ASKTIME
                     ABSTIME (WS-ABSOLUTE-TIME)
           END-EXEC.
           EXEC CICS FORMATTIME ABSTIME(WS-ABSOLUTE-TIME)
                     DATESEP('/')
                     MMDDYY (DATEO)
                     TIMESEP(':')
                     TIME (TIMEO)
           END-EXEC.
           GO TO 9000-SEND-INITIAL-MAP.

      ******************
      * ERROR MESSAGES *
      ******************
       8000-COMMAREA-ERROR.
           MOVE 'Unable to complete transaction conv due to commarea er
      -    'ror'                               TO WS-OUT-MESSAGE.
           GO TO 9300-SEND-TEXT-MESSAGE.

       8100-ANYKEY-ERROR.
           MOVE 'Invalid key'                  TO STVMSGO.
           GO TO 9200-SEND-MAP-DATAONLY.

       8300-MAPFAIL.
           MOVE 'Unable to complete transaction conv due to a map error'
                                               TO WS-OUT-MESSAGE.
           GO TO 9300-SEND-TEXT-MESSAGE.
      *
       8500-FAILED-EDIT.
           MOVE -1                             TO INDATEL.
           MOVE BRT-UNPROT-ALPHA-FSET          TO INDATEA.
           MOVE DRK-ASKIP-ASKIP                TO SIGNA ANSWERA DAYA.
           GO TO 9200-SEND-MAP-DATAONLY.
      *
      ******************
      * RETURN TO CICS *
      ******************
       9000-SEND-INITIAL-MAP.
          EXEC CICS SEND
                    MAP ('STV115A')
                    MAPSET ('STV115M')
                    ERASE
                    END-EXEC
          EXEC CICS RETURN
                    TRANSID ('CONV')
                    COMMAREA (WS-COMMAREA)
                    LENGTH (10)
                    END-EXEC.
      *
       9100-SEND-ONLINE-DATE.
           MOVE WS-OUTPUT-DATE                 TO ANSWERO.
           MOVE NORM-ASKIP-ASKIP               TO SIGNA ANSWERA DAYA.
           MOVE NORM-UNPROT-ALPHA-FSET         TO INDATEA.

       9200-SEND-MAP-DATAONLY.
           IF INDATEL = -1
               OR NBRDAYL = -1
               OR FDATEL = -1
               OR PDATEL = -1
               CONTINUE
           ELSE
               MOVE -1 TO INDATEL.
           CALL 'STV0270B' USING DFHEIBLK DFHCOMMAREA STVMSGI.
           EXEC CICS SEND
                     MAP ('STV115A')
                     MAPSET ('STV115M')
                     DATAONLY
                     CURSOR
                     END-EXEC.
           EXEC CICS RETURN
                     TRANSID ('CONV')
                     COMMAREA (WS-COMMAREA)
                     LENGTH (10)
                     END-EXEC.
      *
       9300-SEND-TEXT-MESSAGE.
           EXEC CICS SEND TEXT
                     FROM(WS-OUT-MESSAGE)
                     LENGTH(70)
                     ERASE
                     END-EXEC.
      *
       9400-XCTL-STV0110C.
           EXEC CICS XCTL
               PROGRAM ('STV0110C')
           END-EXEC.

      ****************************************
      *    IF PF1 ENTERED, XCTL TO THE HELP WINDOW
      ****************************************
       9400-XCTL-WIN0140C.
           EXEC CICS HANDLE CONDITION
               NOSPACE (9400-NOSPACE)
               INVREQ (9400-INVREQ)
           END-EXEC.
      *    help window logic removed

       9400-NOSPACE.
           MOVE 'Unable to show help screen - no space on ts queue'
                                              TO STVMSGI.
           GO TO 9200-SEND-MAP-DATAONLY.

       9400-INVREQ.
           MOVE 'Unable to show help screen - invreq on ts queue'
                                              TO STVMSGI.
           GO TO 9200-SEND-MAP-DATAONLY.

      ******************************************************************
      **              E N D   O F   S T V 0 1 1 5 C                   **
      ******************************************************************

