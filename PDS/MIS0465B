
       TITLE '  MIS0465B: concatenate HSM activity files'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0465B.
      ******************************************************************
      *@@@ II list HSM activity files
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      *****************************************************************
      * Date written: 06/23/96
      * Author      : Jim Iannone
      *               loop through a file built from a LISTCAT catalog
      *               command and concatenate HSM activity file names
      *               into dynamically built jcl
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT HSMIN                ASSIGN TO HSMIN
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT JCLIN                ASSIGN TO JCLIN
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT JCLOUT               ASSIGN TO JCLOUT
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  HSMIN
           RECORDING MODE V
           RECORD CONTAINS 9 TO 121.
       01  HSMIN-RECORD2.
           05  FILLER                  PIC  X(09).
       01  HSMIN-RECORD.
           05  FILLER                  PIC  X(121).

       FD  JCLIN
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  JCLIN-RECORD.
           05  FILLER                  PIC  X(80).

       FD  JCLOUT
           RECORDING MODE F
           BLOCK CONTAINS 0.
       01  JCLOUT-RECORD.
           05  FILLER                  PIC  X(80).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0465B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-YYDDD                PIC  X(05)    VALUE SPACE.
           05  WS-1-CYMD               PIC  X(08)    VALUE SPACE.
           05  WS-1-MDY                PIC  X(06)    VALUE SPACE.
           05  WS-80-LINE              PIC  X(80)    VALUE SPACE.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.

       01  WS-JCL-LINE.
           05  FILLER                  PIC  XX       VALUE '//'.
           05  WS-DD                   PIC  X(05)    VALUE 'HSMIN'.
           05  FILLER                  PIC  X(08)    VALUE
               '    DD  '.
           05  WS-DSN                  PIC  X(57)    VALUE SPACE.

       01  WS-JCL-LINE2.
           05  FILLER                  PIC  XX       VALUE '//'.
           05  FILLER                  PIC  X(13)    VALUE SPACE.
           05  WS-DCB                  PIC  X(13)    VALUE
               'DCB=EROPT=SKP'.
           05  WS-DISP                 PIC  X(44)    VALUE SPACE.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-FILE-RCD-CNT         PIC S9(07)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-HSMIN-SW         PIC  X        VALUE 'N'.
               88  END-OF-HSMIN                      VALUE 'Y'.
               88  MORE-HSMIN-RCDS                   VALUE 'N'.
           05  WS-END-JCLIN-SW         PIC  X        VALUE 'N'.
               88  END-OF-JCLIN                      VALUE 'Y'.
               88  MORE-JCLIN-RCDS                   VALUE 'N'.
           05  WS-FIRST-RCD-SW         PIC  X        VALUE 'Y'.
               88  FIRST-RECORD                      VALUE 'Y'.

      *    year 2000 compatible date routine
       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0465B END  '.

       LINKAGE SECTION.

       01  PARM-FIELD.
           05  PARM-LENGTH             PIC S9(04)    COMP.
           05  PARM-CYMD               PIC  X(08).
           05  PARM-DISP               PIC  X.

      /
       PROCEDURE DIVISION USING PARM-FIELD.

       0100-PROGRAM-ENTRY.
           DISPLAY '      '.
           DISPLAY '**** MIS0465B list HSM activity files '.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-HSMIN.

           DISPLAY '**** Gregorian date      ' WS-1-MDY.
           DISPLAY '**** Julian date         ' WS-YYDDD.
           DISPLAY '**** Records in HSMIN    ' WS-READ-RCD-CNT.
           DISPLAY '**** Allocations         ' WS-FILE-RCD-CNT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0465B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0465B EOJ encountered '.
           DISPLAY '      '.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  HSMIN.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0465B open error on HSMIN'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT  JCLIN.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0465B open error on JCLIN'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT JCLOUT.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0465B open error on JCLOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE JCLOUT.
           CLOSE JCLIN.
           CLOSE HSMIN.

       0400-PROCESS-HSMIN.
           PERFORM 0600-GET-JULIAN.
           PERFORM 0700-READ-HSMIN
               UNTIL END-OF-HSMIN.

       0600-GET-JULIAN.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE PARM-CYMD                     TO STV0400B-1X8.
           MOVE 'CYMD'                        TO STV0400B-IN.
           MOVE 'YDDD'                        TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-1X6 = 'ERROR'
               DISPLAY '**** MIS0465B julian conversion error'
               DISPLAY '**** Input = ' PARM-CYMD
               MOVE +0200                     TO WS-RETURN-CODE
           ELSE
               MOVE STV0400B-DATA             TO WS-YYDDD.

       0700-READ-HSMIN.
           READ HSMIN.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
               PERFORM 0800-SELECT THRU
                       0800-SELECT-EXIT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-HSMIN-SW
           ELSE
               DISPLAY '**** MIS0465B read error on HSMIN '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0700                     TO WS-RETURN-CODE.

       0800-SELECT.
           MOVE HSMIN-RECORD                  TO WS-80-LINE.
           IF WS-80-LINE(18: 9) = 'HSMACT.H1'
               IF WS-80-LINE(36: 5) = WS-YYDDD
                   PERFORM 0900-FILE-DISP
                   ADD 1                      TO WS-FILE-RCD-CNT
                   IF FIRST-RECORD
                       PERFORM 1000-READ-JCLIN
                           UNTIL END-OF-JCLIN
                       MOVE WS-JCL-LINE       TO JCLOUT-RECORD
                       PERFORM 1100-WRITE-JCLOUT
                       MOVE WS-JCL-LINE2      TO JCLOUT-RECORD
                       PERFORM 1100-WRITE-JCLOUT
                       MOVE SPACE             TO WS-DD
                   ELSE
                       MOVE WS-JCL-LINE       TO JCLOUT-RECORD
                       PERFORM 1100-WRITE-JCLOUT
                       MOVE WS-JCL-LINE2      TO JCLOUT-RECORD
                       PERFORM 1100-WRITE-JCLOUT.

       0800-SELECT-EXIT. EXIT.

       0900-FILE-DISP.
           MOVE SPACE                         TO WS-DSN.
           STRING 'DSN='       WS-80-LINE(18: 31)
                  ','  DELIMITED BY SIZE INTO WS-DSN.
           IF PARM-DISP = '2'
               MOVE ',DISP=(OLD,DELETE)'      TO WS-DISP
           ELSE
               MOVE ',DISP=SHR         '      TO WS-DISP.

       1000-READ-JCLIN.
           READ JCLIN.
           IF WS-STAT = ZERO
               MOVE JCLIN-RECORD              TO JCLOUT-RECORD
               PERFORM 1100-WRITE-JCLOUT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-JCLIN-SW
           ELSE
               DISPLAY '**** MIS0465B read error on JCLIN '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +1000                     TO WS-RETURN-CODE.

       1100-WRITE-JCLOUT.
           WRITE JCLOUT-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0465B write error on JCLOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +1100                     TO WS-RETURN-CODE.

      ************************************ END MIS0465B
