
       TITLE '  STV0280B: patient age calculation '.
       IDENTIFICATION DIVISION.                                         STV0280B
       PROGRAM-ID.    STV0280B.                                         STV0280B
       AUTHOR.        TOM RICHARDSON.                                   STV0280B
      ******************************************************************STV0280B
      *@@@ C3 calculate patients age                                    stv0280b
      ******************************************************************stv0280b
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ****************************************************************
      * Program-ID  : STV0280B                                          stv0280b
      * Project #   : 901031                                            stv0280b
      * Date written: 11/19/90                                          stv0280b
      * Programmer  : Tom Richardson                                    stv0280b
      *
      *               This call program calculates a patient's age when stv0280b
      *               supplied with a birthdate in CCYYMMDD format.     stv0280b
      *               An optional AS-OF date (also in CCYYMMDD format)  stv0280b
      *               may be supplied and used in calculating age.  If  stv0280b
      *               AS-OF date is omitted (zeroes, spaces, or low-val-stv0280b
      *               ues), current date is used for calculating age.   stv0280b
      *               If birthdate is greater than AS-OF date supplied, stv0280b
      *               if date fields supplied not numeric, if month     stv0280b
      *               fields supplied not between 1-12, if day fields   stv0280b
      *               supplied invalid for month requested, or date of  stv0280b
      *               birth supplied greater than as of date supplied   stv0280b
      *               (or current date if no as of date supplied),      stv0280b
      *               asterisks will be returned.  Otherwise, age       stv0280b
      *               specifying years, months, and days is returned to stv0280b
      *               calling program (7 positions - 3=years, 2=months, stv0280b
      *               and 2=days respectively).                         stv0280b
      *
      ******************************************************************stv0280b
      * Project #   : 910730
      * Date changed: 07/24/91
      * Programmer  : Jim Iannone
      *               remove current-date: user must supply as-of-date.
      ******************************************************************
      * Project #   : 920999
      * Date changed: 01/18/93
      * Programmer  : P E Bonfiglio
      *               recompiled using Cobol II because Cobol II does
      *               not allow calls to VS Cobol programs.
      ******************************************************************
      * Date changed: 04/07/2006
      * Programmer  : Jim Iannone
      *               compile with Cobol 3 with DYNAM option
      ******************************************************************
                                                                        STV0280B
       ENVIRONMENT DIVISION.                                            STV0280B
       DATA DIVISION.                                                   STV0280B
                                                                        STV0280B
       WORKING-STORAGE SECTION.                                         STV0280B
                                                                        STV0280B
       01  WS-START.                                                    STV0280B
           05  FILLER                  PIC  X(32) VALUE                 STV0280B
               ' WORKING-STORAGE STV0280B START '.                      STV0280B
                                                                        STV0280B
       01  WS-CALCULATE-AGE-WORK-AREAS.                                 STV0280B
           05  WS-LEAP                 PIC S9(3)  VALUE ZERO.           STV0280B
           05  WS-LEAP-REMAIN          PIC S9(2)  VALUE ZERO.           STV0280B
           05  WS-LEAP-REMAIN2         PIC S9(4)  VALUE ZERO.           STV0280B
           05  WS-LEAP-YYYY            PIC 9(04).                       STV0280B
           05  FILLER REDEFINES WS-LEAP-YYYY.                           STV0280B
               10  FILLER              PIC 99.                          STV0280B
               10  WS-LEAP-YY          PIC 99.                          STV0280B
           05  WS-PREV-MM              PIC 9(02)  VALUE 12.             STV0280B
           05  WS-AGE.                                                  STV0280B
               10  WS-YEARS            PIC S9(03).                      STV0280B
               10  WS-MONTHS           PIC S9(02).                      STV0280B
               10  WS-DAYS             PIC S9(02).                      STV0280B
           05  WS-REFORMAT-DOB.                                         STV0280B
               10  WS-RDOB-YYYY        PIC 9(04).                       STV0280B
               10  WS-RDOB-MM          PIC 9(02).                       STV0280B
               10  WS-RDOB-DD          PIC 9(02).                       STV0280B
           05  WS-REFORMAT-AS-OF.                                       STV0280B
               10  WS-RAO-YYYY         PIC 9(04).                       STV0280B
               10  WS-RAO-MM           PIC 9(02).                       STV0280B
               10  WS-RAO-DD           PIC 9(02).                       STV0280B
           05  WS-CAL-DAYS-VALUES.                                      STV0280B
               10  FILLER              PIC X(24)  VALUE                 STV0280B
                                          '312831303130313130313031'.   STV0280B
           05  FILLER REDEFINES WS-CAL-DAYS-VALUES.                     STV0280B
               10  CAL-DAYS-ARRAY      OCCURS 12 TIMES.                 STV0280B
                   15 CAL-DAYS         PIC 9(02).                       STV0280B
                                                                        STV0280B
       01  WS-END.                                                      STV0280B
           05  FILLER                  PIC  X(16)    VALUE              STV0280B
               '  END STV0280B  '.                                      STV0280B
                                                                        STV0280B
       LINKAGE SECTION.                                                 STV0280B
                                                                        STV0280B
       01  LS-DATES-PASSED.                                             STV0280B
           05  LS-DATE-OF-BIRTH.                                        STV0280B
               10  LS-DOB-YYYY         PIC  X(04).                      STV0280B
               10  LS-DOB-MM           PIC  X(02).                      STV0280B
               10  LS-DOB-DD           PIC  X(02).                      STV0280B
           05  LS-AS-OF-DATE.                                           STV0280B
               10  LS-AOD-YYYY         PIC  X(04).                      STV0280B
               10  LS-AOD-MM           PIC  X(02).                      STV0280B
               10  LS-AOD-DD           PIC  X(02).                      STV0280B
           05  LS-AGE.                                                  STV0280B
               10  LS-YEARS            PIC  X(03).                      STV0280B
               10  LS-MONTHS           PIC  X(02).                      STV0280B
               10  LS-DAYS             PIC  X(02).                      STV0280B
                                                                        STV0280B
       PROCEDURE DIVISION USING LS-DATES-PASSED.                        STV0280B
                                                                        STV0280B
       1000-START-PROCESS.                                              STV0280B
           MOVE LS-DOB-YYYY            TO WS-RDOB-YYYY.                 STV0280B
           MOVE LS-DOB-MM              TO WS-RDOB-MM.                   STV0280B
           MOVE LS-DOB-DD              TO WS-RDOB-DD.                   STV0280B
           MOVE LS-AOD-YYYY            TO WS-RAO-YYYY.                  STV0280B
           MOVE LS-AOD-MM              TO WS-RAO-MM.                    STV0280B
           MOVE LS-AOD-DD              TO WS-RAO-DD.                    STV0280B
           MOVE ZEROES                 TO WS-AGE.                       STV0280B
           PERFORM 2000-EDIT-DATES THRU                                 STV0280B
                   2000-EDIT-DATES-EXIT.                                STV0280B
           PERFORM 3000-CALCULATE-AGE THRU                              STV0280B
                   3000-CALCULATE-AGE-EXIT.                             STV0280B
           MOVE WS-YEARS               TO LS-YEARS.                     STV0280B
           MOVE WS-MONTHS              TO LS-MONTHS.                    STV0280B
           MOVE WS-DAYS                TO LS-DAYS.                      STV0280B
           GOBACK.                                                      STV0280B
                                                                        STV0280B
       2000-EDIT-DATES.                                                 STV0280B
           IF LS-DATE-OF-BIRTH NOT NUMERIC                              STV0280B
               GO TO 9000-DATE-ERROR.                                   STV0280B
           IF LS-AS-OF-DATE NOT NUMERIC                                 STV0280B
               GO TO 9000-DATE-ERROR.                                   STV0280B
           IF WS-RDOB-MM < 01 OR > 12                                   STV0280B
               GO TO 9000-DATE-ERROR.                                   STV0280B
           IF WS-RAO-MM < 01 OR > 12                                    STV0280B
               GO TO 9000-DATE-ERROR.                                   STV0280B
           MOVE WS-RDOB-YYYY           TO WS-LEAP-YYYY.                 STV0280B
           PERFORM 2100-DETERMINE-LEAP-YEAR THRU                        STV0280B
                   2100-DETERMINE-LEAP-YEAR-EXIT.                       STV0280B
           IF WS-RDOB-DD < 1 OR > CAL-DAYS(WS-RDOB-MM)                  STV0280B
               GO TO 9000-DATE-ERROR.                                   STV0280B
           MOVE WS-RAO-YYYY            TO WS-LEAP-YYYY.                 STV0280B
           PERFORM 2100-DETERMINE-LEAP-YEAR THRU                        STV0280B
                   2100-DETERMINE-LEAP-YEAR-EXIT.                       STV0280B
           IF WS-RAO-DD < 1 OR > CAL-DAYS(WS-RAO-MM)                    STV0280B
               GO TO 9000-DATE-ERROR.                                   STV0280B
           IF WS-REFORMAT-DOB > WS-REFORMAT-AS-OF                       STV0280B
               GO TO 9000-DATE-ERROR.                                   STV0280B
                                                                        STV0280B
       2000-EDIT-DATES-EXIT.                                            STV0280B
           EXIT.                                                        STV0280B
                                                                        STV0280B
       2100-DETERMINE-LEAP-YEAR.                                        STV0280B
           MOVE 28                     TO CAL-DAYS(2).                  STV0280B
           DIVIDE WS-LEAP-YYYY         BY 4                             STV0280B
               GIVING WS-LEAP          REMAINDER WS-LEAP-REMAIN.        STV0280B
           IF WS-LEAP-REMAIN EQUAL ZERO                                 STV0280B
               MOVE 29                 TO CAL-DAYS(2)                   STV0280B
               IF WS-LEAP-YY = ZERO
                   DIVIDE WS-LEAP-YYYY BY 400
                   GIVING WS-LEAP      REMAINDER WS-LEAP-REMAIN2        STV0280B
                 IF WS-LEAP-REMAIN2 NOT = ZERO                          STV0280B
                     MOVE 28           TO CAL-DAYS(2).                  STV0280B
                                                                        STV0280B
       2100-DETERMINE-LEAP-YEAR-EXIT.                                   STV0280B
           EXIT.                                                        STV0280B
                                                                        STV0280B
       3000-CALCULATE-AGE.                                              STV0280B
           IF WS-RAO-MM > 1                                             STV0280B
               SUBTRACT 1              FROM WS-RAO-MM                   STV0280B
                   GIVING WS-PREV-MM.                                   STV0280B
           COMPUTE WS-DAYS = WS-RAO-DD - WS-RDOB-DD.                    STV0280B
           IF WS-DAYS < 0                                               STV0280B
               COMPUTE WS-DAYS = CAL-DAYS(WS-PREV-MM) + WS-DAYS         STV0280B
               SUBTRACT 1              FROM WS-MONTHS.                  STV0280B
                                                                        STV0280B
       3000-COMPUTE-MONTHS.                                             STV0280B
           COMPUTE WS-MONTHS = WS-MONTHS + WS-RAO-MM                    STV0280B
                                         - WS-RDOB-MM.                  STV0280B
           IF WS-MONTHS < 0                                             STV0280B
               ADD 12                  TO WS-MONTHS                     STV0280B
               SUBTRACT 1              FROM WS-YEARS.                   STV0280B
                                                                        STV0280B
       3000-COMPUTE-YEARS.                                              STV0280B
           COMPUTE WS-YEARS = WS-YEARS + WS-RAO-YYYY                    STV0280B
                                       - WS-RDOB-YYYY.                  STV0280B
                                                                        STV0280B
       3000-CALCULATE-AGE-EXIT. EXIT.                                   STV0280B
                                                                        STV0280B
       9000-DATE-ERROR.                                                 STV0280B
           MOVE '*******'              TO LS-AGE.                       STV0280B
           GOBACK.                                                      STV0280B
                                                                        STV0280B
      ******************************************************************STV0280B
      ****              E N D  O F  S T V 0 2 8 0 B                 ****STV0280B
      ******************************************************************STV0280B
