
       TITLE '  STV1400B: year 2000 common date subroutine'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV1400B.
       AUTHOR.        JIM IANNONE.
      ******************************************************************
      *@@@ II convert dates for the year 2000
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      *****************************************************************
      * Program-id  : STV1400B
      * Date written: 03/06/1998
      * Programmer  : Jim Iannone
      *
      *       CALL PROGRAM TO COMBINE DATE SUBROUTINES FOR
      *       YEAR 2000 COMPLIANCE
      *
      *-----------------------------------------------------------
      *  !!!  IT IS UP TO THE PROGRAMMER TO VERIFY THE FOLLOWING,
      *       ELSE UNPREDICTABLE RESULTS MAY OCCUR:
      *
      *   1.  IF PROVIDING INPUT DATA, THE DATA FORMAT MUST MATCH
      *       THE INPUT PARM DESCRIBING THAT DATA
      *       IE:  DATA: 98/078
      *              IN: CY/DDD
      *
      *       THESE DO NOT MATCH.  '98/078' IMPLIES 'Y/DDD'.
      *
      *   2.  INCOMPATIBLE FORMATS SHOULD NOT BE MIXED:
      *       IE:  DATA: 132400
      *              IN: HMSM
      *             OUT: CYMD
      *
      *       HERE YOU PROVIDED A TIME VALUE AND ASKED THAT IT BE
      *       CONVERTED TO A DATE.
      *
      *   3.  WHEN PROVIDING A DATE IN CYMD FORMAT OTHER THAN
      *       CURRENT DATE, SPECIFY 'CYMD  ' FOR THE 'IN' PARM,
      *       RATHER THAN '      ', WHICH CAUSES CURRENT DATE TO
      *       BE USED.
      *
      *   4.  BE CAREFUL WHEN LOADING STV1400B-DATA.  IT IS DEFINED
      *       AS PIC X(10).  BE CAREFUL WHEN MOVING NUMERIC FIELDS
      *       TO IT; BE SURE ALL DATA IS LEFT JUSTIFIED.
      *       USE STV1400B-CYMD (BELOW) IF IT IS EASIER.
      *
      *   5.  ALWAYS TEST STV1400B-ERROR FOR 'ERROR ' AFTER THE CALL.
      *       AN ERROR NUMBER IS ALSO PROVIDED IN STV1400B-NBR.
      *
      *-----------------------------------------------------------
      *
      * EXAMPLE  :   MARCH 19, 1998, THURSDAY
      * DATE     :   98078 JULIAN,  1:24 PM
      *
      *-----------------------------------------------------------
      *
      * --->     10 BYTE DATA FIELD:
      *               INPUT:  FORMATTED AS 'IN' DATA BELOW.
      *                       SPACES (DEFAULT) IS CYMD.
      *               OUTPUT: FORMATTED AS 'OUT' DATA BELOW,
      *                       OR CONTAINS 'ERROR ####'
      *
      * --->     10 BYTE DATA2 FIELD:
      *               ONLY USED WHEN PROVIDING A DATE OTHER THAN
      *               CURRENT DATE TO THE NBR-OF-DAYS OR PATIENT
      *               AGE ROUTINES.  SEND IN THE SAME FORMAT AS THE
      *               10 BYTE DATA FIELD ABOVE.
      *               ALSO USED TO PROVIDE A FUTURE TIME VALUE.
      *
      * --->     6 BYTE 'IN' DATA FORMAT:
      *
      *              HERE YOU TELL STV1400B THE FORMAT OF THE DATE
      *              OR OTHER DATA, SO THAT STV1400B CAN RECOGNIZE
      *              AND CONVERT IT AS NEEDED.
      *
      *              IN            10 BYTE FIELD CONTENTS
      *              --------      ----------------------
      *              '      '      CURRENT CYMD
      *              'CY    '      '1998      '
      *              'CY/DDD'      '1998/078  '
      *              'CY/M/D'      '1998/03/19'
      *              'CYDDD '      '1998078   '
      *              'CYMD  '      '19980319  '
      *              'DDD   '      '078       '
      *              'HMM   '      '1324      '   MILITARY TIME
      *              'HMSM  '      '132400    '   MILITARY TIME
      *              'H:MM  '      '13:24     '   MILITARY TIME
      *              'H:M:SM'      '13:24:00  '   MILITARY TIME
      *              'HMP   '      '0124PM    '   STANDARD TIME
      *              'H:M P '      '01:24 PM  '   STANDARD TIME
      *              'H:MP  '      '01:24PM   '   STANDARD TIME
      *              'KEY   '      '30019681  '   = 19980319
      *              'M     '      '03        '
      *              'M/D   '      '03/19     '
      *              'M/D/CY'      '03/19/1998'
      *              'M/D/Y '      '03/19/98  '
      *              'MD    '      '0319      '
      *              'MDCY  '      '03191998  '
      *              'MDY   '      '031998    '
      *              'MMM   '      'SEP       '
      *              'MMMCY '      'SEP1998   '
      *              'MMMD  '      'SEP19     '
      *              'MMMYD '      'SEP9819   '
      *              'MONTH '      'SEPTEMBER '
      *              'Y     '      '98        '
      *              'Y/DDD '      '98/078    '
      *              'Y/M/D '      '98/03/19  '
      *              'YDDD  '      '98078     '
      *              'YMD   '      '980319    '
      *
      * --->     6 BYTE 'OUT' DATA FORMAT:
      *
      *              HERE YOU TELL STV1400B THE FORMAT OF THE
      *              DATA THAT YOU WANT AS OUTPUT.
      *
      *              OUT           10 BYTE FIELD RETURNED
      *              --------      ----------------------
      *              '      '      'IN' CYMD DATA, OR CURRENT CYMD
      *              '+00134'      '19980731  '   days future
      *              '-00023'      '19980224  '   days past
      *              'I00134'      '15380000  '   increment # minutes
      *                                   |--> ## nbr days calc'd past
      *              'D00023'      '13010000  '   decrement # minutes
      *              'AGE   '      '0450210   '   YYYMMDD
      *              'CDDEY2'      '1996366   '   EOY - CYDDD - past
      *              'CDDE02'      '1998031   '   EOM - CYDDD - past
      *              'CDD002'      '1998076   '   CYDDD minus 2 days
      *              'CDFEY2'      '2000366   '   EOY - CYDDD - future
      *              'CDFE02'      '1998151   '   EOM - CYDDD - future
      *              'CDF002'      '1998080   '   CYDDD plus 2 days
      *              'CMDEY2'      '19961231  '   EOY - CYMD - past
      *              'CMDE02'      '19980131  '   EOM - CYMD - past
      *              'CMDX  '      '19980319  '   CYMD
      *              'CMD002'      '19980317  '   CYMD minus 2 days
      *              'CMFEY2'      '20001231  '   EOY - CYMD - future
      *              'CMFE02'      '19980531  '   EOM - CYMD - future
      *              'CMF002'      '19980321  '   CYMD plus 2 days
      *              'CY    '      '1998      '
      *              'CY/DDD'      '1998/078  '
      *              'CY/M/D'      '1998/03/19'
      *              'CYDDD '      '1998078   '
      *              'CYDX  '      '1998      '
      *              'CYD002'      '1996      '   CY minus 2 years
      *              'CYF002'      '2000      '   CY plus 2 years
      *              'CYMD  '      '19980319  '
      *              'D     '      '19        '
      *              'DAY   '      'THURSDAY 4'   MON=1...SUN=7
      *              'DAYS  '      '00034     '   CURR - IN-DATE
      *              'DDD   '      '078       '
      *              'HMM   '      '1324      '   MILITARY TIME
      *              'HMSM  '      '132400    '   MILITARY TIME
      *              'H:MM  '      '13:24     '   MILITARY TIME
      *              'H:M:SM'      '13:24:00  '   MILITARY TIME
      *              'HMP   '      '0124PM    '   STANDARD TIME
      *              'H:M P '      '01:24 PM  '   STANDARD TIME
      *              'H:MP  '      '01:24PM   '   STANDARD TIME
      *              'KEY   '      '19980319  '   = 30019681
      *              'M     '      '03        '
      *              'M/D   '      '03/19     '
      *              'M/D/CY'      '03/19/1998'
      *              'M/D/Y '      '03/19/98  '
      *              'MD    '      '0319      '
      *              'MDCY  '      '03191998  '
      *              'MDY   '      '031998    '
      *              'MIN   '      '001440    '  # MINUTES BETWEEN TIMES
      *              'MIN - note: time can be passed as HHMMSS## where
      *                           ## = number days in past (or future
      *                           if valueing DATA2)
      *              'MMM   '      'MAR       '
      *              'MMMCY '      'MAR1998   '
      *              'MMMD  '      'MAR19     '
      *              'MMMYD '      'MAR9819   '
      *              'MONTH '      'MARCH     '
      *              'MYDEY2'      '123196    '   EOY - MDY - past
      *              'MYDE03'      '123197    '   EOM - MDY - past
      *              'MYD002'      '031798    '   MDY minus 2 days
      *              'MYFEY2'      '123100    '   EOY - MDY - future
      *              'MYFE03'      '063098    '   EOM - MDY - future
      *              'MYF002'      '032198    '   MDY plus 2 days
      *              'QTR   '      '1         '   1/2/3/4
      *              'SUFFIX'      'MARCH     '   IN=' 1ST'  OUT='1998'
      *              'Y     '      '98        '
      *              'YDDEY2'      '96366     '   EOY - YDDD - past
      *              'YDDE03'      '97365     '   EOM - YDDD - past
      *              'YDD002'      '98076     '   YDDD minus 2 days
      *              'YDFEY2'      '00366     '   EOY - YDDD - future
      *              'YDFE03'      '98181     '   EOM - YDDD - future
      *              'YDF002'      '98080     '   YDDD plus 2 days
      *              'YMDEY2'      '961231    '   EOY - YMD - past
      *              'YMDE03'      '971231    '   EOM - YMD - past
      *              'YMD002'      '980317    '   YMD minus 2 days
      *              'YMFEY2'      '001231    '   EOY - YMD - future
      *              'YMFE03'      '980630    '   EOM - YMD - future
      *              'YMF002'      '980321    '   YMD plus 2 days
      *              'Y/DDD '      '98/078    '
      *              'Y/M/D '      '98/03/19  '
      *              'YDDD  '      '98078     '
      *              'YMD   '      '980319    '
      *
      *
      *-----------------------------------------------------------
      *    CALL PROGRAMS REFERENCED BY THIS PROGRAM
      *         STV0280B         AGE IN YEARS, MONTHS, DAYS
      *         STV1295B         MONTH NAME/DAY SUFFIX
      *         STV1320B         MILITARY/STANDARD TIME CONVERSION
      *         STV1405B         CYMD CYDDD DATE EDIT
      *         STV1410B         JULIAN/GREGORIAN CONVERSION
      *         STV1420B         DATE/NBR DAYS CONVERSION
      *         STV1805B         DAY NAME AND NUMBER
      *-----------------------------------------------------------
      *
      *    01  WS-STV1400B-FIELDS.
      *        05  STV1400B-DATA         PIC  X(10)   VALUE SPACE.
      *        05  FILLER REDEFINES STV1400B-DATA.
      *            10  STV1400B-ERROR    PIC  X(06).
      *            10  STV1400B-NBR      PIC  X(04).
      *        05  FILLER REDEFINES STV1400B-DATA.
      *            10  STV1400B-CYMD     PIC  X(08).
      *            10  FILLER            PIC  XX.
      *        05  FILLER REDEFINES STV1400B-DATA.
      *            10  STV1400B-DAYS     PIC  X(05).
      *            10  FILLER            PIC  X(05).
      *        05  FILLER REDEFINES STV1400B-DATA.
      *            10  STV1400B-DAY      PIC  X(09).
      *            10  STV1400B-DAY-NBR  PIC  X.
      *        05  STV1400B-DATA2        PIC  X(10)   VALUE SPACE.
      *        05  STV1400B-IN           PIC  X(06)   VALUE SPACE.
      *        05  STV1400B-OUT          PIC  X(06)   VALUE SPACE.
      *
      *-----------------------------------------------------------
      *
      *    EXAMPLES:
      *
      *    MOVE SPACE                         TO WS-STV1400B-FIELDS.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = '19980319  '
      *                STV1400B-IN     = '      '
      *                STV1400B-OUT    = '      '
      *
      *    MOVE SPACE                         TO WS-STV1400B-FIELDS.
      *    MOVE 'M/D/Y '                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = '03/19/98  '
      *
      *    MOVE '980319    '                  TO STV1400B-DATA.
      *    MOVE 'YMD   '                      TO STV1400B-IN.
      *    MOVE '-0002 '                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = '19980317  '
      *
      *    MOVE '1998/03/19'                  TO STV1400B-DATA.
      *    MOVE 'CY/M/D'                      TO STV1400B-IN.
      *    MOVE 'CYDDD '                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = '1998078   '
      *
      *    MOVE '980912    '                  TO STV1400B-DATA.
      *    MOVE 'YMD   '                      TO STV1400B-IN.
      *    MOVE 'MONTH '                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = 'SEPTEMBER '
      *
      *    MOVE '989_2_    '                  TO STV1400B-DATA.
      *    MOVE 'YMD   '                      TO STV1400B-IN.
      *    MOVE 'CYMD  '                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = '19980902  '
      *
      *    MOVE '19980311  '                  TO STV1400B-DATA.
      *    MOVE 'CYMD  '                      TO STV1400B-IN.
      *    MOVE 'DAYS  '                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = '00008     '
      *
      *    MOVE '980311    '                  TO STV1400B-DATA.
      *    MOVE '980317    '                  TO STV1400B-DATA2.
      *    MOVE 'YMD   '                      TO STV1400B-IN.
      *    MOVE 'DAYS  '                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = '00006     '
      *
      *    MOVE '          '                  TO STV1400B-DATA.
      *    MOVE '      '                      TO STV1400B-IN.
      *    MOVE 'SUFFIX'                      TO STV1400B-OUT.
      *    CALL 'STV1400B' USING DFHEIBLK DFHCOMMAREA
      *                          WS-STV1400B-FIELDS.
      *    IF STV1400B-ERROR NOT = 'ERROR' ...
      *    RESULTS:    STV1400B-DATA   = 'MARCH     '
      *                STV1400B-IN     = '19TH  '
      *                STV1400B-OUT    = '1998  '
      *
      ******************************************************************

       ENVIRONMENT DIVISION.
       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING-STORAGE STV1400B START '.

       01  WS-FIELDS.
           05  WS-LS-DATA              PIC  X(10)    VALUE SPACE.
           05  FILLER REDEFINES WS-LS-DATA.
               10  WS-LS-DATA-1X2      PIC  XX.
               10  WS-LS-DATA-3X4      PIC  XX.
               10  WS-LS-DATA-5X6      PIC  XX.
               10  WS-LS-DATA-7X8      PIC  XX.
               10  WS-LS-DATA-9X10     PIC  XX.
           05  FILLER REDEFINES WS-LS-DATA.
               10  FILLER              PIC  XX.
               10  WS-LS-DATA-3        PIC  X.
               10  WS-LS-DATA-4X5      PIC  XX.
               10  WS-LS-DATA-6        PIC  X.
               10  FILLER              PIC  X(04).
           05  FILLER REDEFINES WS-LS-DATA.
               10  WS-LS-DATA-1X8      PIC  X(08).
               10  FILLER              PIC  XX.
           05  WS-LS-DATA2             PIC  X(10)    VALUE SPACE.
           05  FILLER REDEFINES WS-LS-DATA2.
               10  WS-LS-DATA2-C       PIC  XX.
               10  WS-LS-DATA2-Y       PIC  XX.
               10  WS-LS-DATA2-M       PIC  XX.
               10  WS-LS-DATA2-D       PIC  XX.
               10  FILLER              PIC  XX.
           05  WS-LS-IN                PIC  X(06)    VALUE SPACE.
           05  WS-LS-OUT               PIC  X(06)    VALUE SPACE.
           05  WS-1-CYMD               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-1-CYMD.
               10  WS-1-CYMD-C         PIC  XX.
               10  WS-1-CYMD-Y         PIC  XX.
               10  WS-1-CYMD-M         PIC  XX.
               10  WS-1-CYMD-D         PIC  XX.
           05  WS-2-CYMD               PIC  9(08)    VALUE ZERO.
           05  FILLER REDEFINES WS-2-CYMD.
               10  WS-2-CYMD-C         PIC  99.
               10  WS-2-CYMD-Y         PIC  99.
               10  WS-2-CYMD-M         PIC  99.
               10  WS-2-CYMD-D         PIC  99.
           05  FILLER REDEFINES WS-2-CYMD.
               10  WS-2-CYMD-CY        PIC  9(04).
               10  FILLER              PIC  9(04).
           05  WS-2-CYMD-X REDEFINES WS-2-CYMD
                                       PIC  X(08).
           05  WS-I-CYMD               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-I-CYMD.
               10  WS-I-CYMD-CY-N      PIC  9(04).
               10  FILLER              PIC  9(04).
           05  FILLER REDEFINES WS-I-CYMD.
               10  WS-I-CYMD-C         PIC  XX.
               10  WS-I-CYMD-YMD       PIC  X(06).
               10  FILLER REDEFINES WS-I-CYMD-YMD.
                   15  WS-I-CYMD-Y     PIC  XX.
                   15  WS-I-CYMD-Y-N REDEFINES WS-I-CYMD-Y
                                       PIC  99.
                   15  WS-I-CYMD-M     PIC  XX.
                   15  WS-I-CYMD-D     PIC  XX.
           05  WS-O-CYMD               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-O-CYMD.
               10  WS-O-CYMD-C         PIC  XX.
               10  WS-O-CYMD-YMD       PIC  X(06).
               10  FILLER REDEFINES WS-O-CYMD-YMD.
                   15  WS-O-CYMD-Y     PIC  XX.
                   15  WS-O-CYMD-M     PIC  XX.
                   15  WS-O-CYMD-D     PIC  XX.
           05  WS-I-HMS                PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-I-HMS.
               10  WS-I-HMS-1X6.
                   15  WS-I-HMS-H      PIC  XX.
                   15  WS-I-HMS-H-N REDEFINES WS-I-HMS-H
                                       PIC  99.
                   15  WS-I-HMS-M      PIC  XX.
                   15  WS-I-HMS-M-N REDEFINES WS-I-HMS-M
                                       PIC  99.
                   15  WS-I-HMS-S      PIC  XX.
               10  WS-I-HMS-N REDEFINES WS-I-HMS-1X6
                                       PIC  9(06).
               10  WS-I-HMS-DAYS       PIC  XX.
               10  WS-I-HMS-DAYS-N REDEFINES WS-I-HMS-DAYS
                                       PIC  99.
           05  WS-I-HMS2               PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-I-HMS2.
               10  WS-I-HMS2-1X6.
                   15  WS-I-HMS2-H     PIC  XX.
                   15  WS-I-HMS2-H-N REDEFINES WS-I-HMS2-H
                                       PIC  99.
                   15  WS-I-HMS2-M     PIC  XX.
                   15  WS-I-HMS2-M-N REDEFINES WS-I-HMS2-M
                                       PIC  99.
                   15  WS-I-HMS2-S     PIC  XX.
               10  WS-I-HMS2-DAYS      PIC  XX.
               10  WS-I-HMS2-DAYS-N REDEFINES WS-I-HMS2-DAYS
                                       PIC  99.
           05  WS-O-HMS                PIC  X(08)    VALUE SPACE.
           05  FILLER REDEFINES WS-O-HMS.
               10  WS-O-HMS-1X6.
                   15  WS-O-HMS-H      PIC  XX.
                   15  WS-O-HMS-M      PIC  XX.
                   15  WS-O-HMS-S      PIC  XX.
               10  WS-O-HMS-DAYS       PIC  XX.
           05  WS-REV-DATE             PIC  9(08)    VALUE ZERO.
           05  WS-ERROR-NBR            PIC  9(04)    VALUE ZERO.
           05  WS-1-8020               PIC  9(04)    VALUE ZERO.
           05  FILLER REDEFINES WS-1-8020.
               10  WS-1-8020-C         PIC  99.
               10  WS-1-8020-Y         PIC  99.
           05  WS-2-8020-C             PIC  99       VALUE ZERO.
           05  WS-3-8020-C             PIC  99       VALUE ZERO.
           05  WS-EOM-NBR              PIC  99       VALUE ZERO.
           05  WS-ABSTIME              PIC S9(16)    VALUE ZERO COMP.
           05  WS-ASK-TIME             PIC  X(08)    VALUE SPACE.
           05  WS-ASK-YMD              PIC  X(08)    VALUE SPACE.
           05  WS-ASK-YEAR             PIC S9(08)    VALUE ZERO COMP.
           05  WS-FUTURE-SW            PIC  X        VALUE SPACE.
           05  WS-QUOTIENT             PIC  9(05)    VALUE ZERO.
           05  WS-REMAINDER            PIC  9(05)    VALUE ZERO.
           05  WS-PAST-MIN             PIC S9(06)    VALUE ZERO.
           05  WS-FUTURE-MIN           PIC  9(06)    VALUE ZERO.
           05  WS-HOLD-OUT             PIC  X(06)    VALUE SPACE.

       01  WS-STV1420B-FIELDS.
           05  STV1420B-PAST-DATE      PIC  X(08).
           05  FILLER REDEFINES STV1420B-PAST-DATE.
               10  STV1420B-CC         PIC  XX.
               10  STV1420B-YY         PIC  XX.
               10  STV1420B-MM         PIC  XX.
               10  STV1420B-DD         PIC  XX.
           05  STV1420B-FUTURE-DATE    PIC  X(08).
           05  STV1420B-DAYS           PIC  X(05).

      *    YEAR 2000 COMPLIANT JULIAN/GREGORIAN DATE CONVERSION
       01  WS-STV1410B-FIELDS.
           05  STV1410B-IN-DATE        PIC  X(08).
           05  STV1410B-CYDDD          PIC  X(07).
           05  STV1410B-CYMD           PIC  X(08).

      *    RETURN THE DAY OF THE WEEK GIVEN A DATE IN CCYYMMDD FORMAT
       01  WS-STV1805B-FIELDS.
           05  STV1805B-CCYYMMDD       PIC  X(08).
           05  STV1805B-DAY            PIC  X(09).
           05  STV1805B-NBR-DAY        PIC  XX.

      *    STV1295B IS USED TO RETURN A MONTH NAME AND DAY SUFFIX
       01  WS-STV1295B-FIELDS.
           05  STV1295B-DATE           PIC  X(08).
           05  FILLER REDEFINES STV1295B-DATE.
               10  STV1295B-CC         PIC  XX.
               10  STV1295B-YY         PIC  XX.
               10  STV1295B-MM         PIC  XX.
               10  STV1295B-DD         PIC  XX.
           05  STV1295B-MONTH          PIC  X(12).
           05  STV1295B-DAY            PIC  X(04).
           05  STV1295B-YEAR           PIC  X(04).

      *    STV0280B WILL RETURN A PERSONS AGE IN YEARS, MONTHS, DAYS
       01  WS-STV0280B-FIELDS.
           05  STV0280B-DOB-CYMD       PIC  X(08).
           05  STV0280B-AS-OF-CYMD     PIC  X(08).
           05  STV0280B-AGE.
               10  STV0280B-YEARS      PIC  XXX.
               10  STV0280B-MONTHS     PIC  XX.
               10  STV0280B-DAYS       PIC  XX.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  END STV1400B  '.

       LINKAGE SECTION.

       01  LS-FIELDS.
           05  LS-DATA                 PIC  X(10).
           05  LS-DATA2                PIC  X(10).
           05  LS-IN                   PIC  X(06).
           05  LS-OUT                  PIC  X(06).

       PROCEDURE DIVISION USING LS-FIELDS.

       0100-START-PROCESS.

      ****************************************
      *    ALTER THIS CODE TO MAKE YEAR 2000 COMPLIANT
      *    AFTER MOVING TO YEAR 2000 COMPLIANT VERSION OF COBOL.
      *    ALSO ALTER 0400-DETERMINE-CENTURY.
      *+**************************************
           EXEC CICS ASKTIME
               ABSTIME (WS-ABSTIME)
           END-EXEC.
           EXEC CICS FORMATTIME
               ABSTIME (WS-ABSTIME)
               TIME (WS-ASK-TIME)
               YYMMDD (WS-ASK-YMD)
               YEAR (WS-ASK-YEAR)
           END-EXEC.
           MOVE WS-ASK-TIME                   TO WS-I-HMS-1X6.
           MOVE WS-ASK-YMD                    TO WS-I-CYMD-YMD.
           MOVE WS-ASK-YEAR                   TO WS-1-8020.
           MOVE WS-1-8020                     TO WS-I-CYMD-CY-N.
      *    determine 80/20 rolling window
           SUBTRACT 80                        FROM WS-1-8020.
           MOVE WS-1-8020-C                   TO WS-2-8020-C
                                                 WS-3-8020-C.
           ADD 1                              TO WS-2-8020-C.
           SUBTRACT 1                         FROM WS-3-8020-C.

      ****************************************
      *    CONVERT TO, THEN FROM, CYMD FORMAT
      *+**************************************
           MOVE ZERO                          TO WS-ERROR-NBR.
           MOVE LS-DATA                       TO WS-LS-DATA.
           MOVE LS-DATA2                      TO WS-LS-DATA2.
           MOVE LS-IN                         TO WS-LS-IN.
           MOVE LS-OUT                        TO WS-LS-OUT.
           INSPECT WS-LS-IN CONVERTING 'abcdefghijklmnopqrstuvwxyz'
                                    TO 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.
           INSPECT WS-LS-OUT CONVERTING 'abcdefghijklmnopqrstuvwxyz'
                                     TO 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.
           INSPECT WS-LS-DATA CONVERTING 'abcdefghijklmnopqrstuvwxyz'
                                      TO 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.
           INSPECT WS-LS-DATA2 CONVERTING 'abcdefghijklmnopqrstuvwxyz'
                                       TO 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.
           MOVE WS-I-CYMD                     TO WS-1-CYMD.
           MOVE WS-I-HMS                      TO WS-I-HMS2.
           IF WS-LS-DATA NOT = SPACE
               PERFORM 0300-CONVERT-TO-STANDARD
           ELSE
           IF WS-LS-DATA2 = SPACE
               PERFORM 0300-CONVERT-TO-STANDARD.
           IF WS-ERROR-NBR > ZERO
               GO TO 4000-RETURN-ERROR.
           IF WS-LS-DATA2 NOT = SPACE
               MOVE WS-I-CYMD                 TO WS-O-CYMD
               MOVE WS-1-CYMD                 TO WS-I-CYMD
               MOVE WS-LS-DATA2               TO WS-LS-DATA
               MOVE WS-I-HMS                  TO WS-O-HMS
               PERFORM 0300-CONVERT-TO-STANDARD
               IF WS-ERROR-NBR > ZERO
                   GO TO 4000-RETURN-ERROR
               ELSE
                   MOVE WS-I-CYMD             TO WS-LS-DATA2
                   MOVE WS-O-CYMD             TO WS-I-CYMD
                   MOVE WS-I-HMS              TO WS-I-HMS2
                   MOVE WS-O-HMS              TO WS-I-HMS.
           PERFORM 0500-CONVERT-FROM-STANDARD.
           IF WS-ERROR-NBR > ZERO
               GO TO 4000-RETURN-ERROR.
           GO TO 4200-EXIT-PROGRAM.

       0300-CONVERT-TO-STANDARD.
           EVALUATE WS-LS-IN
               WHEN SPACE
                   CONTINUE
               WHEN 'CY    '
                   MOVE WS-LS-DATA(1: 4)      TO WS-I-CYMD(1: 4)
                   PERFORM 0450-EDIT-DATE
               WHEN 'CY/DDD'
                   MOVE WS-LS-DATA(1: 4)      TO STV1410B-IN-DATE
                   MOVE WS-LS-DATA(6: 3)      TO STV1410B-IN-DATE(5: 3)
                   PERFORM 0800-CONV-TO-GREG
                   PERFORM 0450-EDIT-DATE
               WHEN 'CY/M/D'
                   MOVE WS-LS-DATA(1: 4)      TO WS-I-CYMD
                   MOVE WS-LS-DATA(6: 2)      TO WS-I-CYMD-M
                   MOVE WS-LS-DATA(9: 2)      TO WS-I-CYMD-D
                   PERFORM 0450-EDIT-DATE
               WHEN 'CYDDD '
                   MOVE WS-LS-DATA(1: 7)      TO STV1410B-IN-DATE
                   PERFORM 0800-CONV-TO-GREG
                   PERFORM 0450-EDIT-DATE
               WHEN 'CYMD  '
                   MOVE WS-LS-DATA-1X8        TO WS-I-CYMD
                   PERFORM 0450-EDIT-DATE
               WHEN 'DDD   '
                   MOVE WS-I-CYMD(1: 4)       TO STV1410B-IN-DATE
                   MOVE WS-LS-DATA(1: 3)      TO STV1410B-IN-DATE(5: 3)
                   PERFORM 0800-CONV-TO-GREG
                   PERFORM 0450-EDIT-DATE
               WHEN 'HMM   '
                   MOVE WS-LS-DATA-1X2        TO WS-I-HMS-H
                   MOVE WS-LS-DATA-3X4        TO WS-I-HMS-M
               WHEN 'HMSM  '
                   MOVE WS-LS-DATA(1: 8)      TO WS-I-HMS
               WHEN 'H:MM  '
                   MOVE WS-LS-DATA-1X2        TO WS-I-HMS-H
                   MOVE WS-LS-DATA-4X5        TO WS-I-HMS-M
               WHEN 'H:M:SM'
                   MOVE WS-LS-DATA-1X2        TO WS-I-HMS-H
                   MOVE WS-LS-DATA-4X5        TO WS-I-HMS-M
                   MOVE WS-LS-DATA-7X8        TO WS-I-HMS-S
               WHEN 'HMP   '
                   MOVE WS-LS-DATA(1: 8)      TO WS-I-HMS
                   PERFORM 1500-CONVERT-TO-MILITARY
               WHEN 'H:M P '
                   MOVE WS-LS-DATA-1X2        TO WS-I-HMS-H
                   MOVE WS-LS-DATA-4X5        TO WS-I-HMS-M
                   MOVE WS-LS-DATA-7X8        TO WS-I-HMS-S
                   PERFORM 1500-CONVERT-TO-MILITARY
               WHEN 'H:MP  '
                   MOVE WS-LS-DATA-1X2        TO WS-I-HMS-H
                   MOVE WS-LS-DATA-4X5        TO WS-I-HMS-M
                   MOVE WS-LS-DATA(6: 2)      TO WS-I-HMS-S
                   PERFORM 1500-CONVERT-TO-MILITARY
               WHEN 'KEY   '
                   PERFORM 2100-CONVERT-FROM-KEY
                   PERFORM 0450-EDIT-DATE
               WHEN 'M     '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-M
               WHEN 'M/D   '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-M
                   MOVE WS-LS-DATA(4: 2)      TO WS-I-CYMD-D
               WHEN 'M/D/CY'
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-M
                   MOVE WS-LS-DATA(4: 2)      TO WS-I-CYMD-D
                   MOVE WS-LS-DATA(7: 4)      TO WS-I-CYMD(1: 4)
                   PERFORM 0450-EDIT-DATE
               WHEN 'M/D/Y '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-M
                   MOVE WS-LS-DATA(4: 2)      TO WS-I-CYMD-D
                   MOVE WS-LS-DATA(7: 2)      TO WS-I-CYMD-Y
                   PERFORM 0400-DETERMINE-CENTURY
                   PERFORM 0400-CENTURY-OUTSIDE-RANGE
                   PERFORM 0450-EDIT-DATE
               WHEN 'MD    '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-M
                   MOVE WS-LS-DATA-3X4        TO WS-I-CYMD-D
               WHEN 'MDCY  '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-M
                   MOVE WS-LS-DATA-3X4        TO WS-I-CYMD-D
                   MOVE WS-LS-DATA-5X6        TO WS-I-CYMD-C
                   MOVE WS-LS-DATA-7X8        TO WS-I-CYMD-Y
                   PERFORM 0450-EDIT-DATE
               WHEN 'MDY   '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-M
                   MOVE WS-LS-DATA-3X4        TO WS-I-CYMD-D
                   MOVE WS-LS-DATA-5X6        TO WS-I-CYMD-Y
                   PERFORM 0400-DETERMINE-CENTURY
                   PERFORM 0450-EDIT-DATE
               WHEN 'MMM   '
                   PERFORM 1000-DETERMINE-MONTH-NBR
               WHEN 'MMMCY '
                   MOVE WS-LS-DATA-4X5        TO WS-I-CYMD-C
                   MOVE WS-LS-DATA(6: 2)      TO WS-I-CYMD-Y
                   MOVE SPACE                 TO WS-LS-DATA(4: )
                   PERFORM 1000-DETERMINE-MONTH-NBR
                   PERFORM 0450-EDIT-DATE
               WHEN 'MMMD  '
                   MOVE WS-LS-DATA-4X5        TO WS-I-CYMD-D
                   MOVE SPACE                 TO WS-LS-DATA(4: )
                   PERFORM 1000-DETERMINE-MONTH-NBR
               WHEN 'MMMYD '
                   MOVE WS-LS-DATA-4X5        TO WS-I-CYMD-Y
                   MOVE WS-LS-DATA(6: 2)      TO WS-I-CYMD-D
                   MOVE SPACE                 TO WS-LS-DATA(4: )
                   PERFORM 1000-DETERMINE-MONTH-NBR
                   PERFORM 0400-DETERMINE-CENTURY
                   PERFORM 0450-EDIT-DATE
               WHEN 'MONTH '
                   PERFORM 1000-DETERMINE-MONTH-NBR
               WHEN 'Y     '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-Y
                   PERFORM 0400-DETERMINE-CENTURY
               WHEN 'Y/DDD '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-Y
                   PERFORM 0400-DETERMINE-CENTURY
                   MOVE WS-I-CYMD(1: 4)       TO STV1410B-IN-DATE
                   MOVE WS-LS-DATA(4: 3)      TO STV1410B-IN-DATE(5: 3)
                   PERFORM 0800-CONV-TO-GREG
                   PERFORM 0450-EDIT-DATE
               WHEN 'Y/M/D '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-Y
                   MOVE WS-LS-DATA(4: 2)      TO WS-I-CYMD-M
                   MOVE WS-LS-DATA(7: 2)      TO WS-I-CYMD-D
                   PERFORM 0400-DETERMINE-CENTURY
                   PERFORM 0400-CENTURY-OUTSIDE-RANGE
                   PERFORM 0450-EDIT-DATE
               WHEN 'YDDD  '
                   MOVE WS-LS-DATA-1X2        TO WS-I-CYMD-Y
                   PERFORM 0400-DETERMINE-CENTURY
                   MOVE WS-I-CYMD(1: 4)       TO STV1410B-IN-DATE
                   MOVE WS-LS-DATA(3: 3)      TO STV1410B-IN-DATE(5: 3)
                   PERFORM 0800-CONV-TO-GREG
                   PERFORM 0450-EDIT-DATE
               WHEN 'YMD   '
                   MOVE WS-LS-DATA(1: 6)      TO WS-I-CYMD-YMD
                   PERFORM 0400-DETERMINE-CENTURY
                   PERFORM 0450-EDIT-DATE
               WHEN OTHER
                   MOVE 300                   TO WS-ERROR-NBR
           END-EVALUATE.

      ****************************************
      *    Y2K logic for rolling 80/20 window
      ****************************************
      *    yy < current yy        '/'  =  cc+1
      *    yy not < current yy    '/'  =  cc
      *    yy < current yy        '-'  =  cc
      *    yy not < current yy    '-'  =  cc-1
      ****************************************
       0400-DETERMINE-CENTURY.
           IF WS-I-CYMD-Y-N < WS-1-8020-Y
               MOVE WS-2-8020-C               TO WS-I-CYMD-C
           ELSE
               MOVE WS-1-8020-C               TO WS-I-CYMD-C.

       0400-CENTURY-OUTSIDE-RANGE.
           IF WS-LS-DATA-6 = '-'
               IF WS-I-CYMD-Y-N < WS-1-8020-Y
                   MOVE WS-1-8020-C           TO WS-I-CYMD-C
               ELSE
                   MOVE WS-3-8020-C           TO WS-I-CYMD-C.

       0450-EDIT-DATE.
           IF WS-ERROR-NBR = ZERO
               CALL 'STV1405B' USING DFHEIBLK DFHCOMMAREA
                                     WS-I-CYMD
               IF WS-I-CYMD-D NOT NUMERIC
                   MOVE 0301                  TO WS-ERROR-NBR
               ELSE IF WS-I-CYMD-M NOT NUMERIC
                   MOVE 0302                  TO WS-ERROR-NBR
               ELSE IF WS-I-CYMD-Y NOT NUMERIC
                   MOVE 0303                  TO WS-ERROR-NBR
               ELSE IF WS-I-CYMD-C NOT NUMERIC
                   MOVE 0304                  TO WS-ERROR-NBR.

       0500-CONVERT-FROM-STANDARD.
           MOVE SPACE                         TO WS-LS-DATA.
           MOVE WS-I-CYMD                     TO WS-O-CYMD.
           EVALUATE WS-LS-OUT
               WHEN SPACE
                   MOVE WS-O-CYMD             TO WS-LS-DATA
               WHEN 'AGE   '
                   PERFORM 1800-CALC-AGE
               WHEN 'CMDX  '
                   MOVE WS-O-CYMD             TO WS-LS-DATA
               WHEN 'CY    '
               WHEN 'CYDX  '
                   MOVE WS-O-CYMD(1: 4)       TO WS-LS-DATA
               WHEN 'CY/DDD'
                   PERFORM 0900-CONV-TO-JUL
                   MOVE STV1410B-CYDDD(1: 4)  TO WS-LS-DATA(1: 4)
                   MOVE '/'                   TO WS-LS-DATA(5: 1)
                   MOVE STV1410B-CYDDD(5: 3)  TO WS-LS-DATA(6: 3)
               WHEN 'CY/M/D'
                   MOVE WS-O-CYMD-C           TO WS-LS-DATA-1X2
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA-3X4
                   MOVE '/'                   TO WS-LS-DATA(5: 1)
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA(6: 2)
                   MOVE '/'                   TO WS-LS-DATA(8: 1)
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA(9: 2)
               WHEN 'CYDDD '
                   PERFORM 0900-CONV-TO-JUL
                   MOVE STV1410B-CYDDD        TO WS-LS-DATA
               WHEN 'CYMD  '
                   MOVE WS-O-CYMD             TO WS-LS-DATA
               WHEN 'D     '
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA-1X2
               WHEN 'DAY   '
                   PERFORM 1400-DETERMINE-DAY
               WHEN 'DAYS  '
                   PERFORM 1900-NBR-DAYS
               WHEN 'DDD   '
                   PERFORM 0900-CONV-TO-JUL
                   MOVE STV1410B-CYDDD(5: 3)  TO WS-LS-DATA
               WHEN 'HMM   '
                   MOVE WS-I-HMS(1: 4)        TO WS-LS-DATA
               WHEN 'HMSM  '
                   MOVE WS-I-HMS              TO WS-LS-DATA
               WHEN 'H:MM  '
                   MOVE WS-I-HMS-H            TO WS-LS-DATA-1X2
                   MOVE ':'                   TO WS-LS-DATA-3
                   MOVE WS-I-HMS-M            TO WS-LS-DATA-4X5
               WHEN 'H:M:SM'
                   MOVE WS-I-HMS-H            TO WS-LS-DATA-1X2
                   MOVE ':'                   TO WS-LS-DATA-3
                   MOVE WS-I-HMS-M            TO WS-LS-DATA-4X5
                   MOVE ':'                   TO WS-LS-DATA-6
                   MOVE WS-I-HMS-S            TO WS-LS-DATA-7X8
               WHEN 'HMP   '
                   PERFORM 1500-CONVERT-FROM-MILITARY
               WHEN 'H:M P '
                   PERFORM 1500-CONVERT-FROM-MILITARY
               WHEN 'H:MP  '
                   PERFORM 1500-CONVERT-FROM-MILITARY
               WHEN 'KEY   '
                   PERFORM 2100-CONVERT-TO-KEY
               WHEN 'M     '
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA-1X2
               WHEN 'M/D   '
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA-1X2
                   MOVE '/'                   TO WS-LS-DATA(3: 1)
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA(4: 2)
               WHEN 'M/D/CY'
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA-1X2
                   MOVE '/'                   TO WS-LS-DATA(3: 1)
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA(4: 2)
                   MOVE '/'                   TO WS-LS-DATA(6: 1)
                   MOVE WS-O-CYMD-C           TO WS-LS-DATA(7: 2)
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA(9: 2)
               WHEN 'M/D/Y '
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA-1X2
                   MOVE '/'                   TO WS-LS-DATA(3: 1)
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA(4: 2)
                   MOVE '/'                   TO WS-LS-DATA(6: 1)
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA(7: 2)
               WHEN 'MD    '
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA-1X2
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA-3X4
               WHEN 'MDCY  '
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA-1X2
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA-3X4
                   MOVE WS-O-CYMD-C           TO WS-LS-DATA-5X6
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA-7X8
               WHEN 'MDY   '
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA-1X2
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA-3X4
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA-5X6
               WHEN 'MIN   '
                   PERFORM 1300-MIN-BETWEEN-TIME
               WHEN 'MMM   '
                   PERFORM 1100-DETERMINE-MONTH-NAME
                   MOVE SPACE                 TO WS-LS-DATA(4: )
               WHEN 'MMMCY '
                   PERFORM 1100-DETERMINE-MONTH-NAME
                   MOVE WS-O-CYMD-C           TO WS-LS-DATA-4X5
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA(6: 2)
                   MOVE SPACE                 TO WS-LS-DATA(8: )
               WHEN 'MMMD  '
                   PERFORM 1100-DETERMINE-MONTH-NAME
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA-4X5
                   MOVE SPACE                 TO WS-LS-DATA(6: )
               WHEN 'MMMYD '
                   PERFORM 1100-DETERMINE-MONTH-NAME
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA-4X5
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA(6: 2)
                   MOVE SPACE                 TO WS-LS-DATA(8: )
               WHEN 'MONTH '
                   PERFORM 1100-DETERMINE-MONTH-NAME
               WHEN 'QTR   '
                   PERFORM 2200-DETERMINE-QUARTER
               WHEN 'SUFFIX'
                   PERFORM 2000-DETERMINE-SUFFIX
               WHEN 'Y     '
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA-1X2
               WHEN 'Y/DDD '
                   PERFORM 0900-CONV-TO-JUL
                   MOVE STV1410B-CYDDD(3: 2)  TO WS-LS-DATA
                   MOVE '/'                   TO WS-LS-DATA(3: 1)
                   MOVE STV1410B-CYDDD(5: 3)  TO WS-LS-DATA(4: 3)
               WHEN 'Y/M/D '
                   MOVE WS-O-CYMD-Y           TO WS-LS-DATA-1X2
                   MOVE '/'                   TO WS-LS-DATA(3: 1)
                   MOVE WS-O-CYMD-M           TO WS-LS-DATA(4: 2)
                   MOVE '/'                   TO WS-LS-DATA(6: 1)
                   MOVE WS-O-CYMD-D           TO WS-LS-DATA(7: 2)
               WHEN 'YDDD  '
                   PERFORM 0900-CONV-TO-JUL
                   MOVE STV1410B-CYDDD(3: 5)  TO WS-LS-DATA
               WHEN 'YMD   '
                   MOVE WS-O-CYMD-YMD         TO WS-LS-DATA
               WHEN OTHER
                   PERFORM 0700-OTHER-CALC
           END-EVALUATE.

       0700-OTHER-CALC.
           IF WS-LS-OUT(2: 4) NUMERIC
               IF WS-LS-OUT(1: 1) = '-' OR '+'
                   MOVE WS-LS-OUT             TO WS-HOLD-OUT
                   PERFORM 1200-NBR-DAYS
                   MOVE WS-2-CYMD-X           TO WS-LS-DATA
               ELSE
               IF WS-LS-OUT(1: 1) = 'I' OR 'D'
                   PERFORM 1300-INCREMENT-TIME
               ELSE
                   MOVE 702                   TO WS-ERROR-NBR
           ELSE
           IF WS-LS-OUT(1: 2) = 'CM' OR 'CD' OR 'YM' OR 'YD' OR 'MY'
               IF WS-LS-OUT(4: 1) = 'E'
                 IF WS-LS-OUT(5: 1) = 'Y'
                   IF WS-LS-OUT(6: 1) NUMERIC
                       MOVE WS-LS-OUT(6: 1)   TO WS-EOM-NBR
                       IF WS-LS-OUT(3: 1) = 'D'
                           MOVE SPACE         TO WS-FUTURE-SW
                           PERFORM 1700-DETERMINE-EOY
                       ELSE
                       IF WS-LS-OUT(3: 1) = 'F'
                           MOVE 'Y'           TO WS-FUTURE-SW
                           PERFORM 1700-DETERMINE-EOY
                       ELSE
                           MOVE 0708          TO WS-ERROR-NBR
                   ELSE
                       MOVE 0708              TO WS-ERROR-NBR
                 ELSE
                 IF WS-LS-OUT(5: 2) NUMERIC
                     MOVE WS-LS-OUT(5: 2)     TO WS-EOM-NBR
                     IF WS-LS-OUT(3: 1) = 'D'
                         MOVE SPACE           TO WS-FUTURE-SW
                         PERFORM 1600-DETERMINE-EOM
                     ELSE
                     IF WS-LS-OUT(3: 1) = 'F'
                         MOVE 'Y'             TO WS-FUTURE-SW
                         PERFORM 1600-DETERMINE-EOM
                     ELSE
                         MOVE 0706            TO WS-ERROR-NBR
                 ELSE
                     MOVE 0706                TO WS-ERROR-NBR
               ELSE
               IF WS-LS-OUT(4: 3) NUMERIC
                   IF WS-LS-OUT(3: 1) = 'D'
                       MOVE SPACE             TO WS-FUTURE-SW
                       PERFORM 1600-DETERMINE-EOD
                   ELSE
                   IF WS-LS-OUT(3: 1) = 'F'
                       MOVE 'Y'               TO WS-FUTURE-SW
                       PERFORM 1600-DETERMINE-EOD
                   ELSE
                       MOVE 0709              TO WS-ERROR-NBR
               ELSE
                   MOVE 0709                  TO WS-ERROR-NBR
           ELSE
           IF WS-LS-OUT(1: 3) = 'CYD' OR 'CYF'
               IF WS-LS-OUT(4: 3) NUMERIC
                   PERFORM 1400-DETERMINE-YEAR
               ELSE
                   MOVE 0710                  TO WS-ERROR-NBR
           ELSE
               MOVE 0700                      TO WS-ERROR-NBR.

       0800-CONV-TO-GREG.
           MOVE SPACE                         TO STV1410B-CYDDD
                                                 STV1410B-CYMD.
           CALL 'STV1410B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1410B-FIELDS.
           IF STV1410B-CYMD NUMERIC
               IF STV1410B-CYMD NOT = ZERO
                   MOVE STV1410B-CYMD         TO WS-I-CYMD
               ELSE
                   MOVE 800                   TO WS-ERROR-NBR
           ELSE
               MOVE 801                       TO WS-ERROR-NBR.

       0900-CONV-TO-JUL.
           MOVE WS-O-CYMD                     TO STV1410B-IN-DATE.
           MOVE SPACE                         TO STV1410B-CYDDD
                                                 STV1410B-CYMD.
           CALL 'STV1410B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1410B-FIELDS.
           IF STV1410B-CYDDD NUMERIC
               IF STV1410B-CYDDD NOT = ZERO
                   CONTINUE
               ELSE
                   MOVE 900                   TO WS-ERROR-NBR
           ELSE
               MOVE 901                       TO WS-ERROR-NBR.

       1000-DETERMINE-MONTH-NBR.
           EVALUATE WS-LS-DATA
               WHEN 'JAN       '   MOVE '01'  TO WS-I-CYMD-M
               WHEN 'JANUARY   '   MOVE '01'  TO WS-I-CYMD-M
               WHEN 'FEB       '   MOVE '02'  TO WS-I-CYMD-M
               WHEN 'FEBRUARY  '   MOVE '02'  TO WS-I-CYMD-M
               WHEN 'MAR       '   MOVE '03'  TO WS-I-CYMD-M
               WHEN 'MARCH     '   MOVE '03'  TO WS-I-CYMD-M
               WHEN 'APR       '   MOVE '04'  TO WS-I-CYMD-M
               WHEN 'APRIL     '   MOVE '04'  TO WS-I-CYMD-M
               WHEN 'MAY       '   MOVE '05'  TO WS-I-CYMD-M
               WHEN 'JUN       '   MOVE '06'  TO WS-I-CYMD-M
               WHEN 'JUNE      '   MOVE '06'  TO WS-I-CYMD-M
               WHEN 'JUL       '   MOVE '07'  TO WS-I-CYMD-M
               WHEN 'JULY      '   MOVE '07'  TO WS-I-CYMD-M
               WHEN 'AUG       '   MOVE '08'  TO WS-I-CYMD-M
               WHEN 'AUGUST    '   MOVE '08'  TO WS-I-CYMD-M
               WHEN 'SEP       '   MOVE '09'  TO WS-I-CYMD-M
               WHEN 'SEPTEMBER '   MOVE '09'  TO WS-I-CYMD-M
               WHEN 'OCT       '   MOVE '10'  TO WS-I-CYMD-M
               WHEN 'OCTOBER   '   MOVE '10'  TO WS-I-CYMD-M
               WHEN 'NOV       '   MOVE '11'  TO WS-I-CYMD-M
               WHEN 'NOVEMBER  '   MOVE '11'  TO WS-I-CYMD-M
               WHEN 'DEC       '   MOVE '12'  TO WS-I-CYMD-M
               WHEN 'DECEMBER  '   MOVE '12'  TO WS-I-CYMD-M
               WHEN OTHER          MOVE 1000  TO WS-ERROR-NBR
           END-EVALUATE.

       1100-DETERMINE-MONTH-NAME.
           MOVE SPACE                         TO WS-STV1295B-FIELDS.
           MOVE WS-O-CYMD-M                   TO STV1295B-MM.
           CALL 'STV1295B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1295B-FIELDS.
           IF STV1295B-YEAR = '****'
               MOVE 1100                      TO WS-ERROR-NBR
           ELSE
               MOVE STV1295B-MONTH            TO WS-LS-DATA.

       1200-NBR-DAYS.
           MOVE WS-HOLD-OUT(2: 5)             TO STV1420B-DAYS.
           MOVE WS-O-CYMD                     TO STV1420B-PAST-DATE.
           IF WS-HOLD-OUT(1: 1) = '-'
               MOVE STV1420B-PAST-DATE        TO STV1420B-FUTURE-DATE
               MOVE SPACE                     TO STV1420B-PAST-DATE
           ELSE
               MOVE SPACE                     TO STV1420B-FUTURE-DATE.
           CALL 'STV1420B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1420B-FIELDS.
           IF STV1420B-DAYS = '*****'
               MOVE 1200                      TO WS-ERROR-NBR
           ELSE
           IF WS-HOLD-OUT(1: 1) = '-'
               MOVE STV1420B-PAST-DATE        TO WS-2-CYMD-X
           ELSE
               MOVE STV1420B-FUTURE-DATE      TO WS-2-CYMD-X.

       1300-INCREMENT-TIME.
           MOVE ZERO                          TO WS-REMAINDER
                                                 WS-EOM-NBR.
           MOVE WS-LS-OUT(2: 5)               TO WS-QUOTIENT.
           IF WS-QUOTIENT > 59
               DIVIDE WS-QUOTIENT BY 60 GIVING WS-QUOTIENT
                   REMAINDER WS-REMAINDER
               IF WS-LS-OUT(1: 1) = 'I'
                   PERFORM 1300-INCREMENT-HOUR WS-QUOTIENT TIMES
               ELSE
                   PERFORM 1300-DECREMENT-HOUR WS-QUOTIENT TIMES
           ELSE
               MOVE WS-QUOTIENT               TO WS-REMAINDER.
           IF WS-LS-OUT(1: 1) = 'I'
               PERFORM 1300-INCREMENT-MIN WS-REMAINDER TIMES
           ELSE
               PERFORM 1300-DECREMENT-MIN WS-REMAINDER TIMES.
           MOVE WS-I-HMS                      TO WS-LS-DATA.
           MOVE WS-EOM-NBR                    TO WS-LS-DATA-7X8.

       1300-INCREMENT-MIN.
           ADD 1                              TO WS-I-HMS-M-N.
           IF WS-I-HMS-M-N > 59
               MOVE ZERO                      TO WS-I-HMS-M-N
               PERFORM 1300-INCREMENT-HOUR.

       1300-DECREMENT-MIN.
           IF WS-I-HMS-M-N = ZERO
               MOVE 59                        TO WS-I-HMS-M-N
               PERFORM 1300-DECREMENT-HOUR
           ELSE
               SUBTRACT 1 FROM WS-I-HMS-M-N.

       1300-INCREMENT-HOUR.
           ADD 1                              TO WS-I-HMS-H-N.
           IF WS-I-HMS-H-N > 23
               ADD 1                          TO WS-EOM-NBR
               MOVE ZERO                      TO WS-I-HMS-H-N.

       1300-DECREMENT-HOUR.
           IF WS-I-HMS-H-N = ZERO
               MOVE 23                        TO WS-I-HMS-H-N
               ADD 1                          TO WS-EOM-NBR
           ELSE
               SUBTRACT 1 FROM WS-I-HMS-H-N.

      ****************************************
      *    past/curr time: determine # minutes before 00:00 this morning
      *    future time: determine # minutes after 00:00 this morning
      *    determine difference
      *+**************************************
       1300-MIN-BETWEEN-TIME.
           IF WS-I-HMS-DAYS = SPACE
               MOVE '00'                      TO WS-I-HMS-DAYS.
           IF WS-I-HMS2-DAYS = SPACE
               MOVE '00'                      TO WS-I-HMS2-DAYS.
           IF WS-I-HMS NOT NUMERIC
               OR WS-I-HMS2 NOT NUMERIC
               MOVE 1300                      TO WS-ERROR-NBR
           ELSE
               COMPUTE WS-PAST-MIN = WS-I-HMS-DAYS-N * 1440
               COMPUTE WS-QUOTIENT = WS-I-HMS-H-N * 60
               ADD WS-I-HMS-M-N               TO WS-QUOTIENT
               SUBTRACT WS-QUOTIENT FROM WS-PAST-MIN
               COMPUTE WS-FUTURE-MIN = WS-I-HMS2-DAYS-N * 1440
               COMPUTE WS-QUOTIENT = WS-I-HMS2-H-N * 60
               ADD WS-I-HMS2-M-N              TO WS-QUOTIENT
               ADD WS-QUOTIENT                TO WS-FUTURE-MIN
               ADD WS-PAST-MIN                TO WS-FUTURE-MIN
               MOVE WS-FUTURE-MIN             TO WS-LS-DATA(1: 6).

       1400-DETERMINE-DAY.
           MOVE WS-O-CYMD                     TO STV1805B-CCYYMMDD.
           MOVE SPACE                         TO STV1805B-DAY.
           MOVE ZERO                          TO STV1805B-NBR-DAY.
           CALL 'STV1805B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1805B-FIELDS.
           IF STV1805B-NBR-DAY = '00'
               OR STV1805B-DAY = SPACE
               MOVE 1400                      TO WS-ERROR-NBR
           ELSE
               MOVE STV1805B-DAY              TO WS-LS-DATA
               MOVE STV1805B-NBR-DAY(2: 1)    TO WS-LS-DATA(10: 1).

       1400-DETERMINE-YEAR.
           MOVE WS-O-CYMD                     TO WS-2-CYMD.
           MOVE WS-LS-OUT(4: 3)               TO WS-FUTURE-MIN.
           IF WS-LS-OUT(1: 3) = 'CYD'
               COMPUTE WS-2-CYMD-CY = WS-2-CYMD-CY - WS-FUTURE-MIN
           ELSE
               COMPUTE WS-2-CYMD-CY = WS-2-CYMD-CY + WS-FUTURE-MIN.
           MOVE WS-2-CYMD-CY                  TO WS-LS-DATA(1: 4).

       1500-CONVERT-TO-MILITARY.
           CALL 'STV1320B' USING DFHEIBLK DFHCOMMAREA
                                 WS-I-HMS-1X6.
           IF WS-LS-IN = 'HMP   ' OR 'H:M P ' OR 'H:MP  '
               IF WS-I-HMS-S NOT NUMERIC
                   MOVE 1500                  TO WS-ERROR-NBR.

       1500-CONVERT-FROM-MILITARY.
           MOVE WS-I-HMS                      TO WS-O-HMS.
           CALL 'STV1320B' USING DFHEIBLK DFHCOMMAREA
                                 WS-O-HMS-1X6.
           IF WS-I-HMS-S = WS-O-HMS-S
               MOVE 1502                      TO WS-ERROR-NBR
           ELSE
           IF WS-LS-OUT = 'H:M P '
               MOVE WS-O-HMS-H                TO WS-LS-DATA-1X2
               MOVE ':'                       TO WS-LS-DATA-3
               MOVE WS-O-HMS-M                TO WS-LS-DATA-4X5
               MOVE SPACE                     TO WS-LS-DATA-6
               MOVE WS-O-HMS-S                TO WS-LS-DATA-7X8
           ELSE
           IF WS-LS-OUT = 'H:MP  '
               MOVE WS-O-HMS-H                TO WS-LS-DATA-1X2
               MOVE ':'                       TO WS-LS-DATA-3
               MOVE WS-O-HMS-M                TO WS-LS-DATA-4X5
               MOVE WS-O-HMS-S                TO WS-LS-DATA(6: 2)
           ELSE
               MOVE WS-O-HMS                  TO WS-LS-DATA.

       1600-DETERMINE-EOD.
           IF WS-FUTURE-SW = 'Y'
               MOVE '+00'                     TO WS-HOLD-OUT(1: 3)
           ELSE
               MOVE '-00'                     TO WS-HOLD-OUT(1: 3).
           MOVE WS-LS-OUT(4: 3)               TO WS-HOLD-OUT(4: 3).
           PERFORM 1200-NBR-DAYS.
           IF WS-LS-OUT(1: 3) = 'CDD' OR 'CDF'
               MOVE WS-2-CYMD-X               TO WS-O-CYMD
               PERFORM 0900-CONV-TO-JUL
               MOVE STV1410B-CYDDD            TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 3) = 'YDD' OR 'YDF'
               MOVE WS-2-CYMD-X               TO WS-O-CYMD
               PERFORM 0900-CONV-TO-JUL
               MOVE STV1410B-CYDDD(3: 5)      TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 3) = 'CMD' OR 'CMF'
               MOVE WS-2-CYMD-X               TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 3) = 'MYD' OR 'MYF'
               MOVE WS-2-CYMD-M               TO WS-LS-DATA-1X2
               MOVE WS-2-CYMD-D               TO WS-LS-DATA-3X4
               MOVE WS-2-CYMD-Y               TO WS-LS-DATA-5X6
           ELSE
               MOVE WS-2-CYMD-X(3: 6)         TO WS-LS-DATA.

       1600-DETERMINE-EOM.
           MOVE WS-O-CYMD                     TO WS-2-CYMD.
           IF WS-FUTURE-SW = 'Y'
               PERFORM WS-EOM-NBR TIMES
                   ADD 1 TO WS-2-CYMD-M
                   IF WS-2-CYMD-M > 12
                       MOVE 01                TO WS-2-CYMD-M
                       ADD 1 TO WS-2-CYMD-CY
                   END-IF
               END-PERFORM
           ELSE
               PERFORM WS-EOM-NBR TIMES
                   SUBTRACT 1 FROM WS-2-CYMD-M
                   IF WS-2-CYMD-M = ZERO
                       MOVE 12                TO WS-2-CYMD-M
                       SUBTRACT 1 FROM WS-2-CYMD-CY
                   END-IF
               END-PERFORM
           END-IF.
           MOVE SPACE                         TO WS-STV1295B-FIELDS.
           MOVE WS-2-CYMD-C                   TO STV1295B-CC.
           MOVE WS-2-CYMD-Y                   TO STV1295B-YY.
           MOVE WS-2-CYMD-M                   TO STV1295B-MM.
           MOVE SPACE                         TO STV1295B-DD.
           CALL 'STV1295B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1295B-FIELDS.
           IF STV1295B-YEAR = '****'
               MOVE 1600                      TO WS-ERROR-NBR
           ELSE
           MOVE STV1295B-DD                   TO WS-2-CYMD-D
           IF WS-LS-OUT(1: 4) = 'CDDE' OR 'CDFE'
               MOVE WS-2-CYMD-X               TO WS-O-CYMD
               PERFORM 0900-CONV-TO-JUL
               MOVE STV1410B-CYDDD            TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 4) = 'YDDE' OR 'YDFE'
               MOVE WS-2-CYMD-X               TO WS-O-CYMD
               PERFORM 0900-CONV-TO-JUL
               MOVE STV1410B-CYDDD(3: 5)      TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 4) = 'CMDE' OR 'CMFE'
               MOVE WS-2-CYMD-X               TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 3) = 'MYDE' OR 'MYFE'
               MOVE WS-2-CYMD-M               TO WS-LS-DATA-1X2
               MOVE WS-2-CYMD-D               TO WS-LS-DATA-3X4
               MOVE WS-2-CYMD-Y               TO WS-LS-DATA-5X6
           ELSE
               MOVE WS-2-CYMD-X(3: 6)         TO WS-LS-DATA.

       1700-DETERMINE-EOY.
           MOVE WS-O-CYMD                     TO WS-2-CYMD.
           IF WS-FUTURE-SW = 'Y'
               PERFORM WS-EOM-NBR TIMES
                   ADD 1 TO WS-2-CYMD-CY
               END-PERFORM
           ELSE
               PERFORM WS-EOM-NBR TIMES
                   SUBTRACT 1 FROM WS-2-CYMD-CY
               END-PERFORM
           END-IF.
           MOVE 12                            TO WS-2-CYMD-M.
           MOVE 31                            TO WS-2-CYMD-D.
           IF WS-LS-OUT(1: 5) = 'CDDEY' OR 'CDFEY'
               MOVE WS-2-CYMD-X               TO WS-O-CYMD
               PERFORM 0900-CONV-TO-JUL
               MOVE STV1410B-CYDDD            TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 5) = 'YDDEY' OR 'YDFEY'
               MOVE WS-2-CYMD-X               TO WS-O-CYMD
               PERFORM 0900-CONV-TO-JUL
               MOVE STV1410B-CYDDD(3: 5)      TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 5) = 'CMDEY' OR 'CMFEY'
               MOVE WS-2-CYMD-X               TO WS-LS-DATA
           ELSE
           IF WS-LS-OUT(1: 3) = 'MYDEY' OR 'MYFEY'
               MOVE WS-2-CYMD-M               TO WS-LS-DATA-1X2
               MOVE WS-2-CYMD-D               TO WS-LS-DATA-3X4
               MOVE WS-2-CYMD-Y               TO WS-LS-DATA-5X6
           ELSE
               MOVE WS-2-CYMD-X(3: 6)         TO WS-LS-DATA.

      ****************************************
      *    BIRTHDATE MUST BE PROVIDED AS INPUT.  AS-OF-DATE
      *    IS ALWAYS ASSUMED TO BE CURRENT DATE.
      *+**************************************
       1800-CALC-AGE.
           MOVE SPACE                         TO WS-STV0280B-FIELDS.
           IF WS-LS-DATA2 = SPACE
               MOVE WS-1-CYMD                 TO STV0280B-AS-OF-CYMD
           ELSE
               MOVE WS-LS-DATA2               TO STV0280B-AS-OF-CYMD.
           MOVE WS-O-CYMD                     TO STV0280B-DOB-CYMD.
           CALL 'STV0280B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV0280B-FIELDS.
           IF STV0280B-YEARS = '***'
               MOVE 1800                      TO WS-ERROR-NBR
           ELSE
               MOVE STV0280B-YEARS            TO WS-LS-DATA
               MOVE STV0280B-MONTHS           TO WS-LS-DATA(4: 2)
               MOVE STV0280B-DAYS             TO WS-LS-DATA(6: 2).

       1900-NBR-DAYS.
           MOVE SPACE                         TO STV1420B-DAYS.
           MOVE WS-O-CYMD                     TO STV1420B-PAST-DATE.
           IF WS-LS-DATA2 = SPACE
               MOVE WS-1-CYMD                 TO STV1420B-FUTURE-DATE
           ELSE
               MOVE WS-LS-DATA2               TO STV1420B-FUTURE-DATE.
           CALL 'STV1420B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1420B-FIELDS.
           IF STV1420B-DAYS = '*****'
               MOVE 1900                      TO WS-ERROR-NBR
           ELSE
               MOVE STV1420B-DAYS             TO WS-LS-DATA.

       2000-DETERMINE-SUFFIX.
           MOVE SPACE                         TO WS-STV1295B-FIELDS.
           MOVE WS-O-CYMD                     TO STV1295B-DATE.
           CALL 'STV1295B' USING DFHEIBLK DFHCOMMAREA
                                 WS-STV1295B-FIELDS.
           IF STV1295B-YEAR = '****'
               MOVE 2000                      TO WS-ERROR-NBR
           ELSE
               MOVE STV1295B-MONTH            TO WS-LS-DATA
               MOVE STV1295B-DAY              TO WS-LS-IN
               MOVE STV1295B-YEAR             TO WS-LS-OUT.

       2100-CONVERT-FROM-KEY.
           IF WS-LS-DATA-1X8 NUMERIC
               MOVE WS-LS-DATA-1X8            TO WS-REV-DATE
               SUBTRACT WS-REV-DATE FROM 50000000 GIVING WS-2-CYMD
               MOVE WS-2-CYMD                 TO WS-I-CYMD
           ELSE
               MOVE 2100                      TO WS-ERROR-NBR.

       2100-CONVERT-TO-KEY.
           MOVE WS-O-CYMD                     TO WS-REV-DATE.
           SUBTRACT WS-REV-DATE FROM 50000000 GIVING WS-2-CYMD.
           MOVE WS-2-CYMD                     TO WS-LS-DATA-1X8.

       2200-DETERMINE-QUARTER.
           IF WS-O-CYMD-M = '01' OR '02' OR '03'
               MOVE '1'                       TO WS-LS-DATA
           ELSE IF WS-O-CYMD-M = '04' OR '05' OR '06'
               MOVE '2'                       TO WS-LS-DATA
           ELSE IF WS-O-CYMD-M = '07' OR '08' OR '09'
               MOVE '3'                       TO WS-LS-DATA
           ELSE IF WS-O-CYMD-M = '10' OR '11' OR '12'
               MOVE '4'                       TO WS-LS-DATA.

       4000-RETURN-ERROR.
           MOVE 'ERROR     '                  TO WS-LS-DATA.
           MOVE WS-ERROR-NBR                  TO WS-LS-DATA(7: 4).

       4200-EXIT-PROGRAM.
           MOVE WS-LS-DATA                    TO LS-DATA.
           MOVE WS-LS-DATA2                   TO LS-DATA2.
           MOVE WS-LS-IN                      TO LS-IN.
           MOVE WS-LS-OUT                     TO LS-OUT.
           GOBACK.

      ************************************ END STV1400B

