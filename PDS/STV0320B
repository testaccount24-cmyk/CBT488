
       TITLE '  STV0320B: eight digit time conversion'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    STV0320B.
       AUTHOR.        Jim Iannone.
      ******************************************************************
      *@@@ C3 call program to convert military/standard time
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      ****************************************************************
      * PROGRAM-ID  : STV0320B
      * Project #   : 901030
      * Date written: 10/08/90
      * Programmer  : Jim Iannone
      *
      *               This program will accept either a 12 hr or 24 hr
      *               time parameter and return the other time.
      *               The time is accepted as 'hhmmssam' or 'hhmmsspm'
      *               for standard time or 'hhmmss  ' for military time.
      *               if there is any error found, the original time is
      *               returned unchanged.
      ******************************************************************
      * Project #   : 930115
      * Date changed: 03.01.93
      * Programmer  : P E Bonfiglio
      *               This is a copy of STV0165B compiled with Cobol II.
      ******************************************************************
      * Date changed: 04/07/2006
      * Programmer  : Jim Iannone
      *               Compile with Cobol 3 with DYNAM option
      ******************************************************************
      * Date changed: 11/13/2019
      * Programmer  : Jim Iannone
      *               Expand passed field to include seconds
      ******************************************************************

       ENVIRONMENT DIVISION.
       DATA DIVISION.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING-STORAGE STV0320B start '.

       01  WS-FIELDS.
           05  WS-TIME                               VALUE SPACE.
               10  WS-HH               PIC  XX.
               10  WS-MM               PIC  XX.
               10  WS-SS               PIC  XX.
               10  WS-AP               PIC  XX.
           05  WS-NEW-TIME                           VALUE SPACE.
               10  WS-NEW-HH           PIC  XX.
               10  WS-NEW-MM           PIC  XX.
               10  WS-NEW-SS           PIC  XX.
               10  WS-NEW-AP           PIC  XX.
           05  WS-HH-NUMERIC           PIC  99       VALUE ZERO.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  END STV0320B  '.

       LINKAGE SECTION.

       01  LS-CALC-TIME.
           05  FILLER                  PIC  X(08).

       PROCEDURE DIVISION USING LS-CALC-TIME.

      ****************************************
      *    If 'AM' or 'PM' are found, process as a military
      *    conversion, else process as a standard conversion.
      ****************************************
       0000-START-PROCESS.
           MOVE SPACE                         TO WS-TIME
                                                 WS-NEW-TIME.
           MOVE LS-CALC-TIME                  TO WS-TIME.
           IF WS-HH NOT NUMERIC
               OR WS-MM NOT NUMERIC
               GO TO 0300-END-PROCESS.
           IF WS-HH > '23'
               OR WS-MM > '59'
               GO TO 0300-END-PROCESS.
           IF WS-AP = 'AM' OR 'PM'
               GO TO 0100-CALC-TO-MILITARY.

      *    convert from military to standard

           MOVE WS-SS                         TO WS-NEW-SS.
           MOVE WS-MM                         TO WS-NEW-MM.
           IF WS-HH > '12'
               MOVE WS-HH                     TO WS-HH-NUMERIC
               SUBTRACT 12 FROM WS-HH-NUMERIC
               MOVE WS-HH-NUMERIC             TO WS-NEW-HH
               MOVE 'PM'                      TO WS-NEW-AP
               GO TO 0200-MOVE-TIME.
           MOVE WS-HH                         TO WS-NEW-HH.
           IF WS-HH < '12'
               MOVE 'AM'                      TO WS-NEW-AP.
           IF WS-HH = '12'
               MOVE 'PM'                      TO WS-NEW-AP.
           IF WS-HH = ZERO
               MOVE '12'                      TO WS-NEW-HH.
           GO TO 0200-MOVE-TIME.

      *    convert from standard to military

       0100-CALC-TO-MILITARY.
           IF WS-HH > '12'
               OR WS-HH = ZERO
               GO TO 0300-END-PROCESS.
           MOVE WS-SS                         TO WS-NEW-SS.
           MOVE WS-MM                         TO WS-NEW-MM.
           IF WS-AP = 'PM'
             IF WS-HH = '12'
                 MOVE WS-HH                   TO WS-NEW-HH
             ELSE
                 MOVE WS-HH                   TO WS-HH-NUMERIC
                 ADD 12                       TO WS-HH-NUMERIC
                 MOVE WS-HH-NUMERIC           TO WS-NEW-HH
           ELSE
               MOVE WS-HH                     TO WS-NEW-HH
               IF WS-NEW-HH = '12'
                   MOVE ZERO                  TO WS-NEW-HH.

       0200-MOVE-TIME.
           MOVE WS-NEW-TIME                   TO LS-CALC-TIME.

       0300-END-PROCESS.
           GOBACK.

      ************************************ END STV0320B
