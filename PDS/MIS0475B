
       TITLE '  MIS0475B: limit size of HSM list file'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0475B.
      ******************************************************************
      *@@@ II limit size of HSM list file
      ******************************************************************
      *
      *  I am interested in knowing if/where these programs
      *  are in use.     Email:  jiannon@me.com
      *
      *****************************************************************
      * Date written: 04/09/96
      * Author      : Jim Iannone
      *               loop through the HSM list file and delete
      *               records greater than x days old
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT HSMLIST              ASSIGN TO HSMLIST
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT PARM01               ASSIGN TO PARM01
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT HSMOUT               ASSIGN TO HSMOUT
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  HSMLIST
           RECORDING MODE F.
       01  HSMLIST-RECORD.
           05  HSMLIST-YMD             PIC  X(06).
           05  FILLER REDEFINES HSMLIST-YMD.
               10  HSMLIST-YY          PIC  XX.
               10  HSMLIST-MM          PIC  XX.
               10  HSMLIST-DD          PIC  XX.
           05  FILLER                  PIC  X(125).
           05  HSMLIST-TYPE            PIC  X.

       FD  PARM01
           RECORDING MODE F.
       01  PARM01-RECORD.
           05  FILLER                  PIC  X(80).

       FD  HSMOUT
           RECORDING MODE F.
       01  HSMOUT-RECORD.
           05  HSMOUT-RCD              PIC  X(132).
           05  FILLER REDEFINES HSMOUT-RCD.
               10  FILLER              PIC  X(131).
               10  HSMOUT-QUESTION     PIC  X.

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0475B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-DAY-CNT              PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT1             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT2             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT3             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT4             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT5             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT6             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT7             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT8             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT9             PIC  9(05)    VALUE ZERO.
           05  WS-DEL-CNT0             PIC  9(05)    VALUE ZERO.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.

      *    STV0400B WILL PERFORM DATE AND NBR OF DAYS CONVERSIONS
       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06).
           05  STV0400B-OUT            PIC  X(06).

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-WRITE-RCD-CNT        PIC S9(07)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-HSMLIST-SW       PIC  X        VALUE 'N'.
               88  END-OF-HSMLIST                    VALUE 'Y'.
               88  MORE-HSMLIST-RCDS                 VALUE 'N'.
           05  WS-END-PARM01-SW        PIC  X        VALUE 'N'.
               88  END-OF-PARM01                     VALUE 'Y'.
               88  MORE-PARM01-RCDS                  VALUE 'N'.

       01  WS-DAY-PARMS.
           05  WS-PARM-DAYS            PIC  X(60).
           05  FILLER REDEFINES WS-PARM-DAYS.
               10  FILLER              PIC  X.
               10  WS-DAY-CNT1         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT2         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT3         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT4         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT5         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT6         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT7         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT8         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT9         PIC  9(04).
               10  FILLER              PIC  X.
               10  WS-DAY-CNT0         PIC  9(04).
               10  FILLER              PIC  X(10).

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0475B END  '.

      /
       PROCEDURE DIVISION.

       0100-PROGRAM-ENTRY.
           DISPLAY '      '.
           DISPLAY '**** MIS0475B limit size of HSM list file'.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-HSMLIST.

           DISPLAY '****    Days  Deleted '.
           DISPLAY '**** 1  ' WS-DAY-CNT1 '  ' WS-DEL-CNT1.
           DISPLAY '**** 2  ' WS-DAY-CNT2 '  ' WS-DEL-CNT2.
           DISPLAY '**** 3  ' WS-DAY-CNT3 '  ' WS-DEL-CNT3.
           DISPLAY '**** 4  ' WS-DAY-CNT4 '  ' WS-DEL-CNT4.
           DISPLAY '**** 5  ' WS-DAY-CNT5 '  ' WS-DEL-CNT5.
           DISPLAY '**** 6  ' WS-DAY-CNT6 '  ' WS-DEL-CNT6.
           DISPLAY '**** 7  ' WS-DAY-CNT7 '  ' WS-DEL-CNT7.
           DISPLAY '**** 8  ' WS-DAY-CNT8 '  ' WS-DEL-CNT8.
           DISPLAY '**** 9  ' WS-DAY-CNT9 '  ' WS-DEL-CNT9.
           DISPLAY '**** 0  ' WS-DAY-CNT0 '  ' WS-DEL-CNT0.
           DISPLAY '**** Records in HSMLIST   ' WS-READ-RCD-CNT.
           DISPLAY '**** Records kepT         ' WS-WRITE-RCD-CNT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0475B cond code ' WS-RETURN-CODE
                       ' sent by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0475B EOJ encountered '.
           DISPLAY '      '.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  HSMLIST.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0475B open error on HSMLIST'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT  PARM01.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0475B open error on PARM01'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT HSMOUT.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0475B open error on HSMOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0201                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE HSMOUT.
           CLOSE PARM01.
           CLOSE HSMLIST.

       0400-PROCESS-HSMLIST.
           PERFORM 0500-GET-PARM-DATE.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-READ-HSMLIST THRU
                       0600-READ-HSMLIST-EXIT
                       UNTIL END-OF-HSMLIST.

       0500-GET-PARM-DATE.
           PERFORM 0550-READ-PARM01
               UNTIL END-OF-PARM01.
           IF WS-DAY-CNT1 NOT NUMERIC
               OR WS-DAY-CNT2 NOT NUMERIC
               OR WS-DAY-CNT3 NOT NUMERIC
               OR WS-DAY-CNT4 NOT NUMERIC
               OR WS-DAY-CNT5 NOT NUMERIC
               OR WS-DAY-CNT6 NOT NUMERIC
               OR WS-DAY-CNT7 NOT NUMERIC
               OR WS-DAY-CNT8 NOT NUMERIC
               OR WS-DAY-CNT9 NOT NUMERIC
               OR WS-DAY-CNT0 NOT NUMERIC
               DISPLAY '**** MIS0475B days parm not numeric '
               MOVE +0500                     TO WS-RETURN-CODE.

       0550-READ-PARM01.
           READ PARM01.
           IF WS-STAT = ZERO
               IF PARM01-RECORD(1: 1) = '*'
                   CONTINUE
               ELSE
                   MOVE PARM01-RECORD (1: 60) TO WS-PARM-DAYS
           ELSE
               MOVE 'Y'                       TO WS-END-PARM01-SW.

       0600-READ-HSMLIST.
           READ HSMLIST.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
               PERFORM 0700-SELECT THRU
                       0700-SELECT-EXIT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-HSMLIST-SW
           ELSE
               DISPLAY '**** MIS0475B read error on HSMLIST '
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0600                     TO WS-RETURN-CODE.

       0600-READ-HSMLIST-EXIT. EXIT.

       0700-SELECT.
           IF HSMLIST-TYPE = '!'
               GO TO 0700-KEEP-RECORD.
           IF HSMLIST-YMD NOT NUMERIC
               GO TO 0700-WRITE-QUESTION.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE HSMLIST-YMD                   TO STV0400B-1X8.
           MOVE 'YMD   '                      TO STV0400B-IN.
           MOVE 'DAYS  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           IF STV0400B-DAYS NOT NUMERIC
               GO TO 0700-WRITE-QUESTION.
           MOVE STV0400B-DAYS                 TO WS-DAY-CNT.
           IF HSMLIST-TYPE = '1'
             IF WS-DAY-CNT > WS-DAY-CNT1
                 ADD 1                        TO WS-DEL-CNT1
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '2'
             IF WS-DAY-CNT > WS-DAY-CNT2
                 ADD 1                        TO WS-DEL-CNT2
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '3'
             IF WS-DAY-CNT > WS-DAY-CNT3
                 ADD 1                        TO WS-DEL-CNT3
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '4'
             IF WS-DAY-CNT > WS-DAY-CNT4
                 ADD 1                        TO WS-DEL-CNT4
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '5'
             IF WS-DAY-CNT > WS-DAY-CNT5
                 ADD 1                        TO WS-DEL-CNT5
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '6'
             IF WS-DAY-CNT > WS-DAY-CNT6
                 ADD 1                        TO WS-DEL-CNT6
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '7'
             IF WS-DAY-CNT > WS-DAY-CNT7
                 ADD 1                        TO WS-DEL-CNT7
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '8'
             IF WS-DAY-CNT > WS-DAY-CNT8
                 ADD 1                        TO WS-DEL-CNT8
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '9'
             IF WS-DAY-CNT > WS-DAY-CNT9
                 ADD 1                        TO WS-DEL-CNT9
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE = '0'
             IF WS-DAY-CNT > WS-DAY-CNT0
                 ADD 1                        TO WS-DEL-CNT0
                 GO TO 0700-SELECT-EXIT.
           IF HSMLIST-TYPE NOT NUMERIC
             IF WS-DAY-CNT > WS-DAY-CNT5
                 ADD 1                        TO WS-DEL-CNT5
                 GO TO 0700-SELECT-EXIT.

       0700-KEEP-RECORD.
           MOVE HSMLIST-RECORD                TO HSMOUT-RECORD.
           PERFORM 0800-WRITE-HSMOUT.
           GO TO 0700-SELECT-EXIT.

       0700-WRITE-QUESTION.
           MOVE HSMLIST-RECORD                TO HSMOUT-RECORD.
           MOVE '?'                           TO HSMOUT-QUESTION.
           PERFORM 0800-WRITE-HSMOUT.

       0700-SELECT-EXIT. EXIT.

       0800-WRITE-HSMOUT.
           WRITE HSMOUT-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0475B write error on HSMOUT'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

           ADD 1                              TO WS-WRITE-RCD-CNT.

      ************************************ END MIS0475B
