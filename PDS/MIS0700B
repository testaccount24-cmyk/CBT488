
       TITLE '  MIS0700B: incremental delay process'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0700B.
      **********************************************************CX003B
      *@@@ C3 incremental delay process
      ****************************************************************
      *
      *   I am interested in knowing if/where these programs
      *   are used:       Email:  jiannon@me.com
      *
      ****************************************************************
      * Date written: 03/27/2019
      * Author      : Jim Iannone
      *
      *    Force a gap between job runs
      *
      *    Even though like named jobs stack up in the run queue,
      *      this tool will allow forcing a gap between them, or
      *      between different jobs that pass the same job name to
      *      this program.
      *
      *    first run - determine future time window x seconds ahead.
      *    next run - if within calculated time window, delay until
      *      out of window, then generate another window.  this will
      *      prevent jobs from running within same timeframe.
      *
      *    NOTE:  this program calls delay program "NEWWAIT"
      *           which resides on CBT tape #270 and requires
      *           MACLIB from same tape number when compiling
      *
      ****************************************************************
      * Date changed: mm/dd/yyyy username
      *               comments...
      ****************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT PARM01               ASSIGN TO PARM01
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  PARM01
           RECORDING MODE F.
       01  PARM01-RECORD.
           05  PARM01-DOCU             PIC  X.
           05  FILLER                  PIC  X.
           05  PARM01-JOB              PIC  X(08).
           05  FILLER                  PIC  X.
           05  PARM01-END-CYMD         PIC  X(08).
           05  FILLER                  PIC  X.
           05  PARM01-END-HMS          PIC  X(06).
           05  FILLER                  PIC  X(46).
           05  FILLER                  PIC  X(08).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' Working storage MIS0700B start '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-TALLY                PIC S9(04)    VALUE ZERO COMP.
           05  WS-SEC-N                PIC  9(05)    VALUE ZERO.
           05  WS-DELAY                PIC  9(05)    VALUE ZERO.
           05  WS-DELAY7               PIC  9(07)    VALUE ZERO.
           05  WS-1-TOT-SECS           PIC  9(07)    VALUE ZERO.
           05  WS-2-TOT-SECS           PIC  9(06)    VALUE ZERO.
           05  WS-P-TOT-SECS           PIC  9(07)    VALUE ZERO.
           05  WS-F-TOT-SECS           PIC  9(06)    VALUE ZERO.
           05  WS-SEC-DIFF             PIC  9(05)    VALUE ZERO.
           05  WS-DAYS-N               PIC  9(05)    VALUE ZERO.
           05  WS-DAY-DIFF             PIC  9(07)    VALUE ZERO.
           05  WS-LS-DATA              PIC  X(15)    VALUE SPACE.
           05  FILLER REDEFINES WS-LS-DATA.
               10  WS-LS-SECONDS       PIC  X(05).
               10  WS-LS-JOB           PIC  X(08).
               10  WS-LS-EXTRA         PIC  XX.
           05  WS-1-CYMD               PIC  X(08)    VALUE SPACE.
           05  WS-1-CYMD-N             PIC  9(08)    VALUE ZERO.
           05  WS-2-CYMD-N             PIC  9(08)    VALUE ZERO.
           05  WS-P-CYMD-N             PIC  9(08)    VALUE ZERO.
           05  WS-F-CYMD-N             PIC  9(08)    VALUE ZERO.
           05  WS-1-HMS-N              PIC  9(06)    VALUE ZERO.
           05  FILLER REDEFINES WS-1-HMS-N.
               10  WS-1-HMS-H          PIC  99.
               10  WS-1-HMS-M          PIC  99.
               10  WS-1-HMS-S          PIC  99.
           05  WS-2-HMS-N              PIC  9(06)    VALUE ZERO.
           05  FILLER REDEFINES WS-2-HMS-N.
               10  WS-2-HMS-H          PIC  99.
               10  WS-2-HMS-M          PIC  99.
               10  WS-2-HMS-S          PIC  99.
           05  WS-P-HMS-N              PIC  9(06)    VALUE ZERO.
           05  FILLER REDEFINES WS-P-HMS-N.
               10  WS-P-HMS-H          PIC  99.
               10  WS-P-HMS-M          PIC  99.
               10  WS-P-HMS-S          PIC  99.
           05  WS-F-HMS-N              PIC  9(06)    VALUE ZERO.
           05  FILLER REDEFINES WS-F-HMS-N.
               10  WS-F-HMS-H          PIC  99.
               10  WS-F-HMS-M          PIC  99.
               10  WS-F-HMS-S          PIC  99.
           05  WS-HOLD-HM8             PIC  X(08)    VALUE SPACE.
           05  WS-HOLD-DAYS            PIC  XX       VALUE SPACE.
           05  WS-CALL-STV0400B        PIC  X(08)    VALUE 'STV0400B'.
           05  WS-INTRDR-END           PIC  X(05)    VALUE '/*EOF'.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-REWRITE-PARM01-CNT   PIC S9(07)    VALUE ZERO.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(04)    VALUE ZERO.
           05  WS-SUB2                 PIC S9(04)    VALUE ZERO.
           05  WS-SUB3                 PIC S9(04)    VALUE ZERO.

      *   AMDCWAIT or NEWWAIT
       01 NEWWAIT-DATA.
           05  NEWWAIT-LEN             PIC S9999     VALUE +5 COMP.
           05  NEWWAIT-DELAY           PIC  9(05).

      *    year 2000 compatible date routine
       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)    VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X6        PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-1X8        PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)    VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)    VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)    VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0700B END  '.

       LINKAGE SECTION.

       01  LS-FIELD.
           05  LS-LENGTH               PIC S9(04)    COMP.
           05  LS-DATA                 PIC  X(15).
           05  FILLER REDEFINES LS-DATA.
               10  LS-SECONDS          PIC  X(05).
               10  LS-JOB              PIC  X(08).
               10  LS-EXTRA            PIC  XX.

      /

       PROCEDURE DIVISION USING LS-FIELD.

       0100-PROGRAM-ENTRY.
           MOVE +0000                         TO WS-RETURN-CODE.
           PERFORM 0200-OPEN-FILES.
           DISPLAY '    '.
           DISPLAY '**** MIS0700B incremental delay process'.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-PARM01.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0700B cond code ' WS-RETURN-CODE
                       ' set by program'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           DISPLAY '**** MIS0700B EOJ encountered '.
           DISPLAY '    '.
           STOP RUN.

       0200-OPEN-FILES.
           OPEN I-O    PARM01.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0700B open error on PARM01'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE PARM01.

      *---------------------------------------
      *    X
      *---------------------------------------
       0400-PROCESS-PARM01.

           PERFORM 0500-GET-CURRENT-DATE.

           PERFORM 0600-GET-LINKAGE.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-CALC-NEW-CURR-TIME
               PERFORM 0700-LOAD-PARM01 THRU
                       0700-LOAD-PARM01-EXIT.

           IF WS-RETURN-CODE = ZERO
               PERFORM 1000-PREPARE-DELAY.

       0500-GET-CURRENT-DATE.

           MOVE FUNCTION CURRENT-DATE(1: 8)   TO WS-1-CYMD WS-1-CYMD-N.
           MOVE FUNCTION CURRENT-DATE(9: 6)   TO WS-1-HMS-N.

           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-HMS-N                    TO STV0400B-1X8.
           MOVE 'HMSM  '                      TO STV0400B-IN.
           MOVE 'H:M:SM'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-HOLD-HM8.

       0600-GET-LINKAGE.
           IF LS-LENGTH = ZERO
               DISPLAY '**** Parm length is zero '
               MOVE +0500                     TO WS-RETURN-CODE
           ELSE
           IF LS-LENGTH > LENGTH OF WS-LS-DATA
               DISPLAY '**** Parm length is excessive '
               MOVE +0500                     TO WS-RETURN-CODE
           ELSE
               MOVE LS-DATA(1: LS-LENGTH)     TO WS-LS-DATA.

           IF WS-LS-SECONDS NUMERIC
               MOVE WS-LS-SECONDS             TO WS-SEC-N
               IF WS-SEC-N < 2
                   MOVE 2                     TO WS-SEC-N
               ELSE
               IF WS-SEC-N > 14400
                   MOVE 14400                 TO WS-SEC-N
               ELSE
                   CONTINUE
           ELSE
               MOVE 30                        TO WS-SEC-N.

           DISPLAY '**** Number of seconds ahead      ' WS-SEC-N.
           DISPLAY '**** Current date time         ' WS-1-CYMD
                   '  '  WS-HOLD-HM8.


           IF WS-LS-JOB = SPACE
               MOVE 'MIS0700B'                TO WS-LS-JOB.

           MOVE WS-1-CYMD                     TO WS-2-CYMD-N.

       0600-CALC-NEW-CURR-TIME.
           MOVE ZERO                          TO WS-2-TOT-SECS.
           COMPUTE WS-2-TOT-SECS = (WS-1-HMS-H * 3600)
                                 + (WS-1-HMS-M * 60)
                                 + WS-1-HMS-S.
           ADD WS-SEC-N                       TO WS-2-TOT-SECS.
           IF WS-2-TOT-SECS > 86399
               SUBTRACT 86400 FROM WS-2-TOT-SECS
               PERFORM 0700-ADVANCE-2-DAY.
           IF WS-2-TOT-SECS > 3599
               COMPUTE WS-2-HMS-H = WS-2-TOT-SECS / 3600
               COMPUTE WS-2-TOT-SECS = WS-2-TOT-SECS
                                     - (WS-2-HMS-H * 3600)
           ELSE
               MOVE ZERO                      TO WS-2-HMS-H.
           IF WS-2-TOT-SECS > 59
               COMPUTE WS-2-HMS-M = WS-2-TOT-SECS / 60
               COMPUTE WS-2-TOT-SECS = WS-2-TOT-SECS
                                     - (WS-2-HMS-M * 60)
           ELSE
               MOVE ZERO                      TO WS-2-HMS-M.
           MOVE WS-2-TOT-SECS                 TO WS-2-HMS-S.

      *---------------------------------------
      *    determine PARM01 values
      *---------------------------------------
       0700-LOAD-PARM01.
           READ PARM01.
           IF WS-STAT = 10
               GO TO 0700-LOAD-PARM01-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0700B read error on PARM01'
               DISPLAY '**** File stat = ' WS-STAT
               MOVE +0700                     TO WS-RETURN-CODE
               GO TO 0700-LOAD-PARM01-EXIT.

           IF PARM01-DOCU = '*'
               GO TO 0700-LOAD-PARM01.

           IF PARM01-JOB = WS-LS-JOB
               PERFORM 0700-CHECK-PARM01-VALUES.

           GO TO 0700-LOAD-PARM01.

       0700-LOAD-PARM01-EXIT. EXIT.

       0700-CHECK-PARM01-VALUES.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE PARM01-END-HMS                TO STV0400B-1X8.
           MOVE 'HMSM  '                      TO STV0400B-IN.
           MOVE 'H:M:SM'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-HOLD-HM8.
           DISPLAY '**** Prev parm date time       ' PARM01-END-CYMD
                   '  '  WS-HOLD-HM8.

           IF PARM01-END-CYMD = SPACE
               MOVE WS-2-CYMD-N               TO PARM01-END-CYMD
               MOVE WS-2-HMS-N                TO PARM01-END-HMS
               PERFORM 0800-REWRITE-PARM01
           ELSE
           IF PARM01-END-CYMD NUMERIC
             IF PARM01-END-HMS NUMERIC
                 MOVE PARM01-END-CYMD         TO WS-P-CYMD-N
                 MOVE PARM01-END-HMS          TO WS-P-HMS-N
                 PERFORM 0700-COMPARE-TIME
             ELSE
                 MOVE WS-2-CYMD-N             TO PARM01-END-CYMD
                 MOVE WS-2-HMS-N              TO PARM01-END-HMS
                 PERFORM 0800-REWRITE-PARM01
           ELSE
               MOVE WS-2-CYMD-N               TO PARM01-END-CYMD
               MOVE WS-2-HMS-N                TO PARM01-END-HMS
               PERFORM 0800-REWRITE-PARM01.

       0700-COMPARE-TIME.
           MOVE ZERO                          TO WS-DAY-DIFF.
           IF WS-P-CYMD-N = WS-1-CYMD-N
             IF WS-P-HMS-N < WS-1-HMS-N
                 MOVE WS-2-CYMD-N             TO PARM01-END-CYMD
                 MOVE WS-2-HMS-N              TO PARM01-END-HMS
                 PERFORM 0800-REWRITE-PARM01
                 MOVE ZERO                    TO WS-DELAY
             ELSE
             IF WS-P-HMS-N > WS-1-HMS-N
                 PERFORM 0700-GET-TIME-DIFF
                 PERFORM 0700-CALC-FUTURE-PARM
                 MOVE WS-F-CYMD-N             TO PARM01-END-CYMD
                 MOVE WS-F-HMS-N              TO PARM01-END-HMS
                 PERFORM 0800-REWRITE-PARM01
             ELSE
                 MOVE ZERO                    TO WS-DELAY
           ELSE
           IF WS-P-CYMD-N > WS-1-CYMD-N
               PERFORM 0700-GET-DAY-DIFF
               PERFORM 0700-GET-TIME-DIFF
               PERFORM 0700-CALC-FUTURE-PARM
               MOVE WS-F-CYMD-N               TO PARM01-END-CYMD
               MOVE WS-F-HMS-N                TO PARM01-END-HMS
               PERFORM 0800-REWRITE-PARM01
           ELSE
               MOVE WS-2-CYMD-N               TO PARM01-END-CYMD
               MOVE WS-2-HMS-N                TO PARM01-END-HMS
               PERFORM 0800-REWRITE-PARM01
               MOVE ZERO                      TO WS-DELAY.

       0700-CALC-FUTURE-PARM.
           MOVE WS-P-CYMD-N                   TO WS-F-CYMD-N.
           MOVE ZERO                          TO WS-F-TOT-SECS.
           COMPUTE WS-F-TOT-SECS = (WS-P-HMS-H * 3600)
                                 + (WS-P-HMS-M * 60)
                                 + WS-P-HMS-S.
           ADD WS-SEC-N                       TO WS-F-TOT-SECS.
           IF WS-F-TOT-SECS > 86399
               SUBTRACT 86400 FROM WS-F-TOT-SECS
               PERFORM 0700-ADVANCE-F-DAY.
           IF WS-F-TOT-SECS > 3599
               COMPUTE WS-F-HMS-H = WS-F-TOT-SECS / 3600
               COMPUTE WS-F-TOT-SECS = WS-F-TOT-SECS
                                     - (WS-F-HMS-H * 3600)
           ELSE
               MOVE ZERO                      TO WS-F-HMS-H.
           IF WS-F-TOT-SECS > 59
               COMPUTE WS-F-HMS-M = WS-F-TOT-SECS / 60
               COMPUTE WS-F-TOT-SECS = WS-F-TOT-SECS
                                     - (WS-F-HMS-M * 60)
           ELSE
               MOVE ZERO                      TO WS-F-HMS-M.
           MOVE WS-F-TOT-SECS                 TO WS-F-HMS-S.

       0700-ADVANCE-2-DAY.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-2-CYMD-N                   TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE '+00001'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-2-CYMD-N.

       0700-ADVANCE-F-DAY.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-F-CYMD-N                   TO STV0400B-1X8.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE '+00001'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-F-CYMD-N.

       0700-GET-DAY-DIFF.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-1-CYMD-N                   TO STV0400B-1X8.
           MOVE WS-P-CYMD-N                   TO STV0400B-DATA2.
           MOVE 'CYMD  '                      TO STV0400B-IN.
           MOVE 'DAYS  '                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-DAYS                 TO WS-DAYS-N.
           COMPUTE WS-DAY-DIFF = WS-DAYS-N * 86400.

       0700-GET-TIME-DIFF.
           MOVE ZERO                          TO WS-1-TOT-SECS.
           MOVE ZERO                          TO WS-P-TOT-SECS.
           MOVE ZERO                          TO WS-DELAY.
           COMPUTE WS-1-TOT-SECS = (WS-1-HMS-H * 3600)
                                 + (WS-1-HMS-M * 60)
                                 + WS-1-HMS-S.
           COMPUTE WS-P-TOT-SECS = (WS-P-HMS-H * 3600)
                                 + (WS-P-HMS-M * 60)
                                 + WS-P-HMS-S.
           ADD WS-DAY-DIFF                    TO WS-P-TOT-SECS.
      *    DISPLAY '**** Curr seconds                ' WS-1-TOT-SECS.
      *    DISPLAY '**** Parm seconds                ' WS-P-TOT-SECS.
           COMPUTE WS-DELAY7 = WS-P-TOT-SECS - WS-1-TOT-SECS.
           IF WS-DELAY7 > 86400
               MOVE 86400                     TO WS-DELAY
               DISPLAY '**** Calculated seconds > 86400 '
           ELSE
               MOVE WS-DELAY7                 TO WS-DELAY.
      *        DISPLAY '**** Difference                   ' WS-DELAY.

       0800-REWRITE-PARM01.
           PERFORM 0800-HOLD-PARM-TIME.
           REWRITE PARM01-RECORD.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-REWRITE-PARM01-CNT
           ELSE
               DISPLAY '**** MIS0700B rewrite error on PARM01'
               DISPLAY '**** File stat           ' WS-STAT
               MOVE +0800                     TO WS-RETURN-CODE.

       0800-HOLD-PARM-TIME.
           MOVE PARM01-END-CYMD               TO WS-P-CYMD-N.
           MOVE PARM01-END-HMS                TO WS-P-HMS-N.

       1000-PREPARE-DELAY.
           IF WS-DELAY = ZERO
               DISPLAY '**** No delay required'
               PERFORM 1100-SHOW-NEW-TIME
           ELSE
               DISPLAY '**** Delay seconds                ' WS-DELAY
               PERFORM 1100-SHOW-NEW-TIME
               PERFORM 1000-DELAY.

       1000-DELAY.
           MOVE WS-DELAY                      TO NEWWAIT-DELAY.
           CALL 'NEWWAIT' USING NEWWAIT-DATA.

       1100-SHOW-NEW-TIME.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE WS-P-HMS-N                    TO STV0400B-1X8.
           MOVE 'HMSM  '                      TO STV0400B-IN.
           MOVE 'H:M:SM'                      TO STV0400B-OUT.
           CALL WS-CALL-STV0400B USING WS-STV0400B-FIELDS.
           MOVE STV0400B-1X8                  TO WS-HOLD-HM8.

           DISPLAY '**** New parm date time        ' WS-P-CYMD-N
                   '  '  WS-HOLD-HM8.

      ************************************ END MIS0700B
