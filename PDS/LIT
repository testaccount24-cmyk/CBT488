  ISREDIT MACRO
  CONTROL ASIS
/********************************************************************/
/*                                                                  */
/*  WRITTEN BY TOM BARTHOLD (TBARTHOLD@COMCAST.NET)                 */
/*  CBT tape #735   originally named  LIT                           */
/*                                                                  */
/*    THIS MACRO COMPUTES THE SIZE OF A LITERAL ON THE LINE WHERE   */
/*    THE CURSOR IS                                                 */
/*                                                                  */
/*    THIS IS EASIER IF YOU SET IT UP AS A PFKEY FUNCTION           */
/*                                                                  */
/*    TYPE THE COMMAND 'LIT' IN THE COMMAND LINE AND PLACE THE      */
/*    CURSOR ON A LINE THAT CONTAINS A LITERAL (ENCLOSED WITH       */
/*    SINGLE QUOTES).  PRESS ENTER.                                 */
/*                                                                  */
/*    THIS MACRO WILL DISPLAY THE LENGTH OF THE LITERAL.            */
/*                                                                  */
/********************************************************************/
/*                                                                  */
/*  updated version on CBT #488 by Jim Iannone                      */
/*    show length of two literals if two found on line              */
/*    also handle "and" sign if found within literal                */
/*                                                                  */
/********************************************************************/
  ISREDIT (OLD) = LINE .ZCSR
  ISREDIT (NUM) = LINENUM .ZCSR
/*     WRITE 'Num        = &NUM'             */
  IF &NUM < 2 THEN +
       GOTO SHOWU

  ISREDIT F && .ZCSR .ZCSR 7 72
  IF &LASTCC = 0 THEN DO
       SET &ZEDSMSG = &STR(character '&&' found)
       SET &ZEDLMSG = &STR(character '&&' in literals causes issues +
          with the LIT macro)
       ISPEXEC SETMSG MSG(ISRZ001)
       EXIT CODE(0)
  END

  SET &CUR=1
  SET &CHR=X
  DO WHILE &STR(&CHR) NE &STR(')
     SET &CHR = &SUBSTR(&CUR:&CUR,&STR(&OLD))
     SET &CUR = &CUR + 1
     IF &CUR > 72 THEN +
        DO
            GOTO ENDIT
        END
  END
  SET &BEG = &CUR
/*     WRITE 'BEG pos 1  = &BEG.'            */
  SET &CUR = &CUR + 1
  DO UNTIL &STR(&CHR) = &STR(')
     SET &CHR = &SUBSTR(&CUR:&CUR,&STR(&OLD))
     SET &CUR = &CUR + 1
     IF &CUR > 72 THEN +
        DO
            GOTO ENDIT
        END
  END
  SET &END = &CUR - 1
/*     WRITE 'END pos 1  = &END.'            */
  SET &LEN1 = &END - &BEG
/*     WRITE 'LEN 1      = &LEN1.'           */

  SET &LEN2 = 0
  SET &CHR=X
  DO WHILE &STR(&CHR) NE &STR(')
     SET &CHR = &SUBSTR(&CUR:&CUR,&STR(&OLD))
     SET &CUR = &CUR + 1
     IF &CUR > 72 THEN +
        DO
          GOTO SHOWI
        END
  END
  SET &BEG = &CUR
/*     WRITE 'BEG pos 2  = &BEG.'            */
  SET &CUR = &CUR + 1
  DO UNTIL &STR(&CHR) = &STR(')
     SET &CHR = &SUBSTR(&CUR:&CUR,&STR(&OLD))
     SET &CUR = &CUR + 1
     IF &CUR > 72 THEN +
        DO
          GOTO SHOWI
        END
  END
  SET &END = &CUR - 1
/*     WRITE 'END pos 2  = &END.'            */
  SET &LEN2 = &END - &BEG
/*     WRITE 'LEN 2      = &LEN2.'           */

SHOWI: IF &LEN2 = 0 THEN +
           SET &ZEDLMSG = &STR(Literal length = &LEN1.)
       IF &LEN2 > 0 THEN +
           SET &ZEDLMSG = &STR(Literal length = &LEN1 and &LEN2.)
       ISPEXEC SETMSG MSG(ISRZ001)
       EXIT CODE(0)

ENDIT: SET &ZEDLMSG = &STR(Literal not found)
       ISPEXEC SETMSG MSG(ISRZ001)
       EXIT CODE(0)

SHOWU: SET &ZEDLMSG = &STR(Place cursor on line with ' ' literal)
       ISPEXEC SETMSG MSG(ISRZ001)
       EXIT CODE(0)

